#include "FINR140.CH"
//#Include "PROTHEUS.Ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ FIN140	³ Autor ³ Wagner Xavier 		  ³ Data ³ 01.06.92 				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Fluxo de Caixa Analitico											 				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FIN140(void)															  				³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
USER Function FIN140()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis 														  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL wnrel
LOCAL cString:= "SE1"
LOCAL cDesc1 := STR0001  //"Este programa ir  imprimir o Fluxo de Caixa, informando      "
LOCAL cDesc2 := STR0002  //"ao usu rio quais as suas contas a receber e a pagar dia a    "
LOCAL cDesc3 := STR0003  //"dia e tambem seu dispon¡vel de acordo com os saldos banc rios"
Local aAplicSeh

PRIVATE titulo := OemToAnsi(STR0004)  // "Fluxo de Caixa Analitico"
PRIVATE cabec1
PRIVATE cabec2
PRIVATE tamanho:= "G"
PRIVATE aReturn	:= {OemToAnsi(STR0005), 1,OemToAnsi(STR0006), 2, 2, 1, "",1 }  // "Zebrado"###"Administracao"
PRIVATE nomeprog	:= "FIN140"
PRIVATE aLinha 	:= { }
PRIVATE cPerg		:= "FIN140"
PRIVATE adCompras := {},adVendas:={}
PRIVATE aCompras	:= {},aVendas:={}
PRIVATE cErros 	:= ""
Private nRegs
Private cIpProj := GetMv("MV_IPPRJAP")
Private nLastKey:=0

dbSelectArea("SM0")
nRegs:=SM0->(RecCount())
GetMv("MV_IPPRJAP")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas 								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("FIN140",.F.)
aAplicSeh := Aplicacoes(MV_PAR02)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros								  ³
//³ mv_par01				// Nro de Dias 								  ³
//³ mv_par02				// Moeda 										  ³
//³ mv_par03				// Imprime Por Filial ou Empresa 		  ³
//³ mv_par04				// Considera Pedidos de Vendas			  ³
//³ mv_par05				// Considera Pedidos de Compras			  ³
//³ mv_par06				// Considera Vencidos						  ³
//³ mv_par07				// Considera Comissoes						  ³
//³ mv_par08				// Considera Moedas							  ³
//³ mv_par09				// Do Prefixo       							  ³
//³ mv_par10				// At‚ o Prefixo       						  ³
//³ mv_par11				// Retroativo?         						  ³ 
//³ mv_par12				// Outras Moedas?    				        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabecalhos												  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cabec1 := OemToAnsi(STR0007)  //"Data Ref Prf Numero-PC     Tipo Dt Emiss Cli/For               Nome Cliente/Fornecedor     Historico                Valor Original         a Pagar             a Receber        Disponivel      Observacoes"
cabec2 := " "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT 							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "FIN140"            //Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho,"",.F.)

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString,.F.)

If nLastKey == 27
	Return
Endif

RptStatus({|lEnd| Fa140Imp(@lEnd,wnRel,cString,aAplicSeh)},Titulo)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ FA140Imp ³ Autor ³ Wagner Xavier 		  ³ Data ³ 01.06.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Fluxo de Caixa Analitico											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ FA140IMP(lEnd,wnRel,cString)										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd	  - A‡Æo do Codeblock										  ³±±
±±³			 ³ wnRel   - T¡tulo do relat¢rio 									  ³±±
±±³			 ³ cString - Mensagem													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FA140Imp(lEnd,wnRel,cString,aAplicSeh)

LOCAL cbCont,CbTxt
LOCAL nDias
LOCAL nValor
LOCAL w
LOCAL nTotSaidas
LOCAL dDataVenc
LOCAL lAchou
LOCAL dDataAte
LOCAL dDataImp
LOCAL limite			:= 220
LOCAL lContinua		:= .T.
LOCAL lFirst			:= .T.
LOCAL lFirst1			:= .T.
LOCAL nDispon			:= 0
LOCAL nEl				:= 0
LOCAL aContas			:= {}
LOCAL nDiasInt			:= 0
LOCAL nJurDiario		:= 0
LOCAL nValJuros		:= 0
LOCAL nValGanho		:= 0
LOCAL aValAplic      := 0
LOCAL nTotTitR			:= 0
LOCAL nTotTitP			:= 0
LOCAL nTotGTitR		:= 0
LOCAL nTotGTitP		:= 0
LOCAL nRecHoje			:= 0
LOCAL nPagHoje			:= 0
LOCAL nTotAplic		:= 0
LOCAL nTotResg			:= 0
LOCAL nTotEntradas	:= 0
LOCAL aAplic			:= {}
LOCAL aResgate			:= {}
LOCAL aBancosA			:= {}
LOCAL aBancosR			:= {}
LOCAL nSalDup			:= 0
LOCAL cArqSE1			:= " "
LOCAL cArqSE2			:= " "
LOCAL cArqSE5			:= " "
LOCAL cArqSEG			:= " "
LOCAL cArqSEH			:= " "
LOCAL cCondSE1			:= " "
LOCAL cCondSE2			:= " "
LOCAL cCondSE5			:= " "
LOCAL cCondSEG			:= " "
LOCAL cCondSEH			:= " "
LOCAL nIndexSE3
LOCAL nIndexSE1
LOCAL nIndexSE2
LOCAL nIndexSE5
LOCAL nIndexSEG
LOCAL nIndexSEH
LOCAL nApagVenc		:= 0 
LOCAL nArecVenc		:= 0 
LOCAL nComissoes	:= 0
LOCAL cArqSE3		:= ""
Local dDtSE8        := Ctod("")
Local lSldSE8		:= .F.
Local nIndSE8		:= 1
Local cArqSE8       := ""
Local nRegSa6       := 0
Local nDecs			:= GetMv("MV_CENT"+(IIF(mv_par02 > 1 , STR(mv_par02,1),""))) 
Local nMoedaBco		:=	1
Local nMoeda        	:= mv_par02
Local cAplCotas   	:= GetMv("MV_APLCAL4")
Local nAscan
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cbtxt  := SPACE(10)
cbcont := 0
li 	 := 80
m_pag  := 1

if mv_par03 == 2

	// Contas a receber
	dbSelectArea("SE1")
	cArqSE1 := SubStr(criatrab("",.f.),1,7)+"A"
	IndRegua("SE1",cArqSE1,"DTOS(E1_VENCREA)+E1_NOMCLI+E1_PREFIXO+E1_NUM+E1_PARCELA",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	nIndexSE1 := RetIndex("SE1") + 1
	#IFNDEF TOP
		SE1->(dbSetIndex(cArqSE1+OrdBagExt()))
	#ENDIF
	dbSetOrder( nIndexSE1 ) 
	
	// Contas a pagar
	dbSelectArea("SE2")
	cArqSE2 := SubStr(criatrab("",.f.),1,7)+"B"
	IndRegua("SE2",cArqSE2,"DTOS(E2_VENCREA)+E2_NOMFOR+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	nIndexSE2 := RetIndex("SE2") + 1
	#IFNDEF TOP
		SE2->(dbSetIndex(cArqSE2+OrdBagEXT()))
	#ENDIF
	dbSetOrder( nIndexSE2 )
		
	// Movimentacao bancaria
	dbSelectArea("SE5")
	cArqSE5 := SubStr(criatrab("",.f.),1,7)+"C"
	IndRegua("SE5",cArqSE5,"DTOS(E5_DATA)+E5_BANCO+E5_AGENCIA+E5_CONTA",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	nIndexSE5 := RetIndex("SE5") + 1
	#IFNDEF TOP
		SE5->(dbSetIndex(cArqSE5+OrdBagEXt()))
	#ENDIF

	// Saldo Bancario
	dbSelectArea("SE8")
	cArqSE8 := CriaTrab(,.F.)
	IndRegua("SE8",cArqSE8,"E8_BANCO+E8_AGENCIA+E8_CONTA+DTOS(E8_DTSALAT)",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	nIndSE8 := RetIndex("SE8") + 1
	#IFNDEF TOP
		dbSetIndex(cArqSE8+OrdBagExT())
	#ENDIF
	dbSetOrder(nIndSE8)
	// Aplicacoes e emprestimos
	dbSelectArea("SEG")
	cArqSEG := SubStr(criatrab("",.f.),1,7)+"D"
	IndRegua("SEG",cArqSEG,"DTOS(EG_DATARES)+EG_BANCO+EG_AGENCIA+EG_CONTA",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	SEG->(dbCommit())
	nIndexSEG := RetIndex("SEG") + 1
	#IFNDEF TOP
		SEG->(dbSetIndex(cArqSEG+OrdBagEXT()))
	#ENDIF
	// Aplicacoes e emprestimos
	dbCommit()
	dbSelectArea("SEH")
	cArqSEH := criatrab("",.f.)
	IndRegua("SEH",cArqSEH,"EH_STATUS+EH_APLEMP",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	SEH->(dbCommit())
	nIndexSEH := RetIndex("SEH") + 1
	#IFNDEF TOP
		SEH->(dbSetIndex(cArqSEH+OrdBagExt()))
	#ENDIF
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria indice condicional temporario para analise das comissoes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
if mv_par07 == 1		// Analisa comissoes == Sim

	If Empty(cArqSE3)

		dbSelectArea("SE3")
		cArqSE3 := SubStr(criatrab("",.f.),1,7)+"E"
		cChave := "DTOS(E3_DATA)"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Condicao 1 - Somente filial 			  ³
		//³Condicao 2 - Todas filias (Empresa)	  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP
			if mv_par03 == 1
				// Nao Alterar o ctod(01/01/80)
				cCond := 'DTOS(E3_DATA)<="'+Dtos(ctod("01/01/80","ddmmyy"))+'".AND.E3_FILIAL=="'+xFilial("SE3")+'"'
			else
				cCond := 'DTOS(E3_DATA)<="'+Dtos(ctod("01/01/80","ddmmyy"))+'"'
			endif
		#ELSE
			If mv_par03 == 1
				cCond := "EMPTY(E3_DATA) .and. E3_FILIAL == xfilial('SE3')"
			Else
				cCond := "EMPTY(E3_DATA)"
			Endif
		#ENDIF
		
		IndRegua("SE3",cArqSE3,cChave,,cCond,OemToAnsi(STR0008))  //"Selecionando Registros..."
		dbSelectArea("SE3")
		nIndexSE3		:= RetIndex("SE3") + 1
		#IFNDEF TOP
			SE3->(dbSetIndex(cArqSE3+OrdBagExt()))
		#ENDIF
		dbSetOrder(nIndexSE3)
		SE3->(DbGoTop())
	Else
		dbSelectArea("SE3")
		nIndexSE3		:= RetIndex("SE3") + 1
		#IFNDEF TOP
			SE3->(dbSetIndex(cArqSE3+OrdBagExt()))
		#ENDIF
		dbSetOrder( nIndexSE3 )
	Endif
Endif

Titulo += " em "+GetMV("MV_MOEDA"+AllTrim(Str(mv_par02,2)))
dDataAte := dDataBase+mv_par01

IF mv_par02 != 1
	cSuf := AllTrim(Str(mv_par02,2))
	cabec1 := OemToAnsi(STR0009) //"Data Ref Prf Numero    PC Tipo Dt Emiss Cli/For               Nome Cliente/Fornecedor     Historico                  Valor Original          a Receber            a Pagar          Disponivel         Taxa do Dia    Observacoes"
	dbSelectArea("SM2")
	dbSeek( dDataBase )
EndIF

IF li == 80
	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,NumImp())
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime disponibilidade Bancaria									  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA6")
If ( MV_PAR03 == 1 )
	dbSeek(cFilial)
Else
	dbGotop()
EndIf
cSuf := AllTrim(Str(mv_par02,2,0))
While ! SA6->(Eof()) .and. If(MV_PAR03==1,SA6->A6_FILIAL == cFilial,.T.)
	
	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,NumImp())
		lFirst:=.T.
	EndIF
	
	IF SA6->A6_FLUXCAI == "N"
		SA6->(dbSkip())
		Loop
	Endif
	nRegSa6 := RecNo()
	If lFirst
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se houve movimenta‡„o bancaria no dia da emissao do fluxo³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE5")
		// 1-Verifica filial
		If mv_par03 == 1
			cCondSE5 := "!EOF() .and. E5_DATA == dDataBase .And. E5_FILIAL == xFilial('SE5')"
		Else
			cCondSE5 := "!EOF() .and. E5_DATA == dDataBase"
		Endif
		
		If mv_par03 == 1  // 1-Verifica Filial
			dbSetOrder(1)
			dbSeek(cFilial+DTOS(dDataBase))
		Else
			dbSetOrder(nIndexSE5)
			dbSeek(DTOS(dDataBase))
		Endif
		
		While &cCondSE5
			
			IF mv_par03 == 1 .and. E5_FILIAL != cFilial
				Exit
			EndIF
			
			IF E5_SITUACA == "C" .or. E5_TIPODOC $ "JRşMTşDCşBAşCMşJ2şM2şD2şC2şV2şCXşCPüTL"
				dbSkip( )
				Loop
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe baixas estornadas           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If TemBxCanc(E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_SEQ)
				dbskip()
				loop
			EndIf
			nMoedaTit := BuscaMoeda()
        		
			If cPaisLoc	# "BRA".And.!Empty(SE5->E5_BANCO+SE5->E5_AGENCIA+SE5->E5_CONTA)
				SA6->(DbSetOrder(1))
				SA6->(DbSeek(xFilial()+SE5->E5_BANCO+SE5->E5_AGENCIA+SE5->E5_CONTA))
				nMoedaBco	:=	Max(SA6->A6_MOEDA,1)
			Else
				nMoedaBco	:=	1
			Endif

            If mv_par12 = 2
               If nMoedaBco <> nMoeda   
                  SE5->(dbSkip())
                  Loop
               EndIf
            EndIf
			
			IF E5_TIPODOC == "AP"       //Aplicacoes
				nTotAplic += xMoeda(E5_VALOR,nMoedaBco,nMoeda,E5_DATA,nDecs+1)
				nEl := Ascan(aBancosA,E5_BANCO)
				IF nEl == 0
					AADD(aBancosA,E5_BANCO)
					AADD(aAplic,{E5_BANCO,E5_VALOR,E5_FILIAL})
				Else
					aAplic[nEl][2] += xMoeda(E5_VALOR,nMoedaBco,nMoeda,E5_DATA,nDecs+1)
				EndIf
			ElseIf E5_TIPODOC == "RF"   //Resgates
				nTotResg += xMoeda(E5_VALOR,nMoedaBco,nMoeda,E5_DATA,nDecs+1)
				nEl := Ascan(aBancosR,E5_BANCO)
				IF nEl == 0
					AADD(aBancosR,E5_BANCO)
					AADD(aResgate,{E5_BANCO,E5_VALOR,E5_FILIAL})
				Else
					aResgate[nEl][2]+=xMoeda(E5_VALOR,nMoedaBco,nMoeda,E5_DATA,nDecs+1)
				EndIf
			ElseIf E5_RECPAG == "R"
				nRecHoje += Iif(mv_par02 == 1,xMoeda(E5_VALOR,nMoedaBco,1,E5_DATA,nDecs+1),;
									 xMoeda(E5_VLMOED2,nMoedaTit,nMoeda,E5_DATA,nDecs+1))
			ElseIf E5_RECPAG == "P"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                             
				nPagHoje += Iif(mv_par02 == 1,xMoeda(E5_VALOR,nMoedaBco,1,E5_DATA,nDecs+1),;
									 xMoeda(E5_VLMOED2,nMoedaTit,nMoeda,E5_DATA,nDecs+1))
			EndIf
			
			dbSkip()
			
		End
		
		IF (nPagHoje+nRecHoje+nTotAplic+nTotResg) != 0
			@li,0 PSAY OemToAnsi(STR0010)  //"Movimentacao na data ate a hora da emissao"
			nTotEntradas := nTotResg + nRecHoje
			nTotSaidas	 := nTotAplic + nPagHoje
			li++
			@li,	0 PSAY OemToAnsi(STR0011)+Replicate(".",109)  //"Aplicacoes "
			@li,141 PSAY IIF(mv_par02 == 1,nTotAplic,nTotAplic / &('SM2->M2_MOEDA'+cSuf))  PicTure tm(IIF(mv_par02==1,nTotAplic,nTotAplic/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			IF nTotAplic>0
				For w := 1 To Len(aBancosA)
					li++
					@li,  4 PSAY aAplic[w][1]
					dbSelectArea("SA6")
					If ( xFilial("SA6") == Space(2) )
						dbSeek(cFilial+aAplic[w][1])
					Else
						dbSeek(aAplic[w,3]+aAplic[w][1])
					EndIf
					@li,  9 PSAY AllTrim(A6_NREDUZ)+Replicate(".",90-Len(AllTrim(A6_NREDUZ)))
					@li,123 PSAY IIF(mv_par02==1,aAplic[w][2],aAplic[w][2]/&('SM2->M2_MOEDA'+cSuf))	PicTure tm(IIF(mv_par02==1,aAplic[w][2],aAplic[w][2]/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				Next w
			EndIF
			li++
			@li,	0 PSAY OemToAnsi(STR0012)+Replicate(".",111)  //  "Resgates "
			@li,160 PSAY IIF(mv_par02==1,nTotResg,nTotResg/&('SM2->M2_MOEDA'+cSuf))	 PicTure tm(IIF(mv_par02==1,nTotResg,nTotResg/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			IF nTotResg>0
				For w:=1 To Len(aBancosR)
					li++
					@li,  4 PSAY aResgate[w][1]
					dbSelectArea("SA6")
					If ( xFilial("SA6") == Space(2) )
						dbSeek(cFilial+aResgate[w][1])
					Else
						dbSeek(aResgate[w,3]+aResgate[w][1])
					EndIf
					@li,  9 PSAY AllTrim(A6_NREDUZ)+Replicate(".",90-Len(AllTrim(A6_NREDUZ)))
					@li,123 PSAY IIF(mv_par02==1,aResgate[w][2],aResgate[w][2]/&('SM2->M2_MOEDA'+cSuf))  PicTure tm(IIF(mv_par02==1,aResgate[w][2],aResgate[w][2]/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				Next w
			EndIF
			li++
			@li,	0 PSAY OemToAnsi(STR0013)+Replicate(".",106)  //"Outras Saidas "
			@li,141 PSAY IIF(mv_par02==1,nPagHoje,nPagHoje/&('SM2->M2_MOEDA'+cSuf))  PicTure tm(IIF(mv_par02==1,nPagHoje,nPagHoje/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			li++
			@li,	0 PSAY OemToAnsi(STR0014)+Replicate(".",104)  //"Outras Entradas "
			@li,160 PSAY IIF(mv_par02==1,nRecHoje,nRecHoje/&('SM2->M2_MOEDA'+cSuf))  PicTure tm(IIF(mv_par02==1,nRecHoje,nRecHoje/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			li+=2
			@li,	0 PSAY OemToAnsi(STR0015)+Replicate(".",90)  //"Totais ate a hora da emissao "
			@li,141 PSAY IIF(mv_par02==1,nTotSaidas,nTotSaidas/&('SM2->M2_MOEDA'+cSuf))	PicTure tm(IIF(mv_par02==1,nTotSaidas,nTotSaidas/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			@li,160 PSAY IIF(mv_par02==1,nTotEntradas,nTotEntradas/&('SM2->M2_MOEDA'+cSuf))  PicTure tm(IIF(mv_par02==1,nTotEntradas,nTotEntradas/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			li++
			@li,0 PSAY Replicate("-",220)
			li+=2
		EndIF
	EndIF
	
	dbSelectArea("SA6")
	dbGoTo(nRegSa6)
	dbSelectArea("SE8")
	dbSetOrder( nIndSE8 )
	If !(dbSeek(If(MV_PAR03==1,xFilial(),"")+SA6->A6_COD+SA6->A6_AGENCIA+SA6->A6_NUMCON+DtoS(dDataBase),.t.))
		dbSkip( -1 )
		dDtSE8  := SE8->E8_DTSALAT
		lSldSE8 := .F.
		While (  !Bof() .And. If(MV_PAR03==1,xFilial()==SE8->E8_FILIAL,.T.) .And.;
					SA6->A6_COD+SA6->A6_AGENCIA+SA6->A6_NUMCON == ;
					SE8->E8_BANCO+SE8->E8_AGENCIA+SE8->E8_CONTA .And.;
					SE8->E8_DTSALAT == dDtSE8 )
				dbSkip(-1)	
				lSldSE8 := .T.
		EndDo
		If ( lSldSE8 )
			dbSkip()
		EndIf
	EndIf
	If ( !Eof() .And. If(mv_par03==1,xFilial("SE8")==SE8->E8_FILIAL,.T.) .And.;
		SA6->A6_COD+SA6->A6_AGENCIA+SA6->A6_NUMCON == ;
	  	SE8->E8_BANCO+SE8->E8_AGENCIA+SE8->E8_CONTA .And.;
	   SE8->E8_DTSALAT <= dDataBase )
		nValor:=xMoeda(SE8->E8_SALATUA,If(cPaisLoc=="BRA",1,Max(SA6->A6_MOEDA,1)),mv_par02)
	Else
		nValor := 0
	EndIf
	nLimCred := xMoeda(SA6->A6_LIMCRED,If(cPaisLoc=="BRA",1,Max(SA6->A6_MOEDA,1)),mv_par02)
	If nValor != 0
		If lFirst
			@ li,0 PSAY OemToAnsi(STR0016)  //"Disponibilidade imediata"
			lFirst := .f.
		EndIf
		li++
		@li,	0 PSAY SA6->A6_NREDUZ + "  (" + SA6->A6_COD + ")" +;
			Space(6)+"  AG: " + SA6->A6_AGENCIA + "  C/C: " +;
			SA6->A6_NUMCON
		@li,179 PSAY nValor	Picture tm(nValor,16,nDecs)
		li++
		@li,0 PSAY OemToAnsi(STR0027)+Replicate(".",134)  //"Limite de Credito: "
		@li,179 PSAY nLimCred Picture tm(nLimCred,16,nDecs)
		nDispon += (nValor+nLimCred)
	Endif
	dbSelectArea("SA6")
	dbGoTo(nRegSa6)
	SA6->(dbSkip())
	lFirst := .f.
EndDO

IF (nRegSa6 != 0)
	li += 2
	@li, 0 PSAY OemToAnsi(STR0017) +Replicate(".",135)  //"Total Disponivel "
	@li,179 PSAY nDispon	PicTure tm(nDispon,16,nDecs)
EndIF

IF !lFirst
	li++
EndIF

lFirst := .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma Titulos vencidos - a receber e a pagar						 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par06 == 1	 // Considera titulos em atraso
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Soma Contas a pagar vencida pela data de Vencimento			  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	IF TcSrvType() != "AS/400" 
		cOrdSE2 := SqlOrder(SE2->(Indexkey()))
		cRecPagAnt := FormatIn( MVPAGANT, "/" )
		cQuery := "SELECT *"
		cQuery += " FROM " + RetSqlName("SE2")
		If mv_par03 == 1
			cQuery += " WHERE E2_FILIAL = '" + xFilial("SE2") + "'"
		Else
			cQuery += " WHERE E2_FILIAL between ' ' AND 'zz'"
		Endif
		cQuery += " AND E2_PREFIXO between '" + mv_par09 + "' AND '"+mv_par10 + "'"
		cQuery += " AND E2_MOEDA IN " + FormatIn(mv_par08,,1)
		cQuery += " AND E2_VENCREA <= '" + Dtos(dDataBase-1) +"'"
		cQuery += " AND E2_TIPO NOT IN " + cRecPagAnt
		If mv_par11 == 2
			cQuery += " AND E2_SALDO > 0 "
		Endif
		If mv_par12 == 2
			cQuery += " AND E2_MOEDA = " + Str(nMoeda,2)
		Endif
		cQuery += " AND E2_FLUXO <> 'N'"
		cQuery += " AND E2_EMIS1 <= '"+Dtos(dDataBase) +"'"
		cQuery += " AND D_E_L_E_T_ = ' '"
		cQuery += " ORDER BY " + cOrdSE2
		
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"E2VENC",.T.,.T.)
		aEval(SE2->(DbStruct()), {|e| If(e[2]!= "C", TCSetField("E2VENC", e[1], e[2],e[3],e[4]),Nil)})
		SetRegua(RecCount())
		While ! E2VENC->(EOF())
			IncRegua()
			If E2VENC->E2_BAIXA > dDataBase .And. mv_par11 == 1
				nSaldo:=SaldoTit(E2VENC->E2_PREFIXO,E2VENC->E2_NUM,E2VENC->E2_PARCELA,E2VENC->E2_TIPO,E2VENC->E2_NATUREZA,"P",E2VENC->E2_FORNECE,mv_par02,E2VENC->E2_VENCREA,dDataBase,E2VENC->E2_LOJA)
			Else
				nSaldo:=xMoeda((E2VENC->E2_SALDO+E2VENC->E2_SDACRES-E2VENC->E2_SDDECRE),E2VENC->E2_MOEDA,mv_par02,E2VENC->E2_VENCREA,nDecs+1)
			EndIf

			If E2VENC->E2_TIPO $ MVABATIM .or. E2VENC->E2_TIPO $ MV_CPNEG
				nApagVenc -= nSaldo
			Else
				nApagVenc += nSaldo
			Endif
			E2VENC->(dbSkip())
		Enddo
		dbCloseArea()
		dbSelectArea("SE2")
	Else
#ENDIF
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma Contas a pagar vencida pela data de Vencimento			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dDataVenc := dDataBase-1
		
		dbSelectArea("SE2")
		If mv_par03 == 1
			If lFirst
				RetIndex("SE2")
			EndIf
			dbSelectArea("SE2")
			SE2->(dbSetOrder(3))
			SE2->(dbSeek(cFilial+DTOS(CTOD("01/01/60","ddmmyy")) ,.t.))
		Else
			dbGoTop()
		EndIf
		
		While ! SE2->(EOF()) .And. SE2->E2_VENCREA <= dDataVenc
			
			IF mv_par03 == 1 .and. SE2->E2_FILIAL != cFilial
				Exit
			EndIf
			
			If SE2->E2_TIPO $ MVPAGANT
				SE2->(dbSkip())
				Loop
			EndIf
			
			If ! ( AllTrim(Str(SE2->E2_MOEDA,2)) $ mv_par08 )
				dbSkip()
				Loop
			EndIf
			
			If SE2->E2_SALDO == 0 .and. IIf(mv_par11==1,SE2->E2_BAIXA <= dDataBase,.T.)
				SE2->(dbSkip())
				Loop
			EndIf
			
			IF SE2->E2_PREFIXO < mv_par09 .or. SE2->E2_PREFIXO > mv_par10  //Do Prefixo ao Prefixo
				SE2->(dbSkip())
				Loop
			EndIF
			
			If SE2->E2_FLUXO == "N" .or. SE2->E2_EMIS1 > dDataBase
				dbSkip()
				Loop
			EndIf
			
	        If mv_par12 = 2
	           If SE2->E2_MOEDA != nMoeda   
	              SE2->(dbSkip())
	              Loop
	           EndIf
	        EndIf
		
			If SE2->E2_BAIXA > dDataBase .And. mv_par11 == 1
				nSaldo:=SaldoTit(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_TIPO,SE2->E2_NATUREZA,"P",SE2->E2_FORNECE,mv_par02,SE2->E2_VENCREA,dDataBase,SE2->E2_LOJA)
			Else
				nSaldo:=xMoeda((SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE),SE2->E2_MOEDA,mv_par02,SE2->E2_VENCREA,nDecs+1)
			EndIf
			
			If SE2->E2_TIPO $ MVABATIM+"/"+MV_CPNEG
				nApagVenc -= nSaldo
			Else
				nApagVenc += nSaldo
			EndIf
			SE2->(dbSkip())
		End
#IFDEF TOP
	Endif
#ENDIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma Contas a receber vencida pela data de Vencimento		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	IF TcSrvType() != "AS/400" 
		cRecPagAnt := FormatIn( MVRECANT + "/" + MVIRF, "/" )
		cOrdSE1 := SqlOrder(SE1->(Indexkey()))
		cQuery := "SELECT * "
		cQuery += " FROM " + RetSqlName("SE1")
		If mv_par03 == 1
			cQuery += " WHERE E1_FILIAL = '" + xFilial("SE1") + "'"
		Else
			cQuery += " WHERE E1_FILIAL between ' ' AND 'zz'"
		Endif
		cQuery += " AND E1_PREFIXO between '" + mv_par09 + "' AND '"+mv_par10 + "'"
		cQuery += " AND E1_MOEDA IN " + FormatIn(mv_par08,,1)
		cQuery += " AND E1_VENCREA <= '" + Dtos(dDataBase-1) +"'"
		cQuery += " AND E1_TIPO NOT IN " + cRecPagAnt
		cQuery += " AND E1_SITUACA NOT IN ('2','7')"
		If mv_par11 == 2
			cQuery += " AND E1_SALDO > 0 "
		Endif
		If mv_par12 == 2
			cQuery += " AND E1_MOEDA = " + Str(nMoeda,2)
		Endif
		cQuery += " AND E1_FLUXO <> 'N'"
		cQuery += " AND E1_EMISSAO <= '"+Dtos(dDataBase) +"'"
		cQuery += " AND D_E_L_E_T_ = ' '"
		cQuery += " ORDER BY " + cOrdSE1
		
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"E1VENC",.T.,.T.)
		aEval(SE1->(DbStruct()), {|e| If(e[2]!= "C", TCSetField("E1VENC", e[1], e[2],e[3],e[4]),Nil)})
		SetRegua(RecCount())	
		While ! E1VENC->(EOF())
			IncRegua()
			If E1VENC->E1_BAIXA > dDataBase .And. mv_par11 == 1
				nSaldo:=SaldoTit(E1VENC->E1_PREFIXO,E1VENC->E1_NUM,E1VENC->E1_PARCELA,E1VENC->E1_TIPO,E1VENC->E1_NATUREZA,"R",E1VENC->E1_CLIENTE,mv_par02,E1VENC->E1_VENCREA,dDataBase,E1VENC->E1_LOJA)
			Else
				nSaldo:=xMoeda((E1VENC->E1_SALDO+E1VENC->E1_SDACRES-E1VENC->E1_SDDECRE),E1VENC->E1_MOEDA,mv_par02,E1VENC->E1_VENCREA,nDecs+1)
			End
			IF E1VENC->E1_TIPO $ MVABATIM+"/"+MV_CRNEG
				nArecVenc -= nSaldo
			Else
				nArecVenc += nSaldo
			EndIf
			E1VENC->(dbSkip())
		Enddo
		dbCloseArea()
		dbSelectArea("SE1")
	Else	
#ENDIF
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma Contas a receber vencida pela data de Vencimento			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE1")
		If mv_par03 == 1
			If lFirst
				RetIndex("SE1")
			EndIf
			dbSelectArea("SE1")
			SE1->(dbSetOrder(7))
			SE1->(dbseek(cFilial+DTOS(CTOD("01/01/60","ddmmyy")),.T.))
		Else
			dbGoTop()
		EndIf
		
		While ! SE1->(EOF()) .and. SE1->E1_VENCREA <= dDataVenc
			
			IF mv_par03 == 1 .and. cFilial != SE1->E1_FILIAL
				Exit
			EndIf
			
			If SE1->E1_TIPO $ MVRECANT
				SE1->(dbSkip())
				Loop
			EndIf
			
			IF SE1->E1_PREFIXO < mv_par09 .or. SE1->E1_PREFIXO > mv_par10  //Do Prefixo ao Prefixo
				SE1->(dbSkip())
				Loop
			EndIF
			
			If ! ( AllTrim(Str(SE1->E1_MOEDA,2)) $ mv_par08 )
				dbSkip( )
				Loop
			EndIf
			
			If SE1->E1_FLUXO == "N" .or. SE1->E1_EMISSAO > dDatabase
				dbSkip()
				Loop
			EndIf
			
			IF SE1->E1_SALDO == 0 .AND. IIF(mv_par11 == 1,SE1->E1_BAIXA <= dDataBase,.T.)
				SE1->(dbSkip())
				Loop
			EndIf
		
	        If mv_par12 = 2
	           If SE1->E1_MOEDA != nMoeda   
	              SE1->(dbSkip())
	              Loop
	           EndIf
	        EndIf
			
			If SE1->E1_BAIXA > dDataBase .And. mv_par11 == 1
				nSaldo:=SaldoTit(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,SE1->E1_NATUREZA,"R",SE1->E1_CLIENTE,mv_par02,SE1->E1_VENCREA,dDataBase,SE1->E1_LOJA)
			Else
				nSaldo:=xMoeda((SE1->E1_SALDO+SE1->E1_SDACRES-SE1->E1_SDDECRE),SE1->E1_MOEDA,mv_par02,SE1->E1_VENCREA,nDecs+1)
			End
			
			IF SE1->E1_SITUACA != "2" .And. SE1->E1_SITUACA != "7"
				IF SE1->E1_TIPO $ MVABATIM+"/"+MV_CRNEG
					nArecVenc -= nSaldo
				Else
					nArecVenc += nSaldo
				EndIf
			EndIf
			
			SE1->(dbSkip())
		End
#IFDEF TOP
	Endif
#ENDIF		
	If nArecVenc # 0 .or. nApagVenc # 0
		nDispon += nArecVenc
		nDispon -= nApagVenc
		li++
		@ li,000 PSAY OemToAnsi(STR0018)  //"Total de Titulos Vencidos"
		@ li,141 PSAY nApagVenc Picture tm(nApagVenc,16,nDecs)
		@ li,160 PSAY nArecVenc Picture tm(nArecVenc,16,nDecs)
		@ li,179 PSAY nDispon   Picture tm(nDispon,16,nDecs)
		li ++
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma as Comissoes. Os criterios de selecao dos registros foram³
//³determinados no instante da criacao do indice condicional.	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
if mv_par07 == 1
	DbSelectArea("SE3")
	dbGoTop()
	While !Eof() .and. SE3->E3_DATA <= dDataBase
		nComissoes += xMoeda(SE3->E3_COMIS,1,mv_par02)
		dbSkip()
	End
	If nComissoes # 0
		li ++
		nDispon -= nComissoes
		@ li,000 PSAY OemToAnsi(STR0019)  //"Total de Comissoes A Pagar"
		@ li,141 PSAY nComissoes Picture tm(nComissoes,16,nDecs)
		@ li,179 PSAY nDispon 	Picture tm(nDispon,16,nDecs)
		li++
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Acessa titulos iniciais												  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nDias := (dDataAte-dDataBase)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle da Regua 														  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(nDias)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica Pedidos de Compra e de Venda								  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par04 == 1
	fc020Venda( mv_par08 )
Endif

If mv_par05 == 1
	fc020Compra(,,,mv_par02)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma Contas a Receber pela data de Vencimento					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For j := 1 To nDias
	
	dDataVenc := dDataBase+j-1
	
	lAchou := .F.
	dbSelectArea("SE1")
	
	If mv_par03 == 1
		dbSetOrder(7)
		dbseek(cFilial+DTOS(dDataVenc))
	Else
		dbSetOrder( nIndexSE1 )
		dbSeek(DTOS(dDataVenc))
	Endif
	
	IncRegua()
	
	While !Eof() .And. SE1->E1_VENCREA == dDataVenc
		
		IF mv_par03 == 1 .and. SE1->E1_FILIAL != cFilial
			Exit
		EndIF
		
		If SE1->E1_TIPO $ MVRECANT
			dbSkip()
			Loop
		Endif
		
		IF SE1->E1_PREFIXO < mv_par09 .or. SE1->E1_PREFIXO > mv_par10  //Do Prefixo ao Prefixo
			SE1->(dbSkip())
			Loop
		EndIF
		
		If ! ( AllTrim(Str(SE1->E1_MOEDA,2)) $ mv_par08 )
			dbSkip( )
			Loop
		EndIf
		
		If SE1->E1_FLUXO == "N" .or. SE1->E1_EMISSAO > dDatabase
			dbSkip()
			Loop
		EndIf
		
		IF ( SE1->E1_SALDO = 0 .and. IIF( mv_par11 == 1, SE1->E1_BAIXA <= dDataBase, .T. )  ) .or. SE1->E1_SITUACA $ "27"
			dbSkip()
			Loop
		EndIF
	
        If mv_par12 = 2
           If SE1->E1_MOEDA != nMoeda   
              SE1->(dbSkip())
              Loop
           EndIf
        EndIf

		If SE1->E1_BAIXA > dDataBase .And. mv_par11 == 1
			nSaldup:=SaldoTit(SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,SE1->E1_NATUREZA,"R",SE1->E1_CLIENTE,mv_par02,SE1->E1_VENCREA,dDataBase,SE1->E1_LOJA)
		Else
			nSaldup:=xMoeda((SE1->E1_SALDO+SE1->E1_SDACRES-SE1->E1_SDDECRE),SE1->E1_MOEDA,mv_par02,SE1->E1_VENCREA,nDecs+1)
		End
		
		dbSelectArea("SE1")
		
		If mv_par03 == 1
			dbSetOrder(7)
		Endif
		
		IF SE1->E1_TIPO $ MVABATIM+"/"+MV_CRNEG
			nDispon-=nSalDup
		Else
			nDispon+=nSalDup
		EndIf
		
		IF li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,NumImp())
		EndIf
		
		li++
		IF !lAchou
			@li, 0 PSAY SE1->E1_VENCREA
		EndIf
		
		@li, 11 PSAY SE1->E1_PREFIXO
		@li, 15 PSAY SE1->E1_NUM+(IIF(!EMPTY(SE1->E1_PARCELA),"-",""))+SE1->E1_PARCELA
		@li, 30 PSAY SE1->E1_TIPO
		@li, 34 PSAY SE1->E1_EMISSAO
		@li, 45 PSAY SE1->E1_CLIENTE
		@li, 68 PSAY SubStr(SE1->E1_NOMCLI,1,25)
		@li, 96 PSAY SubStr(SE1->E1_HIST,1,25)
		@li,123 PSAY xMoeda(SE1->E1_VALOR+SE1->E1_ACRESC-SE1->E1_DECRESC,SE1->E1_MOEDA,mv_par02,dDataVenc)  Picture PesqPict("SE1","E1_VALOR",16,mv_par02)
		@li,160 PSAY nSalDup	Picture tm(nSalDup,16,nDecs)
		@li,179 PSAY nDispon	Picture tm(nDispon,16,nDecs)
		IF mv_par02 != 1 .and. !lAchou
			@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
			dDataImp:=dDataVenc
		EndIf
		lAchou:=.t.
		dbSelectArea("SE1")
		IF SE1->E1_TIPO $ MVABATIM+"/"+MV_CRNEG
			nTotTitR -= nSalDup
		Else
			nTotTitR += nSalDup
		End
		dbSkip()
	EndDO
If cIpProj =="S"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe aplica‡„o a ser resgatada no dia				³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SEG")
	If mv_par03 == 1
		dbSetOrder(2)
		dbSeek(cFilial+Dtos(dDataVenc))
	Else
		dbSetOrder(nIndexSEG)
		dbSeek(Dtos(dDataVenc))
	Endif
	
	lAchou :=.f.
	While !Eof() .And. EG_DATARES == dDataVenc
		
		IF mv_par03 == 1 .and. EG_FILIAL != cFilial
			Exit
		End
		
		If SEG->EG_TIPO == "EMP"
			dbSkip( )
			Loop
		End
		nDiasInt	:= EG_DATARES-EG_DATA	//Dias de Intervalo
		nJurDiario:= EG_TAXA/nDiasInt	  //Juros Diario
		nValJuros := EG_VALOR+((EG_VALOR*nJurDiario)/100)*nDiasInt
		nValGanho := nValJuros-EG_VALOR
		nValJuros -= nValGanho*EG_IMPOSTO/100 //Impostos
		IF mv_par02 == 1
			nDispon += nValJuros
		Else
			dbSelectArea("SM2")
			dbSeek( dDataVenc )
			nDispon += nValJuros/&('SM2->M2_MOEDA'+cSuf)
		EndIF
		dbSelectArea("SEG")
		li++
			IF !lAchou
				@li, 0 PSAY EG_DATARES
			EndIF

			@li,011 PSAY OemToAnsi(STR0021)+ EG_TIPO + " - " + EG_BANCO  //"Projecao de Resgate de Aplicacao "
			@li,123 PSAY IIF(mv_par02==1,EG_VALOR,EG_VALOR/&('SM2->M2_MOEDA'+cSuf)) Picture tm(IIF(mv_par02==1,EG_VALOR,EG_VALOR/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			@li,160 PSAY IIF(mv_par02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))	  Picture tm(IIF(mv_par02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
			@li,179 PSAY nDispon		Picture tm(nDispon, 16,nDecs)

			IF mv_par02!=1 .and. !lAchou
				dbSelectArea("SM2")
				dbSeek(dDataVenc)
				@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
			EndIF
			lAchou:=.T.
		dbSelectArea("SEG")
		nTotTitR	+= IIF(mv_par02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))
		dbSkip()
	EndDO
	dbSelectArea("SEH")
	If mv_par03 == 1
		dbSetOrder(2)
		dbSeek(xFilial("SEH")+"A")
	Else
		dbSetOrder(nIndexSEH)
		dbSeek("A")
	Endif

	lAchou := .f.
	While ( !Eof() .And.If(MV_PAR03==1,SEH->EH_FILIAL==xFilial("SEH"),.T.) .And.;
				SEH->EH_STATUS == "A" )
		If ( SEH->EH_APLEMP != "EMP" )
			If ( Empty(SEH->EH_DATARES) .And. J==1 ) .Or.;
				( SEH->EH_DATARES == dDataVenc )
				nValJuros := SEH->EH_SALDO
				If !SEH->EH_TIPO $ cAplCotas
					aValAplic := Fa171Calc(	dDataVenc,,,,,,,, mv_par03 = 1)
					nValJuros += aValAplic[5]-aValAplic[2]-aValAplic[3]-aValAplic[4]
				Else
					aValAplic := {0,0,0,0,0,0}
					nAscan := Ascan(aAplicSeh, {|e|	e[1] == SEH->EH_CONTRAT .And.;
													 		   e[2] == SEH->EH_BCOCONT .And.;
															   e[3] == SEH->EH_AGECONT})
					If nAscan > 0																	   
						aValAplic :=	Fa171Calc(dDataVenc,SEH->EH_SLDCOTA,,,,SE9->E9_VLRCOTA,aAplicSeh[nAscan][6],(SEH->EH_SLDCOTA * aAplicSeh[nAscan][6]))
					Endif	
					nValJuros := xMoeda(aValAplic[1]-(aValAplic[2]+aValAplic[3]+aValAplic[4]),1,mv_par02)
				Endif
			Else
				nValJuros := 0
			EndIf
			If ( nValJuros != 0 )
				IF MV_PAR02 == 1
					nDispon += (nValJuros)
				Else
					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek( dDataVenc )
					nDispon += (nValJuros)/&('SM2->M2_MOEDA'+cSuf)
				EndIf
				li++
				IF !lAchou
					@li, 0 PSAY SEH->EH_DATARES
				EndIF
				@li,011 PSAY OemToAnsi(STR0021)+SEH->EH_TIPO+" - "+SEH->EH_BANCO  //"Projecao de Resgate de Aplicacao "
				@li,123 PSAY If(MV_PAR02==1,SEH->EH_SALDO,SEH->EH_SALDO/&('SM2->M2_MOEDA'+cSuf)) PICTURE TM(If(MV_PAR02==1,SEH->EH_SALDO,SEH->EH_SALDO/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				@li,160 PSAY If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))	  PICTURE TM(If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				@li,179 PSAY nDispon		Picture tm(nDispon, 16,nDecs)
				IF ( MV_PAR02 != 1 .And. !lAchou )
					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek(dDataVenc)
					@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
				EndIf
			EndIf
			lAchou:=.T.
			nTotTitR	+= If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))
		EndIf
		dbSelectArea("SEH")
		dbSkip()
	EndDo
Endif
	dbSelectArea("SE2")
	If mv_par03 == 1
		If lFirst
			RetIndex("SE2")
		End
		SE2->(dbSetOrder(3))
		SE2->(dbSeek(cFilial+DTOS(dDataVenc) ,.t.))
	Else
		dbSetOrder( nIndexSE2 )
		SE2->(dbSeek(DTOS(dDataVenc)),.t.)
	End
	
	lAchou := .f.
	While !Eof() .And. E2_VENCREA == dDataVenc
		
		IF mv_par03 == 1 .and. E2_FILIAL != cFilial
			Exit
		EndIF
		
		If SE2->E2_SALDO == 0 .and. IIf(mv_par11==1,SE2->E2_BAIXA <= dDataBase,.t.)
			SE2->(dbSkip())
			Loop
		End
		
        If mv_par12 = 2
           If SE2->E2_MOEDA != nMoeda   
              SE2->(dbSkip())
              Loop
           EndIf
        EndIf
		
		If SE2->E2_BAIXA > dDataBase .And. mv_par11 == 1
			nSaldup:=SaldoTit(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_TIPO,SE2->E2_NATUREZA,"P",SE2->E2_FORNECE,mv_par02,SE2->E2_VENCREA,dDataBase,SE2->E2_LOJA)
		Else
			nSaldup:=xMoeda((SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE),SE2->E2_MOEDA,mv_par02,SE2->E2_VENCREA,nDecs+1)
		End
		
		If E2_TIPO $ MVPAGANT
			dbSkip( )
			Loop
		Endif
		
		IF SE2->E2_PREFIXO < mv_par09 .or. SE2->E2_PREFIXO > mv_par10  //Do Prefixo ao Prefixo
			SE2->(dbSkip())
			Loop
		EndIF
		
		If ! ( AllTrim(Str(SE2->E2_MOEDA,2)) $ mv_par08 )
			dbSkip()
			Loop
		End
		
		If SE2->E2_FLUXO == "N" .or. SE2->E2_EMIS1 > dDataBase
			dbSkip()
			Loop
		End
		
		dbSelectArea("SE2")
		If mv_par03 == 1
			dbSetOrder(3)
		Endif
		
		IF E2_TIPO $ MVABATIM+"/"+MV_CPNEG
			nDispon+=nSalDup
		Else
			nDispon-=nSalDup
		Endif
		
		IF li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,NumImp())
		Endif
		
		li++
		IF !lAchou
			@li, 0 PSAY SE2->E2_VENCREA
		EndIF
		@li, 11 PSAY SE2->E2_PREFIXO
		@li, 15 PSAY SE2->E2_NUM+(IIF(!EMPTY(SE2->E2_PARCELA),"-",""))+SE2->E2_PARCELA
		@li, 30 PSAY SE2->E2_TIPO
		@li, 34 PSAY SE2->E2_EMISSAO
		@li, 45 PSAY SE2->E2_FORNECEDOR
		@li, 68 PSAY SubStr(SE2->E2_NOMFOR,1,25)
		@li, 96 PSAY SubStr(SE2->E2_HIST,1,25)
		@li,123 PSAY xMoeda(SE2->E2_VALOR+SE2->E2_ACRESC-SE2->E2_DECRESC,SE2->E2_MOEDA,mv_par02,dDataVenc)  Picture PesqPict("SE2","E2_VALOR",mv_par02)
		@li,141 PSAY nSalDup					Picture tm(nSalDup,16,nDecs)
		@li,179 PSAY nDispon					Picture tm(nDispon,16,nDecs)
		IF mv_par02!=1 .and. !lAchou
			dbSelectArea("SM2")
			dbSeek(dDataVenc)
			@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
		EndIF
		lAchou:=.T.
		dbSelectArea("SE2")
		IF E2_TIPO $ MVABATIM+"/"+MV_CPNEG
			nTotTitP -= nSalDup
		Else
			nTotTitP += nSalDup
		End
		dbSkip()
	End
If cIpProj =="S"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe emprestimo a pagar no dia.					  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SEG" )
	If mv_par03 == 1
		dbSetOrder(2)
		dbSeek(cFilial+Dtos(dDataVenc))
	Else
		dbSetOrder( nIndexSEG ) 
		dbSeek(Dtos(dDataVenc))
	Endif
	
	lAchou := .f.
	While !Eof() .And. EG_DATARES == dDataVenc
		
		IF mv_par03 == 1 .and. EG_FILIAL != cFilial
			Exit
		End
		
		If SEG->EG_TIPO != "EMP"
			dbSkip( )
			Loop
		End
		
		nDiasInt	:= EG_DATARES-EG_DATA	//Dias de Intervalo
		nJurDiario:= EG_TAXA/nDiasInt	  //Juros Diario
		nValJuros := EG_VALOR+((EG_VALOR*nJurDiario)/100)*nDiasInt
		nValGanho := nValJuros-EG_VALOR
		nValJuros -= nValGanho*EG_IMPOSTO/100 //Impostos
		IF mv_par02 == 1
			nDispon -= nValJuros
		Else
			dbSelectArea("SM2")
			dbSeek( dDataVenc )
			nDispon -= nValJuros/&('SM2->M2_MOEDA'+cSuf)
		EndIF
		dbSelectArea("SEG")
		li++
		IF !lAchou
			@li, 0 PSAY EG_DATARES
		EndIF
		@li,011 PSAY OemToAnsi(STR0022)   + EG_TIPO + " - " + EG_BANCO  //"Projecao de Pagamento de Emprestimo "
		@li,123 PSAY IIF(mv_par02==1,EG_VALOR,EG_VALOR/&('SM2->M2_MOEDA'+cSuf)) Picture tm(IIF(mv_par02==1,EG_VALOR,EG_VALOR/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
		@li,141 PSAY IIF(mv_par02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))	  Picture tm(IIF(mv_par02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
		@li,179 PSAY nDispon		Picture tm(nDispon, 16,nDecs)
		
		IF mv_par02!=1 .and. !lAchou
			dbSelectArea("SM2")
			dbSeek(dDataVenc)
			@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
		EndIF
		
		lAchou:=.T.
		dbSelectArea("SEG")
		nTotTitP	+= IIF(mv_par02==1,nValJuros,nValJuros/ &('SM2->M2_MOEDA'+cSuf))
		dbSkip()
	End
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe emprestimo a pagar no dia.					  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SEH")
	If ( MV_PAR03 == 1 )
		dbSetOrder(2)
		dbSeek(xFilial("SEH")+"A")
	Else
		dbSetOrder( nIndexSEH ) 
		dbSeek("A")
	Endif

	lAchou := .f.
	While ( !Eof() .And. If(MV_PAR03==1,SEH->EH_FILIAL==xFilial("SEH"),.T.) .And.;
				SEH->EH_STATUS=="A" )
		If ( SEH->EH_APLEMP == "EMP" )
			If ( Empty(SEH->EH_DATARES) .And. J==1 ) .Or.;
				( SEH->EH_DATARES == dDataVenc )
				nValJuros := xMoeda(SEH->EH_SALDO,SEH->EH_MOEDA,1)
				aValAplic := Fa171Calc(dDataVenc,,,,,,,, mv_par03 = 1)
				If Len(aValAplic) > 0	
					nValJuros += aValAplic[2,2]
				Endif	
			Else
				nValJuros := 0
			EndIf
			If ( nValJuros != 0 )
				If ( MV_PAR02 == 1 )
					nDispon -= (nValJuros)
				Else
					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek( dDataVenc )
					nDispon -= (nValJuros)
				EndIf
				li++
				IF !lAchou
					@li, 0 PSAY SEH->EH_DATARES
				EndIf
				@li,011 PSAY OemToAnsi(STR0022)+SEH->EH_TIPO+" - "+SEH->EH_BANCO  //"Projecao de Pagamento de Emprestimo "
				@li,123 PSAY If(MV_PAR02==1,SEH->EH_SALDO,SEH->EH_SALDO/&('SM2->M2_MOEDA'+cSuf)) PICTURE TM(If(MV_PAR02==1,SEH->EH_SALDO,SEH->EH_SALDO/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				@li,141 PSAY If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf)) PICTURE TM(If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf)),16,nDecs)
				@li,179 PSAY nDispon		PICTURE TM(nDispon,16,nDecs)
				If ( MV_PAR02!=1 .And. !lAchou )
					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek(dDataVenc)
					@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) PICTURE TM(&('SM2->M2_MOEDA'+cSuf),10)
				EndIf
				
			EndIf
			lAchou:=.T.
			nTotTitP	+= If(MV_PAR02==1,nValJuros,nValJuros/&('SM2->M2_MOEDA'+cSuf))
		EndIf
		dbSelectArea("SEH")
		dbSkip()
	EndDo
Endif
	nEL:=0
	IF Len(aVendas)>0
		nEl:=Ascan(adVendas,dDataVenc)
	EndIF
	lAchou := .f.
	IF nEl != 0
		li++
		IF !lAchou
			@li, 0 PSAY dDataVenc
		EndIF
		@li,11 PSAY OemToAnsi(STR0023)  //"Pedidos de Vendas"
		nValor:=aVendas[nEl][2]
		@li,160 PSAY nValor	  Picture tm(nValor,16,nDecs)
		nDispon += nValor
		@li,179 PSAY nDispon		Picture tm(nDispon, 16,nDecs)
		IF mv_par02 != 1 .and. !lAchou
			dbSelectArea("SM2")
			Seek dDataVenc
			@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
		EndIF
		lAchou := .T.
		nTotTitR += nValor
	EndIF
	
	nEl:=0
	
	IF Len(aCompras)>0
		nEl := Ascan(adCompras,dDataVenc)
	EndIF
	
	lAchou := .f.
	IF nEl != 0
		li ++
		IF !lAchou
			@li, 0 PSAY dDataVenc
		EndIF
		@li,11 PSAY OemToAnsi(STR0024)  //"Pedidos de Compras"
		nValor := aCompras[nEl][2]
		@li,141 PSAY nValor	  Picture tm(nValor,16,nDecs)
		nDispon -= nValor
		@li,179 PSAY nDispon		Picture tm(nDispon, 16,nDecs)
		IF mv_par02 != 1 .and. ! lAchou
			dbSelectArea("SM2")
			Seek dDataVenc
			@li,199 PSAY &('SM2->M2_MOEDA'+cSuf) Picture tm(&('SM2->M2_MOEDA'+cSuf),10)
		EndIF
		nTotTitP += nValor
		lAchou := .T.
	EndIF
	
	If nTotTitR !=0 .or. nTotTitP != 0
		Li+=2
		IF li > 58
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,NumImp())
		End
		@Li,000 PSAY OemToAnsi(STR0025) +DTOC(dDataVenc)  //"Total Do Dia ---> "
		@Li,141 PSAY nTotTitP 					  Picture Tm(nTotTitP,16,nDecs)
		@Li,160 PSAY ntotTitR 					  Picture Tm(nTotTitR,16,nDecs)
		@li,179 PSAY nDispon				      Picture tm(nDispon ,16,nDecs)
		Li++
	Endif
	
	nTotGTitR += nTotTitR
	nTotGTitP += nTotTitP
	nTotTitP := 0
	nTotTitR := 0
	
Next j

IF li != 80
	Li++
	@Li,000 PSAY OemToAnsi(STR0026)  //"Total Geral  --->"
	@Li,141 PSAY nTotGTitP 					  Picture Tm(nTotGTitP,16,nDecs)
	@Li,160 PSAY ntotGTitR 					  Picture Tm(nTotGTitR,16,nDecs)
	@li,179 PSAY nDispon				      Picture tm(nDispon ,16,nDecs)
	Li++
	roda(cbcont,cbtxt,"G")
EndIF
// Contas a receber
RetIndex("SE1")
dbSelectArea("SE1")
SE1->(dbSetOrder(1))
SE1->(dbClearFilter(NIL))

// Contas a pagar
RetIndex("SE2")
dbSelectArea("SE2")
SE2->(dbSetOrder(1))
SE2->(dbClearFilter(NIL))

// Comissao de vendas
RetIndex("SE3")
dbSelectArea("SE3")
SE3->(dbSetOrder(1))
SE3->(dbClearFilter(NIL))

// Movimentacao bancaria
RetIndex("SE5")
dbSelectArea("SE5")
SE5->(dbSetOrder(1))
SE5->(dbClearFilter(NIL))

// Saldos bancarios
RetIndex("SE8")
dbSelectArea("SE8")
SE8->(dbSetOrder(1))
SE8->(dbClearFilter(NIL))

// Controle de aplicacoes
RetIndex("SEG")
dbSelectArea("SEG")
SEG->(dbSetOrder(1))
SEG->(dbClearFilter(NIL))

RetIndex("SEH")
dbSelectArea("SEH")
SEH->(dbSetOrder(1))
SEH->(dbClearFilter(NIL))

Ferase(cArqSEH+OrdBagExt())
Ferase(cArqSEG+OrdBagExt())
Ferase(cArqSE5+OrdBagExt())
Ferase(cArqSE2+OrdBagExt())
Ferase(cArqSE1+OrdBagExt())
Ferase(cArqSE3+OrdBagExt())
Ferase(cArqSE8+OrdBagExt())

Set Device To Screen

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	ourspool(wnrel)
Endif
MS_FLUSH()


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³AplicacoesºAutor  ³Claudio D. de Souza º Data ³  03/10/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Exibe aplicacoes para selecao/informacao das cotas diarias º±±
±±º          ³ Parametros:                                                º±±
±±º          ³ nMoeda      -> Codigo da moeda                             º±±
±±º          ³ Retorno:                                                   º±±
±±º          ³ aRet[n] = .F.,Banco,Agencia,Conta,Nome,Saldo               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Finc021                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Aplicacoes(nMoeda)
Local oDlgAplic
Local aAplic
Local nX
Local aArea    := GetArea()
Local aAreaSe9 := Se9->(GetArea())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa array com os bancos existentes.						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			  
aAplic := GetAplic()
If Len(aAplic) > 0
	ASort(aAplic)
		
	DEFINE MSDIALOG oDlgAplic TITLE STR0011 From 5,5 To 20,87 OF oMainWnd  //"Informe os valores das cotas diarias"
	@	.8, .5 LISTBOX oAplic FIELDS HEADER  STR0012,STR0013,STR0014,STR0015,STR0016,STR0017 FIELDSIZES 14,25,31,31,60,40,40 SIZE 275, 100 OF oDlgAplic //"Contrato"###"Banco"###"Agencia"###"Saldo"###"Vlr. Cota Atual"###"Vlr. Cota do dia"
	oAplic:SetArray(aAplic)
	oAplic:bLine      := {|| {	aAplic[oAplic:nAt,1],;
							   		aAplic[oAplic:nAt,2],;
							   		aAplic[oAplic:nAt,3],;
							   		aAplic[oAplic:nAt,4],;
							   		aAplic[oAplic:nAt,5],;
							   		aAplic[oAplic:nAt,6]}}
							   
	oAplic:bLDblClick := {|| EditAplic(6,@oAplic,@aAplic),oAplic:GoRight(),oAplic:GoLeft()}

	DEFINE SBUTTON FROM 05, 290 TYPE 1 ACTION oDlgAplic:End() ENABLE OF oDlgAplic
	ACTIVATE MSDIALOG oDlgAplic
	
Endif
// Transforma o ultimo elemento da matriz (Valor da cota do dia) para numerico
// Eliminando a picture deste campo e grava valor da cota do dia, caso exista o campo
For nX := 1 To Len(aAplic)
	aAplic[nX][6] := Val(StrTran(StrTran(aAplic[nX][6],".",""),",","."))
	If SE9->(FieldPos("E9_COTADIA"))>0
		dbSelectArea("SE9")
		dbSetOrder(1)
		MsSeek(xFilial("SE9")+aAplic[nX][1]+aAplic[nX][2]+aAplic[nX][3])
		If Str(SE9->E9_COTADIA,17,4) != Str(aAplic[nX][6],17,4)
			RecLock("SE9")
			SE9->E9_COTADIA := aAplic[nX][6]
		Endif	
	Endif
Next
Se9->(RestArea(aAreaSe9))
RestArea(aArea)
Return aAplic

Static Function EditAplic(nCol,oAplic,aAplic)
Local nClick := 0

nClick := oAplic:nAtCol(nCol)

If nClick <> 1
	// Transforma para numerico para que o usuario possa editar o campo com entrada
	// de numeros apenas.
	aAplic[oAplic:nAt][nCol] := Val(StrTran(StrTran(aAplic[oAplic:nAt][nCol],".",""),",","."))
	lEditCell(@aAplic,oAplic,PesqPict("SE9","E9_VLRCOTA",18),nCol)
	// Volta para caracter o numero editado para que possa ser exibido corretamente
	// pela LISTBOX.
	aAplic[oAplic:nAt][nCol] := Transform(aAplic[oAplic:nAt][nCol],PesqPict("SE9","E9_VLRCOTA",18))
	oAplic:SetFocus()
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³GetAplic  ºAutor  ³Claudio D. de Souza º Data ³  03/10/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Obter as aplicacoes do SEH                                 º±±
±±º          ³ Retorno:                                                   º±±
±±º          ³ aRet[n] = .F.,Banco,Agencia,Conta,Nome,Saldo               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Finc021                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GetAplic()
Local aRet     := {},;
		aArea    := GetArea(),;
		aAreaSeh := Seh->(GetArea()),;
		aAreaSe9 := Se9->(GetArea()),;
		cAplCotas:= GetMv("MV_APLCAL4"),;
		cIndSEH  := ""
DbSelectArea("SEH")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra o arquivo de aplicacoes  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SEH")
cIndSEH := CriaTrab(,.F.)
IndRegua("SEH",cIndSEH,IndexKey(),,"EH_TIPO$"+cAplCotas+" .And. EH_FILIAL='"+xFilial("SEH")+"'")
nIndSEH := RetIndex("SEH")	
nIndSEH++
#IFNDEF TOP
	dbSetIndex(cIndSEH+ordBagExt())
#ENDIF

While !Eof() // Processa arquivo filtrado
	dbSelectArea("SE9")
	dbSetOrder(1)
	MsSeek(xFilial("SE9")+SEH->EH_CONTRAT+SEH->EH_BCOCONT+SEH->EH_AGECONT)
	dbSelectArea("SEH")
	Aadd(aRet,{	SEH->EH_CONTRAT,SEH->EH_BCOCONT,SEH->EH_AGECONT,Transform(SEH->EH_SALDO,"@E 999,999,999.99"),Transform(SE9->E9_VLRCOTA,PesqPict("SE9","E9_VLRCOTA",18)),;
					If(se9->(FieldPos("E9_COTADIA"))>0 .And. SE9->E9_COTADIA > 0,;
						Transform(SE9->E9_COTADIA,PesqPict("SE9","E9_COTADIA",18)),;
						Transform(SE9->E9_VLRCOTA,PesqPict("SE9","E9_VLRCOTA",18)))})
	DbSkip()
End

If !Empty(cIndSEH)
	dbSelectArea("SEH")
	RetIndex("SEH")
	dbClearFilter()	
	Ferase(cIndSEH+OrdBagExt())
EndIf

SeH->(RestArea(aAreaSeh))
Se9->(RestArea(aAreaSe9))
RestArea(aArea)

Return aRet