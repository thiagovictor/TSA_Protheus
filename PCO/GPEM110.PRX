#INCLUDE "Protheus.ch"
#INCLUDE "GPEM110.CH"

#IFNDEF TOP
	#DEFINE __TiposValidos__ 			( "FL*13*PR*PF*PD" )
#ELSE
	#DEFINE __TiposValidos__ 			( "'FL','13','PR','PF','PD'" )
#ENDIF	

#DEFINE __Elementos_aSrvReturn__	09	

#DEFINE __aSrvReturn_TudoOK__		01
#DEFINE __aSrvReturn_aCols__ 		02
#DEFINE __aSrvReturn_aHeader__		03
#DEFINE __aSrvReturn_aRecnos__		04
#DEFINE __aSrvReturn_nPosFil__		05
#DEFINE __aSrvReturn_nPosPD__		06
#DEFINE __aSrvReturn_nPosLctop__ 	07
#DEFINE __aSrvReturn_aRecLctop__	08
#DEFINE __aSrvReturn_cAliasLctop__	09

#DEFINE __Elementos_aSRZzs__		04

#DEFINE __aSzs_Pos_Fil__			01
#DEFINE __aSzs_Pos_Cc__				02
#DEFINE __aSzs_Pos_Mat__			03
#DEFINE __aSzs_Pos_Pd__				04

#DEFINE __LCTO_PCO_FOLHA__ 			'000081'

Static lGpem110ExecInRemote

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEM110  ³ Autor ³ Marcos Stiefano       		  ³ Data ³ 07/06/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Contabiliza‡ao da Folha                                    			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GEPM110()                                                  			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS 			 ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Andreia     ³16/07/08³148037			 ³ Ajuste na contabilizacao de provisões,po-³±±
±±³            ³        ³      			 ³ is se contabilizasse prov. de férias não ³±±
±±³            ³        ³      			 ³ permitia contabilizar prov. de 13o pos-  ³±±
±±³            ³        ³      			 ³ teriormente.                             ³±±
±±³Jonatas     |31/07/08|105440			 |Incluido tratamento dos campos _ITEM e    ³±±
±±|            |        |      			 |_CLVL.                                    ³±±
±±³Jonatas     ³25/08/08³152509			 ³Ajuste no teste de item/classe na funcao  ³±±
±±³   		   ³		³	  			 ³fGeraFolSRZ().                            ³±±
±±³Andreia     ³08/08/08³150868			 ³Tratamento da contabilizacao da primeira  ³±±
±±³   		   ³		³	   			 ³parcela do decimo terceiro para Argentina ³±±
±±³Luciana     ³13/11/08³144823			 ³Melhoria de performance. Desenvolvimento  ³±±
±±³            ³        ³------			 ³de procedure para gravacao dos registros  ³±±
±±³            ³        ³------			 ³no SRZ                                    ³±±
±±³Luciana     ³16/01/09³648/09			 ³Ajuste na rotina de contabilização da     ³±±
±±³            ³        ³------			 ³folha para gravar os campos de Item e     ³±±
±±³            ³        ³------			 ³Classe em branco para as verbas           ³±± 
±±³            ³        ³------			 ³cadastradas no parâmetro 15               ³±± 
±±³Jonatas     ³26/02/09³003929			 ³Inclusao de tratamento para item/classe   ³±±
±±³            ³        ³  2008			 ³na contabilizacao da provisao.            ³±±
±±³Luciana     ³16/04/09³007505			 ³Tratamento para a variavel cTiposDel des- ³±±
±±³            ³        ³  2009			 ³considerar a virgula qdo lProvFer esta    ³±±
±±³            ³        ³      			 ³com .F. e lProv13o esta com .T.           ³±±
±±|Jonatas     |19/06/09|013786			 |Inclusao de help p/ tratar lock do SRZ    ³±±
±±|            |        |  2009			 |onde ha acesso exclusivo a tabela.        ³±±
±±³Alex        ³18/08/09³020257			 ³Ajuste no Grupo de Campos filial          ³±±
±±³            ³        ³ /2009			 ³tratamento para não considerar 2 posições ³±±
±±³            ³        ³      			 ³fixas.                                    ³±±
±±³Alex        ³22/09/09³022766			 ³Tratamento na opção AMBAS, geração Manad  ³±±
±±³            ³        ³ /2009			 ³Rotina fGeraFolSRZ, passava gravando para ³±±
±±³            ³        ³      			 ³Mensal e 13º com o Apelido no SQL errado. ³±±
±±³Tiago Malta ³19/10/09³022649			 ³Ajuste para integração Logix com          ³±±
±±³            ³        ³ /2009			 ³Rh Porhteus                               ³±±  
±±³Tiago Malta ³07/12/09³028284			 ³Ajuste na inicialização de variaveis      ³±±
±±³            ³        ³ /2009			 ³ usando Space(FWGETTAMFILIAL).            ³±± 
±±³            ³        ³      			 ³ Alterado o uso da variavel cEmpant       ³±± 
±±³            ³        ³      			 ³ para FwCodEmp("SRA")                     ³±± 
±±³Renata Elena³05/12/09³027144			 ³Tratamento de Item Contabil e Classe      ³±±
±±³            ³        ³ /2009			 ³de Valores no Padrao.                     ³±±
±±|Luciana     |21/01/10|030630			 |Ajuste na fGeraEncInss e fProcGravaSRZ    ³±±
±±|            |        |  2009			 |para atribuir o alias cArqSRI para as     ³±±
±±|            |        |      			 |tabelas de 13o. salario.                  ³±± 
±±|Luciana     |22/12/00|029847			 |Ajuste na fGeraEncInss e fProcGravaSRZ    ³±±
±±|            |        |  2009			 |para atribuir o alias cArqSRI para as     ³±±
±±|            |        |      			 |tabelas de 13o. salario.                  ³±± 
±±|Luciana     |02/02/10|001868			 |Validacao da pergunta Mes/Ano da Contabi- ³±±
±±|            |        |  2010			 |lizacao para impedir a digitacao de       ³±±
±±|            |        |      			 |periodos invalidos.                       ³±±
±±|Mauricio T. |24/03/10|00000006562/2010|Alterado o parametro FWCODEMP para cEmpAnt³±±
±±|            |        |  				 |na Funcao Gpem110FilCar                   ³±±
±±³Mauricio MR ³26/08/10³00000018570/2010³Realizacao de ajuste gerais para uso de   ³±±
±±³            ³        ³  				 ³procedure.								³±±
±±³            ³        ³ 			     ³Ajuste na query/procedure para nao gerar  ³±±
±±³            ³        ³ 			     ³chave duplicada e gps nula ao possuir para³±±
±±³            ³        ³ 			     ³metro 15. Implementacao de tratamento para³±±
±±³            ³        ³			     ³expatriados/residentes no exterior no uso ³±±
±±³            ³        ³   			 ³da procedure. Ver GPER040.				³±±
±±³Mauricio MR ³07/10/10³00000023098/2010³Criacao do ponto de entrada "GP110VLD"    ³±±
±±³            ³        ³  				 ³para consistencias adicionais para a exe- ³±±
±±³            ³        ³      		  	 ³cucao da rotina.						  	³±±
±±³Mauricio MR ³17/11/10³00000024155/2010³Ajustes nas queries e referencias a filial³±±
±±³            ³        ³  				 ³para adequar ao uso da gestao corporativa.³±±
±±³Renata E    ³20/10/11³00000027114/2011³Ajuste na funcao fDelRegSRZ para quando   ³±±
±±³            ³        ³                ³chamada pela provisao, deletar registros  ³±±
±±³            ³        ³                ³PR, PD e FP 						        ³±±
±±³Aldo Marini ³04/11/11³00000000000/2011³Contabilizar valores do mes provisao      ³±±
±±³Mauricio MR ³30/11/11³00000030147/2011³Correcao para considerar a ordem adequada ³±±
±±³            ³        ³				 ³para item e classe de valor quando exis-  ³±±
±±³            ³        ³TEBY23			 ³tirem.									³±±
±±³Kelly Soares³02/05/12³00000010656/2012³Ajuste na contabilizacao de provisões.    ³±±
±±³            ³        ³Chamado: TEUOTJ ³                                          ³±±
±±³Kelly Soares³22/06/12³00000010656/2012³Alteracoes na forma de contabilizacao dos ³±±
±±³            ³        ³TEUOTJ          ³valores do mes - fGeraCTab: contabilizacao³±±
±±³            ³        ³                ³pela funcao fGrvRateioda; fGravaSRZ: remo-³±±
±±³            ³        ³                ³vido o bloco de codigos para lGeraPMes.   ³±±
±±³Kelly Soares³04/07/12³00000010656/2012³Ajuste na funcao SRZTotAux para casos de  ³±±
±±³            ³        ³TEUOTJ          ³item e classe de valor.                   ³±±
±±³Kelly Soares³06/07/12³00000010656/2012³Ajuste na funcao fGrvRateio.              ³±±
±±³Silvia Tag  ³23/07/12³TFLUJA          ³Retirada do tratamento para Rateio na     ³±±
±±³            ³        ³                ³Provisao                                  ³±±
±±³Christiane V³03/09/12³TFQAE8          ³Inclusão do Rateio na Provisão            ³±±
±±³R.Berti     ³29/11/12³TGENIN - CBM    ³Corrig.chave duplicada SRZ-GRID habilitado³±±
±±³Leandro Dr. ³28/01/13³REQ140_01       ³Tratamento para integracao com LOGIX via  ³±±
±±³            ³        ³M12RH02         ³geracao de TXT.                           ³±±
±±³Allyson M   ³20/02/12³TGNGAS - CBM    ³Inserido TBLOG conf. MV_GPCTLOG - R.Berti	³±±
±±³Allyson M   ³09/04/13³TGJMN9		     ³Ajuste na query p/ encontrar o numero de	³±±
±±³            ³        ³                ³registros que serao processados na SRZ e  ³±±
±±³            ³        ³                ³que e' utilizada na regua de processamento³±±
±±³Gustavo M   ³02/05/13³THBEDL		     ³Ajuste para nao duplicar os registros caso³±±
±±³            ³        ³                ³SRV seja compartilhada.					³±±
±±³Claudinei S.³12/03/13³00000017758/2013³Criada fValSIX() para validar os indices  ³±±
±±³            ³        ³THOLEI          ³da SRZ e solicitar a execucao do RHUPDMOD.³±±
±±|Gustavo M.  |19/09/13|TGWAX7 		 |Ajuste no MV_ITMCLVL para nao obrigar		³±±
±±|            |        |                |preenchimento de ambos valores (Cont="3")	³±±
±±|Sidney O.   |27/11/13|TI4626    		 |Usuário agora pode informar o código do   ³±±
±±|            |        |       		 |histórico na geração do arquivo.     		³±±
±±|            |        |       		 |Usuário agora pode informar um diretório	³±±
±±|            |        |       		 |fora da árvore do servidor para salvar o 	³±±
±±|            |        |       		 |arquivo que será exportado para o sistema ³±±
±±|            |        |       		 |externo                                   ³±±
±±³Allyson M   |13/05/14|TILVGY		     |Ajuste p/ nao carregar mais de uma vez o  ³±±
±±³            |        |                |valor do rateio da provisao caso o fun-   ³±±
±±³            |        |                |cionario possua transferencia no mes      ³±±
±±³Allyson M   |15/07/14|TPVGE5		     |-Ajuste p/ contabilizacao da provisao por ³±±
±±³            |        |                |CC + Item + Classe.  						³±±
±±³            |        |                |-Ajuste na identacao do codigo no trecho  ³±±
±±³            |        |                |referente contabilizacao da provisao.     ³±±
±±³Allyson M   |21/07/14|TPVGE5		     |Ajuste na ordenacao do temporario TPR na 	³±±
±±³            |        |                |contabilizacao da provisao.				³±±
±±³Gustavo M   |08/08/14|TQGYH5		     |Ajuste para nao contabilizar todos os val-³±±
±±³            |        |                |ores sem respeitar o pergunte.(chave dupl)³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM110()
 
Local aArea		:= GetArea()
Local aSays		:= {}
Local aButtons	:= {}
Local aTitle		:= {}
Local aLog			:= {}
Local aTimeLog		:= {}

Local cSvFilAnt		:= cFilAnt

Local nOpca			:= 0.00
Local nTimeIni		:= nTimeFim		:= Seconds()

AjustaSx6() //Cria parametro MV_ERPLOGI

Private aRotina		:= {;
						{ STR0005 , "GPE110Par" , 0 , 1},;	//"Parƒmetros"
                      	{ STR0006 , "GPE110Con" , 0 , 4} ;	//"Autom tica"
                    } 
Private aIncLog 	:= {}
Private aErrProc	:= Array(3,0)
Private cCadastro	:= OemToAnsi(STR0007) //"Contabiliza‡„o da Folha"

Private lErpLogix := SuperGetMv("MV_ERPLOGI",,"2") == "1"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Apura a existencia de pontos de entrada					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private lGP110VLD	:= ExistBlock("GP110VLD") //-- Consistencias adicionais para a execucao da rotina

Private lGeraPMes := .F.

If SuperGetMv("MV_RATPROV",,"N") == "S"
	lGeraPMes	  := fChkRHQBase()	// Verifica a existencia da tabela RHQ
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta Tela de Entrada para a Selecao dos Parametros          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdd(aSays,OemToAnsi(STR0008) )  //"Este programa contabilizar  os lan‡amentos da folha de pagamento "
aAdd(aSays,OemToAnsi(STR0009) )  //"para a contabilidade lendo o arquivo (SRZ) . Dever  ser utilizado"
aAdd(aSays,OemToAnsi(STR0010) )  //"apos as emissäes da folha, GRPS e provisäes."

aAdd(aButtons, { 5,.T.,{|| Pergunte("GPM110",.T. ) } } )
aAdd(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpconfOK(),FechaBatch(),nOpca:=0) }} )
aAdd(aButtons, { 2,.T.,{|o| FechaBatch() }} )
	
FormBatch( cCadastro , aSays , aButtons )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( nOpca == 1 )
	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Adiciona Informacoes no Log de Processamento 							³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aAdd( aTitle 	, STR0052 ) // "Tempo de processamento da Contabilização"
	aAdd( aTimeLog	, STR0053 + Dtoc(MsDate()) + ', ' + STR0054 + ' ' + SecsToTime( nTimeIni ) + '.' ) // "Início do Processamento : "##"às"

	// Executa a contabilizacao
	Gpem110Processa( .T. , .T. )

	aAdd( aTimeLog	, STR0055 + Dtoc(MsDate()) + ', ' + STR0054 + ' ' + SecsToTime( nTimeFim := Seconds() ) + '.' ) // "Fim do Processamento    : "##"às"
	aAdd( aTimeLog	, STR0056 + SecsToTime( nTimeFim - nTimeIni ) )//"Duração do processamento: "

	aAdd( aLog, aClone( aTimeLog ) )
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Geracao do Log de Ocorrencias                                ³
³    [1] - Erro na criacao da procedure                        ³
³    [2] - Erro na execucao da procedure                       ³
³    [3] - Erro na obtencao de acessso exclusivo ao SRZ        ³
³    [4] - Inconsistencias                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If !Empty( aErrProc[1] )
	aAdd( aTitle, STR0058 ) // "Não foi possível criar a procedure para gravação do SRZ"
	aTitle[ Len(aTitle) ] += " - (" + Alltrim( PosAlias( "SX2", "SRZ", "", "X2Nome()", 1, .F. ) ) + ") " //Descricao da tabela SRZ
	aAdd( aLog, aClone( aErrProc[1] ) )
EndIf
If !Empty( aErrProc[2] )
	aAdd( aTitle, STR0059 ) // "Não foi possível executar a procedure para gravação do SRZ"
	aTitle[ Len(aTitle) ] += " - (" + Alltrim( PosAlias( "SX2", "SRZ", "", "X2Nome()", 1, .F. ) ) + ") " //Descricao da tabela SRZ
	aAdd( aLog, aClone( aErrProc[2] ) )
EndIf
If !Empty( aErrProc[3] )
	aAdd( aTitle, STR0060 ) // "O arquivo SRZ não pôde ser aberto por estar sendo utilizado por outro usuário"
	aAdd( aLog, aClone( aErrProc[3] ) )
EndIf
If !Empty( aIncLog )
	aAdd( aTitle , STR0033 ) // "Foram Encontradas Inconsistencias na Contabilizacao."
	aAdd( aLog, aClone( aIncLog ) )
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Gera o Log de Ocorrencias     							   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF !Empty( aLog )
	MsAguarde( { || fMakeLog( aLog, aTitle , "GPM110" , lGpem110ExecInRemote , FunName() , STR0057, , , , .F. ) } , STR0057 ) // "Log de Ocorrências na Contabilização"
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura a Filial de Entrada                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFilAnt := cSvFilAnt

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura os Ponteiros de Entrada                             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RestArea( aArea )

Return( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³Gpem110Processa³Autor³Equipe Advanced RH   ³ Data ³14/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Processa a Contabilizacao do Modulo SIGAGPE    			     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                   	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM110  													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function Gpem110Processa( lShowProc , lExecInRemote  )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ As variaveis Locais de Ambiente devem Ser Declaradas e Inicia³
³ lizadas Aqui                                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local aArea			:= GetArea()
Local aAreaSRZ		:= SRZ->( GetArea() )
Local aAreaSRA		:= SRA->( GetArea() )
Local aAreaSI3		:= SI3->( GetArea() )
Local aAreaSM0		:= SM0->( GetArea() )
Local cSvFilAnt		:= cFilAnt
Local cMvCPosSrv	:= ""

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ As variaveis Locais do corpo do programa devem ser Aqui Decla³
³ radas mas  inicializa das Abaixo. GOTO :INIT LOCAL           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local aSrvLanc
Local aSrvPCO
Local aCT5
Local aPadrao
Local aLancPCO
Local aFiliais
Local aLogIncon
Local aLogTitle
Local aFilContab
Local aSRZzs
Local aModuloReSet
Local bSrvLancAsc
Local bSrvPcoAsc
Local bTotal
Local cInconsist
Local cSrvPds
Local cArquivo
Local cFilConDe
Local cFilConAte
Local cAliasCc
Local cZzsInRZFil
Local cZzsInRZCc
Local cZzsInRZMat
Local cZzsInRZPd
Local cFilReplace
Local cFilContab
Local cGPCTLOG 	:= SuperGetMv("MV_GPCTLOG",," ")
Local lExistMatSRZ
Local lGerouFol
Local lGerou13s
Local lSrzCompart
Local lSrvCompart
Local lCcCompart
Local lCt5Compart
Local lSi5Compart
Local lMvCPosSrv
Local lMvCPosLcp
Local lMvCPosCtt
Local lSrvLancOk
Local lSrvPCOOk
Local lTemLog 	:= .F.
Local lCtbInUse
Local nContador
Local nPosPadrao
Local nFilial
Local nFiliais
Local nVerba
Local nVerbas
Local nLastRec
Local cMesAno	:= SuperGetMv("MV_FOLMES",,"")		//Parametro mes de processamento da folha
Local lContFech := SuperGetMv("MV_GPCTAFM",,.F.)   //Parametro se deve contabilizar somente apos fechamento
Local lCTBok
Local lPCOok
Local lIntePco
Local lFolPgBk
Local lFol13Bk

Local nI := 0
#IFDEF TOP             	
   	Local aSrzFields
   	Local aSrvFields
   	Local aCcFields
   	Local aNewCpos
   	Local aOldSRZ
	Local cQuery
	Local cQueryPds
	Local cQueryCc
	Local cQueryOrd
	Local cSrzQuery
	Local cQueryTmp
	Local cSrzFields
	Local cRetSqlName
	Local cSRZIndexKey
	Local cSrvCpos
	Local cCcCpos
	Local lSelectPds
	Local nSrzFields
	Local nField
	Local nElem
	Local nCpos
	Local nOldSRZ
#ENDIF
Private cMesCont := SuperGetMv("MV_GPMESCT",,"")  //Parametro que armazena o ultimo mes contabilizado, atualizado apos a contabilizacao.


If cPaisLoc == "URU"
	Private nTotFol := 0
	Private nTot13  := 0
	Private lAgui := .F.
	Private lFolha:= .F. 
	Private nFuncFol := 0
Endif


DEFAULT lShowProc := .F.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se o Processo de Contabilizacao esta sendo Executado³
³ via Remote												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Gpem110InRemote( @lExecInRemote )

ReabSRZ()

Begin Sequence

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Chamada Recursiva com Barra de Processamento                 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( ( lShowProc ) .and. ( lGpem110ExecInRemote ) )
		Processa( {|lEnd| Gpem110Processa( .F. , lGpem110ExecInRemote ) , STR0011 } )  //"Contabiliza‡„o da Folha"
		Break
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Criacao de semaforo para acesso exclusivo ao alias SRZ        ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !( WhileNoLock( "SRZ" , NIL , { "SRZ" + cEmpAnt } , 1 , 1 , .T. , NIL , NIL , NIL , .F. ) )
		Help( " ",1,"NOPENSRZ", )		// Nao conseguiu exclusividade do arquivo
		aAdd( aErrProc[3], STR0061 )	// "Execute novamente a rotina após a liberação de acesso ao arquivo SRZ."
		Break
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ :INIT LOCAL												   ³
	³ Aqui eh que as variaveis Locais que serao utilizadas no corpo³
	³ do progama deverao ser Inicializadas						   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aSrvLanc		:= {}
	aSrvPco			:= {}
	aCT5			:= {}
	aPadrao			:= {}
	aLancPCO		:= {}
	aFiliais		:= {}
	aLogIncon		:= {}
	aLogTitle		:= {}
	aFilContab		:= {}
	aSRZzs			:= SRZzs()
	bSrvLancAsc		:= { || NIL }
	bSrvPcoAsc		:= { || NIL }
	bTotal			:= { || NIL }
	cInconsist		:= ""
	cSrvPds			:= ""
	cArquivo		:= ""
	cFilConDe		:= Space(FWGETTAMFILIAL)
	cFilConAte		:= cFilConDe
	cAliasCc    	:= ""
	cZzsInRZFil		:= aSRZzs[__aSzs_Pos_Fil__]
	cZzsInRZCc		:= aSRZzs[__aSzs_Pos_Cc__ ]
	cZzsInRZMat		:= aSRZzs[__aSzs_Pos_Mat__]
	cZzsInRZPd		:= aSRZzs[__aSzs_Pos_Pd__ ]
	
	cFilReplace		:= Space(FWGETTAMFILIAL)
	cFilContab		:= Space(FWGETTAMFILIAL)
	lExistMatSRZ	:= ( Type("SRZ->RZ_MAT") == "C" )
	lGerouFol		:= .F. 
	lGerou13s 		:= .F.
	lSrzCompart		:= FWModeAccess("SRZ",3)<>'E' 
	lSrvCompart		:= FWModeAccess("SRV",3)<>'E'
	lCt5Compart		:= ( ( Select("CT5") > 0.00 ) .and. FWModeAccess("CT5",3)<>'E' )
	lSi5Compart		:= ( ( Select("SI5") > 0.00 ) .and. FWModeAccess("SI5",3)<>'E' )
	lMvCPosSrv		:= .F.
	lMvCPosLcp		:= .F.
	lMvCPosCtt		:= .T.
	lSrvLancOk		:= .F.

	lSrvPCOOk		:= .F.
	lCtbInUse		:= CtbInUse()
	nContador		:= 0.00
	nPosPadrao		:= 0.00
	nFilial			:= 0.00
	nFiliais		:= 0.00
	nVerba			:= 0.00
	nVerbas			:= 0.00
	nLastRec		:= 0.00
	nTimeIni		:= Seconds()
	nTimeFim		:= Seconds()
	lCTBok          := .T.
	lPCOok          := .T.
	lIntePco 		:= (SuperGetMV("MV_PCOINTE",.F.,"2")=="1" .And. SRV->(FieldPos("RV_LANCPCO"))>0)
	lSPGP110 		:= SuperGetMv("MV_SPGP110",,.F.)

	#IFDEF TOP
	   	aSrzFields		:= SRZ->( dbStruct() )
	   	aSrvFields		:= SRV->( dbStruct() )
	   	aCcFields		:= IF( lCtbInUse , CTT->( dbStruct() ) , SI3->( dbStruct() ) )
	   	aNewCpos		:= {}
	   	aOldSRZ			:= aClone( aSrzFields )
		cQuery			:= ""
		cQueryPds		:= ""
		cQueryCc		:= ""
		cQueryOrd       := ""
		cSrzQuery 		:= ""
		cQueryTmp		:= ""
		cSrzFields		:= ""
		cRetSqlName		:= InitSqlName( "SRZ" )
		cSRZIndexKey	:= ""
		cSrvCpos		:= ""
		cCcCpos			:= ""
		lSelectPds		:= .T.
		nSrzFields		:= Len( aSrzFields	)
		nField			:= 0.00
		nElem			:= 0
		nCpos			:= 0
		nOldSRZ			:= nSrzFields
	#ENDIF

	Private Inclui		:= .T.
	Private cLote		:= ""
	Private cPadrao		:= ""	
	Private dDataLanc	:= dDataBase
	Private nTotRegs 	:= 0.00
	Private nTotal   	:= 0.00
	Private cEncInss	:= GetMv("MV_ENCINSS",,"N")
	Private cMesAnoCtab	:= ""	
	
	// Verificar contabilizacao por item contabil e classe de valor
	Private lItemClVl := SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "13"
							
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Algum Lugar Esta Zerando nModulo, Entao Aqui garantimos   que³
	³ Tenha o Conteudo correspondente ao Modulo SIGAGPE            ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( ( Type("nModulo") == "U" ) .or. ( ( Type("nModulo") == "N" ) .and. ( nModulo == 0.00 ) ) )
		aModuloReSet := SetModulo( "SIGAGPE" , "GPE" )
	EndIF
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega as Perguntas										   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	Pergunte( "GPM110" , .F. )
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Inicia Processo Lancamento - Integracao com modulo PCO       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	PcoIniLan(__LCTO_PCO_FOLHA__)
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Transfere as Perguntas para variaveis de Memoria			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cMesAnoCtab := mv_par01								// MM/AAAA da Contabilizacao
	lFolPgto    := ( mv_par02 == 1 .or. mv_par02 == 3 ) // Contabilizar Folha:1-Folha de Pgto;2-Folha de 13o Sal;3-Ambas;4-Nao
	lFol13Sl    := ( mv_par02 == 2 .or. mv_par02 == 3 ) // Contabilizar Provisao:1-Ferias;2-13o.Sal.;3-Ambas;4-Nao
	lProvFer    := ( mv_par03 == 1 .or. mv_par03 == 3 ) // Provisao de Ferias
	lProv13o    := ( mv_par03 == 2 .or. mv_par03 == 3 )	// Provisao de 13o. Salario
	lCtabBxaFer := ( mv_par04 == 1 )                    // Cont.Bx.Ferias.Prov:1-Sim;2-Nao
	lCtabBxaRes := ( mv_par05 == 1 )                    // Cont.Bx.Res.Prov:1-Sim;2-Nao
	lDigita     := ( mv_par06 == 1 )					// Mostra Lancamentos:1-Sim;2-Nao
	lAglut      := ( mv_par07 == 1 )					// Aglutina Lancamentos:1-Sim;2-Nao
	cFilConDe	:= mv_par08								// Filial De
	cFilConAte	:= mv_par09								// Filial Ate
	If cPaisLoc == "DOM"
   		cSemana := mv_par10
	Endif

	IF ( cEncInss == "S" ) .and. ( lExistMatSRZ )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega as Perguntas										   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		Pergunte( "GPM11A" , .T. )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Transfere as Perguntas para variaveis de Memoria			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		lContabCC	:= (mv_par01 = 1 )

		mv_par01	:= cMesAnoCtab // atribuindo o conteudo do mv_par01 para a variavel que esta sendo usada abaixo.
  		
	Endif 
		
	If cPaisLoc == "ARG" .and. lFol13Sl
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega as Perguntas										   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		Pergunte( "GPM11B" , .T. )
		
		nI := MV_PAR01

		mv_par01	:= cMesAnoCtab // atribuindo o conteudo do mv_par01 para a variavel que esta sendo usada abaixo.
		
	EndIf	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ VERIFICA A EXISTENCIA DO INDICE NA TABELA SRZ                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
 	If !fValSIX()
 		Aviso( STR0044, STR0093, {STR0001} ) //"Atencao"##"Execute a opção do compatibilizador referente a Criação de indice por C.Custo na Tabela SRT - Movimento de Provisões. Para maiores informações, verifique respectivo Boletim Técnico."##"Confirma"
 		ReabSRZ()
  	    Return
  	Endif   
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ VERIFICA MES DE PROCESSAMENTO DA FOLHA COM MES DE CONTABILIZACAO ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
 	If Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2) > cMesAno 
		MsgInfo(  STR0047 + ": "+ Right(cMesAno,2)+ "/" + Left(cMesAno,4) ,STR0044 ) //"Aviso"#"Mes de Contabilizacao nao pode ser maior que o mes de Processamento Atual"
		ReabSRZ()
  	    Return
  	Endif    

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ VERIFICA SE DEVE CONTABILIZAR SOMENTE APOS FECHAMENTO CONFORME PARAMETRO ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If lContFech .And. cMesAno <= Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2)
		MsgInfo(  STR0043, STR0042 ) //Aviso#Fechamento do mes nao foi processado
		ReabSRZ()
	   	Return
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ VERIFICA ULTIMO MES CONTABILIZADO COM O MES QUE ESTA SENDO CONTABILIZADO ³
	³ E AVISO USUARIO QUE MES E MENOR QUE ULTIMO CONTABILIZADO COM DE CONTINUAR³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If cPaisLoc <> "CHI"
		If cMesCont >= Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2)
			IF !MsgNoYes(OemToAnsi(STR0045)+ cMesAnoCtab+CRLF+OemToAnsi(STR0046)+":"+Right(cMesCont,2)+Left(cMesCont,4)+CRLF+OemToAnsi(STR0048),OemToAnsi(STR0044)) //"ATEN€O"#"O Mes que esta sendo Contabilizado, "#" e menor que o mes da ultima contabilizacao, "#"Verifique se mes ja foi contabilizado."
				ReabSRZ()
  				Return                                                    //#"Verifique se mes ja foi contabilizado."
			EndIF
		Endif	
	Else
		If lContFech .And. cMesCont >= Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2) 
			MsgInfo(  STR0049, STR0042 ) //Aviso#Este mes ja foi contabilizado
			ReabSRZ()
		   	Return
		Endif		   
	Endif	 

	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Ponto de entrada para consistencia adicionais de usuario     ³
	³ antes da execucao da contabilizacao.						   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF lGP110VLD
		IF Execblock("GP110VLD",.F.,.F.)
			ReabSRZ()	
		   	Break			 
		EndIf
    Endif

	//-- Cria tabela de log para uso em procedure
	If "C" $ cGPCTLOG
		lTemLog := GP40TabLog( "TBLOG", "TBLOG", lItemClVl)
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Gerar arquivo SRZ com os lancamentos contabeis			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( lFolPgto )	// Folha Pagto / Indeterminado-Determinado
		lFol13Bk := lFol13Sl
		lFol13Sl := .F.		

		lGerouFol := fGeraFolSRZ(1,{1,2},cMesAnoCtab)

   		lFol13Sl := lFol13Bk

   	EndIF
	
	IF ( lFol13Sl )	// Folha 13§ / Indeterminado-Determinado
   		lFolPgBk := lFolPgto
  		lFolPgto := .F.

		If cPaisLoc <> "ARG"
			lGerou13s := fGeraFolSRZ(2,{1,2},cMesAnoCtab) 
		Else
 			If nI ==1 .or. nI == 2
				lGerou13s := fGeraFolSRZ(2,{1,2},cMesAnoCtab,nI)
			ElseIf nI ==3                
			  	For nI := 1 to 2
					lGerou13s := fGeraFolSRZ(2,{1,2},cMesAnoCtab,nI)
				Next nI	
			EndIf				 
		EndIf	

		lFolPgto := lFolPgBk

	EndIF
	
	//-- Tabela de Log para Procedure 
	If "C" $ cGPCTLOG
		If lTemLog
			If Select("TBLOG") > 0
				TBLOG->( dbCloseArea() )   
			Endif
		Endif  
	Endif  

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Inicializa as constantes e chama a funcao fGeraProvSrz()	   |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( ( lProvFer ) .or. ( lProv13o ) )	// "Geracao de Lancamentos - Provisao"
		GpeProvisao(STR0023,cMesAnoCtab,,,6)
	EndIF
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Posiciona arquivos na Ordem 1 para a contabilizacao 		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	SRA->( dbSetOrder( 01 ) )
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define o Alias do arquivo de Centro de Custo e Seleciona a or³
	³ dem														   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cAliasCc := IF( lCtbInUse , "CTT" , "SI3" )
	(cAliasCc)->( dbSetOrder( 01 ) )
	
	lCcCompart := FWModeAccess(cAliasCc,3)<>'E' 
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Seleciona a Ordem 1 para o arquivo de Lancamentos Padroes    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( lCtbInUse )
		CT5->( dbSetOrder( 01 ) )
	Else
		SI5->( dbSetOrder( 01 ) )
	EndIF
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta Bloco para o aScan no aSrvLanc                		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvLancAsc	:= { |x|;
					  		(;
					  			( x[ aSrvLanc[ __aSrvReturn_nPosFil__ ] ]  == xFilial("SRV", RZ_FILIAL)  ) ;
					  		) .and. ;
					  		( x[ aSrvLanc[ __aSrvReturn_nPosPD__ ] ] == RZ_PD );
					}
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Monta Bloco para o aScan no aSrvPco                		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvPcoAsc	:= { |x|;
					  		(;
					  			(x[ aSrvPco[ __aSrvReturn_nPosFil__ ] ] == xFilial("SRV", RZ_FILIAL) );
					  		) .and. ;
					  		( x[ aSrvPco[ __aSrvReturn_nPosPD__ ] ] == RZ_PD );
					}

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega as Filiais que serao utilizadas para a Contabilizacao³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aFiliais := Gpem110FilCar( cEmpAnt , cFilConDe , cFilConAte  )
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Redefine o Conteudo das variavels cFilConDe e cFilConAte quan³
	³do modo de acesso do SRZ for compartilhado					   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( lSrzCompart )
		cFilConDe	:= xFilial( "SRZ" , cFilConDe 	)
		cFilConAte	:= xFilial( "SRZ" , cFilConAte	)
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Acertar valores do SRZ antes da contabilizacao               ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( ExistBlock( "GP110ACT" ) )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Existem Clientes que, para contabilizar Todas as filiais    em³
		³uma, mesmo com o Modo de Acesso do SRZ igual a Exclusivo, alte³
		³ram o conteudo do campo RZ_FILIAL para branco (Simulando um ar³
		³quivo compartilhado). Para prever essas situacoes, e, se o pon³
		³ponto de Entrada abaixo retornar conteudo correspondente a  fi³
		³lial a ser pesquisada e Contabilizada Array com dois Elementos³
		³Obs.: Recomendamos o uso da funcao SRZFilReplace, caso seja ne³
		³cessario estar dando Replace no campo RZ_FILIAL. A funcao deve³
		³ra ser chamada da seguinte forma:                             ³
		³SRZFilReplace( cFilReplace , [ bCondReplace ] )               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aFilContab := ExecBlock( "GP110ACT" , .F. , .F. )
		IF ( ( ValType( aFilContab ) == "A" ) .and. ( Len( aFilContab ) == 2 ) )
			cFilReplace := aFilContab[ 01 ]
			cFilContab	:= aFilContab[ 02 ]
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Se o Retorno do ExecBlock for Caracter e o Tamanho for Equiva³
			³ Lente ao Tamanho do RZ_FILIAL								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( ( ValType( cFilReplace ) == "C" ) .and. ( Len( cFilReplace ) == TamSx3("RZ_FILIAL")[1] ) ) .and.;
			   ( ( ValType( cFilContab  ) == "C" ) .and. ( Len( cFilContab  ) == TamSx3("RZ_FILIAL")[1] ) ) 
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Verifica se a Filial Onde sera Efetuada a Contabilizacao   eh³
				³ Valida													   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				IF ( aScan( aFiliais , { |x| x[1] == cFilContab } ) == 0.00 )
		 			cInconsist := ( STR0039 + cFilContab )	//"A Filial: " 
		 			cInconsist += STR0040					//",Retornada pelo Ponto de Entrada GP110ACT eh Invalida."
		 			aAdd( aIncLog , cInconsist )
		 			cInconsist := STR0041					//"Informe uma Filial valida para que possa ocorrer a contabilizacao."
					aAdd( aIncLog , cInconsist )
					ReabSRZ()
					Break
				EndIF
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Assume a Filial Retornada pelo ExecBlock                     ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cFilConAte := ( cFilConDe := cFilReplace )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Simulando o SRZ compartilhado                                ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				lSrzCompart	:= .T.
			EndIF
		EndIF
	EndIF
	
	#IFNDEF TOP
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega Regua de Processamento                               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( lGpem110ExecInRemote )
			ProcRegua( ( SRZ->( RecCount() ) + 1 ) )
		EndIF
	#ELSE
		For nField := 1 To nSrzFields
			cSrzFields += aSrzFields[ nField , 01 ] + ", "
		Next nField
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Retira a Ultima Virgula									   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cSrzFields := SubStr( cSrzFields , 1 , Len( cSrzFields ) - 2 )
	#ENDIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Define o Bloco para totalizacao                              ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bTotal := { || ;
	  				RodaProva(nHdlPrv,nTotal) ,;
	  				cA100Incl(cArquivo,nHdlPrv,3,cLote,lDigita,lAglut);
	  		  }
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Percorre Todas as Filiais Selecionadas nos Parametros De/Ate ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nFiliais := Len( aFiliais )
	For nFilial := 1 To nFiliais
	
		lCTBok	:= .T. 
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Altera o Conteudo de cFilAnt                                 ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF !Empty( cFilContab )
			cFilAnt := cFilContab
		Else
			cFilAnt := aFiliais[ nFilial , 01 ]
		EndIF	
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega conteudo dos parametros                              ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cMvCPosSrv	:= SuperGetMV("MV_CPOSSRV")
		cMvCPosSrv	:= If(Empty( cMvCPosSrv ), "S", cMvCPosSrv) 
		lMvCPosSrv	:= (cMvCPosSrv =="S")
		lMvCPosLcp	:= ( SuperGetMV("MV_CPOSLCP",NIL,"S") == "S" )
		lMvCPosCtt	:= ( SuperGetMV("MV_CPOSCC" ,NIL,"1" ) == "1" ) // 1-Posiciona no c.custo; 2-Nao posiciona no c.custo(utiliza PE)
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Reincializa aCT5 a Cada Filial                               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF !( lCt5Compart )
			aCT5 := {}
		EndIF
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Reincializa aPadrao a Cada Filial                            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( ( lCtbInUse ) .and. !( lCt5Compart ) )
			aPadrao := {}
			aLancPCO := {}
		ElseIF !( lSi5Compart )
			aPadrao := {}
			aLancPCO := {}
		EndIF
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Posiciona o SM0 na Filial Corrente                           ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		SM0->( MsGoto( aFiliais[ nFilial , 02 ] ) )
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega Regua de Processamento                               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( Empty( aSrvLanc ) .or. !( lSrvCompart ) )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Carrega as Verbas Vs Lancamentos Padroes. Se nao Existir,  Ge³
			³ ra Log													   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF !( lSrvLancOk := ( aSrvLanc := ( Gpem110SrvLanc( cFilAnt , cFilAnt , @aPadrao , lCtbInUse) ) )[__aSrvReturn_TudoOK__] )
				cInconsist := ( STR0025 + cFilAnt )	//" - Nao Existem Verbas a serem contabilizadas na filial: "
				cInconsist += STR0026				//" - Verifique se os Lancamentos Padroes estao definidos."
				aAdd( aIncLog , cInconsist )				
//				Loop
				lCTBok	:=	.F.
			Else
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Reinicializa lSelectPds									   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				lSelectPds := .T.
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Monta String Com as Verbas que serao Utilizadas na  Contabili³
				³ zacao														   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cSrvPds := ""
				nVerbas := Len( aSrvLanc[ __aSrvReturn_aCols__ ] )
				For nVerba := 1 To nVerbas
					cSrvPds += ( "'" + aSrvLanc[ __aSrvReturn_aCols__ , nVerba , aSrvLanc[ __aSrvReturn_nPosPD__ ] ] + "'," )
				Next nVerba
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Retira a Ultima Virgula									   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cSrvPds := SubStr( cSrvPds , 1 , Len( cSrvPds ) - 1 )
			EndIF
		EndIF

		/*Logica para carregar lancamentos do PCO*/
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega Regua de Processamento                               ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If lIntePco 
			IF ( Empty( aSrvPCO ) .or. !( lSrvCompart ) ) 
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Carrega as Verbas Vs Lancamentos PCO. Se nao Existir,  Ge³
				³ ra Log							 					   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				IF  !( lSrvPcoOk := ( aSrvPCO := ( Gpem110PCOSrv( cFilAnt , cFilAnt , @aLancPCO) ) )[__aSrvReturn_TudoOK__] )
					cInconsist := ( STR0025 + cFilAnt )	//" - Nao Existem Verbas a serem contabilizadas na filial: "
					cInconsist += STR0051
					aAdd( aIncLog , cInconsist )				
	//				Loop
					lPCOok	:=	.F.
				EndIF
			EndIF
		Else
			lPCOok	:=	.F.
			lSrvPcoOk := .F.
		Endif

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Se nao Conseguiu Carregar as Verbas x Lancamentos Padroes para³
		³a Filial pesquisa a proxima								   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF !( lSrvLancOk ) 
			cInconsist := ( STR0025 + cFilAnt )	//" - Nao Existem Verbas a serem contabilizadas na filial: "
			cInconsist += STR0026				//" - Verifique se os Lancamentos Padroes estao definidos."
			IF ( aScan( aIncLog , { |x| ( x == cInconsist ) } ) == 0.00 )
				aAdd( aIncLog , cInconsist )
			EndIF
		//	Loop
			lCTBok := .F.
		EndIF
		
		 		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Se nao Conseguiu Carregar as Verbas x Lancamentos Padroes PCOpara ³
		³a Filial pesquisa a proxima						     		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF !(lSrvPcoOK) .And. lIntePco
			cInconsist := ( STR0025 + cFilAnt )	//" - Nao Existem Verbas a serem contabilizadas na filial: "
			cInconsist += STR0051				//" - Verifique se os Lancamentos Padroes do PCO estao definidos."
			IF ( aScan( aIncLog , { |x| ( x == cInconsist ) } ) == 0.00 )
				aAdd( aIncLog , cInconsist )
			EndIF
		//	Loop
			lPCOok := .F.
		EndIF
		
		If !lCTBOk .AND. !lPCOok
			Loop
		Endif 
		
		If lCTBok	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Carrega o Numero do Lote Contabil                            ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF Empty( cLote := Gpem110LoteCont( "GPE" , cFilAnt ) )
				cInconsist := ( STR0027 + cFilAnt )		//" - Nao Existe o Lote Contabil GPE para a Filial: "
				cInconsist += STR0028					//" - Verifique no SX5 (Tabelas:Tabela 09) a Existencia do Lote."
				aAdd( aIncLog , cInconsist )
				If !lPcoOK
					Loop	
				Endif		
				lCtbOk	:=	.F.
			EndIF
		Endif
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Ponto Entrada Apos carregar o Numero do Lote contabil        ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/  
		If ExistBlock("GP110ANL")
		   EXECBLOCK("GP110ANL",.F.,.F.)
		Endif
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Inicializa Variaveis                                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		nContador := 0
		nTotal    := 0
		nHdlPrv   := 0
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Percorre todos os Registros do SRZ para a Filial corrente	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		dbSelectArea( "SRZ" )
		If lItemClVl
			dbSetOrder(3)
		Else
			dbSetOrder(1)
		Endif
		#IFNDEF TOP
			SRZ->( dbSeek( IF( !( lSrzCompart ) , cFilAnt , cFilConDe ) , .T. ) )
		#ELSE
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Obtem a Chave do Indice									   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cSRZIndexKey := SRZ->( IndexKey() )
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Monta a Query para selecao das informacoes do SRZ			   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cSrzQuery	:= "SELECT " 
			cSrzQuery	+= cSrzFields
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Ponto Entrada para incluir campos do SRV/CTT/SI3 na Query    ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If ExistBlock("GP110QRY")
			   aNewCpos := EXECBLOCK("GP110QRY",.F.,.F.)
			Endif
			
			For nElem := 1 To Len( aNewCpos )
				Do Case
					Case aNewCpos[nElem,1] == "SRV"
						For nCpos := 1 To Len( aNewCpos[nElem,2] )
							If aScan( aSrvFields, { |y| y[1] = aNewCpos[nElem,2,nCpos] } ) > 0
								cSrvCpos += ", " + aNewCpos[nElem,2,nCpos]
							EndIf
						Next nCpos
					Case aNewCpos[nElem,1] == If( lCtbInUse , "CTT" , "SI3" )
						For nCpos := 1 To Len( aNewCpos[nElem,2] )
							If aScan( aCcFields, { |y| y[1] = aNewCpos[nElem,2,nCpos] } ) > 0
								cCcCpos += ", " + aNewCpos[nElem,2,nCpos]
							EndIf
						Next nCpos
				EndCase
			Next nElem
			
			cSrzQuery	+= cSrvCpos + cCcCpos
			cQuery		:= " FROM "
			cQuery		+= cRetSqlName + " SRZ , " + InitSqlName( "SRV" ) + " SRV "
			If !Empty( cCcCpos )
				cQuery		+= ", " + InitSqlName( cAliasCc ) + " " + cAliasCc + " "
			EndIf
			cQuery		+= "WHERE "
			cQuery		+= "SRZ.RZ_FILIAL='" + IF( lSrzCompart , cFilConDe , cFilAnt ) + "'"
			cQuery		+= " AND " 
						
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Despreza Centros de Custo com 'zz...'						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cQuery		+= "SRZ.RZ_CC NOT IN("+ cZzsInRZCc + ")"
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Filtra as Matriculas                 						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( lExistMatSRZ ) 
		   		IF ( cEncInss == "S" ) 
					If lContabCC 
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Despreza os RZ_MAT sem 'zz..'                                ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			    		cQuery	+= " AND " 
						cQuery	+= "SRZ.RZ_MAT IN("+ cZzsInRZMat + ")" 
					Else 
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Despreza os RZ_MAT com 'zz..' quando cEndInss == "S"		   ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			    		cQuery	+= " AND " 
			    		cQuery	+= "SRZ.RZ_MAT NOT IN("+ cZzsInRZMat + ")"
		    		Endif 
		      	ElseIF ( cEncInss == "N" )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Despreza os RZ_MAT sem 'zz..' quando cEndInss == "N"		   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		    		cQuery	+= " AND "
		    		cQuery	+= "SRZ.RZ_MAT IN("+ cZzsInRZMat + ")"
				EndIF
			EndIF
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Folha, ou Se Solicitou e nao con³
			³segui Gerar a Folha										   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( !( lFolPgto ) .or. ( ( lFolPgto ) .and. !( lGerouFol ) ) )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Despreza Todos os Lancamentos para o Tipo Folha "FL"          ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cQuery	+= " AND "
				cQuery	+= "SRZ.RZ_TIPO NOT IN('FL')"
	        EndIF
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Folha de 13o. Salario, ou Se  So³
			³licitou e nao Consegui Gerar Informacoes de 13o. Salario	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( !( lFol13Sl ) .or. ( ( lFol13Sl ) .and. !( lGerou13s ) ) )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Despreza Todos os Lancamentos para o Tipo 13o. Salario "13"   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cQuery	+= " AND "
				cQuery	+= "SRZ.RZ_TIPO NOT IN('13')"
	        EndIF
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Provisao de Ferias              ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF !( lProvFer )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Despreza Todos os Lancamentos para o Tipo Provisao de FErias  ³
				³"PF"  													       ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cQuery	+= " AND "
				cQuery	+= "SRZ.RZ_TIPO NOT IN('PF')"
			ENDIF
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Provisao de 13o.Salario         ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF !( lProv13o )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³Despreza Todos os Lancamentos para o Tipo Provisao de 13o.  Sa³
				³lario														   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cQuery	+= " AND "
				cQuery	+= "SRZ.RZ_TIPO NOT IN('PD')"
			EndIF
			cQuery		+= " AND "
			cQuery		+= "SRZ.D_E_L_E_T_=' ' "
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Monta a Query para selecao das Verbas                         ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cQueryPds	:= " AND "
			cQueryPds	+= "SRZ.RZ_PD = SRV.RV_COD AND "
			If !lSrvCompart
				cQueryPds	+= "SRZ.RZ_FILIAL = SRV.RV_FILIAL AND "
			Else
				cQueryPds	+=	"SRV.RV_FILIAL = '" + xFilial('SRV',If(cFilConDe==Space(FWGETTAMFILIAL),cFilAnt,cFilConDe)) +"' AND "
			Endif
			cQueryPds	+= "SRV.RV_LCTOP <> '   ' AND "
			cQueryPds	+= "SRV.D_E_L_E_T_=' '"
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Adiciona relacionamento com centro de custo                   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If !Empty( cCcCpos )
				cQueryCc	:= " AND "
				cQueryCc	+= "SRZ.RZ_CC = " + If( lCtbInUse, "CTT.CTT", "SI3.I3" ) + "_CUSTO AND "
				
				If !lCcCompart
					cQueryCc	+= "SRZ.RZ_CC = " + If( lCtbInUse, "CTT.CTT", "SI3.I3" ) + "_FILIAL AND "
				Endif
				
				cQueryCc	+= cAliasCc + ".D_E_L_E_T_ = ' ' "
			EndIf

			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Monta a Query para selecao da Ordem                           ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cQueryOrd	:= " ORDER BY " + SqlOrder( cSRZIndexKey )
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Seleciona as Verbas										   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			cSrzQuery	:= ChangeQuery( ( cSrzQuery + cQuery + cQueryPds + cQueryCc + cQueryOrd ) )
			
			SRZ->( dbCloseArea() ) //Fecha o SRZ para uso da Query
			IF ( MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cSrzQuery),"SRZ",.T.,.T.) )
				// Inclui campos do SRV adicionados via Ponto de Entrada
				If !Empty( cSrvCpos )
					aEval( aSrvFields, { |x| If( x[2] <> "C" .And. x[1] $ cSrvCpos, Eval( { |y| aAdd( aSrzFields, y ), nSrzFields++ }, x ), ) } )
				EndIf
				// Inclui campos do CTT/SI3 adicionados via Ponto de Entrada
				If !Empty( cCcCpos )
					aEval( aCcFields,  { |x| If( x[2] <> "C" .And. x[1] $ cCcCpos,  Eval( { |y| aAdd( aSrzFields, y ), nSrzFields++ }, x ), ) } )
				EndIf
				For nField := 1 To nSrzFields
					IF ( aSrzFields[ nField , 2 ] <> "C" )
						TcSetField("SRZ",aSrzFields[nField,1],aSrzFields[nField,2],aSrzFields[nField,3],aSrzFields[nField,4])
					EndIF
				Next nField
				// Restaura estrutura do SRZ e sua qtde. de itens originais
				aSrzFields	:= aClone( aOldSRZ )
				nSrzFields	:= nOldSRZ
				IF ( lGpem110ExecInRemote )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Verifica o Total de Registros a Serem Processados            ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					cSrzQuery	:= "SELECT COUNT(*) NLASTREC "
					cSrzQuery	+= cQuery
					cSrzQuery	+= IF( lSelectPds , cQueryPds , "" )
					cSrzQuery	:= ChangeQuery( cSrzQuery )
		        	IF ( MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cSrzQuery),"__QRYCOUNT",.T.,.T.) )
						nLastRec := __QRYCOUNT->NLASTREC
						__QRYCOUNT->( dbCloseArea() )
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Redefine o Total da Regua de Processamento     			   ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						ProcRegua( ( nLastRec + 1 ) )
					EndIF
				EndIF
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Reinizializa as variaveis de Querys						   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cSrzQuery	:= ""
				cQuery		:= ""
				cQueryTmp	:= ""
				cQueryPds	:= ""
				cQueryCc	:= ""
				cQueryOrd	:= ""
			Else
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Reinizializa as variaveis de Querys						   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cSrzQuery	:= ""
				cQuery		:= ""
				cQueryTmp	:= ""
				cQueryPds	:= ""
				cQueryCc	:= ""
				cQueryOrd	:= ""
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Carrega a Inconsistencia            						   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	         	cInconsist := ( STR0035 +  cFilAnt )	//"- Nao foi possivel abrir o SRZ da Filial: "
				cInconsist += STR0036					//"- A contabilizacao dessa filial nao foi efetuada"
			 	aAdd( aIncLog , cInconsist )
				Loop
			EndIF
		#ENDIF	
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Inicializa a Mensagem da Regua de Processamento			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( lGpem110ExecInRemote )
			IncProc( OemToAnsi( STR0018 + cFilAnt ) ) //"Processando a Contabilizacao para a Filial: "
		EndIF	
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Percorre todos os Registros do SRZ para a Filial corrente	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		While SRZ->( !Eof() .and. ( RZ_FILIAL == IF( lSrzCompart , cFilConDe , cFilAnt ) ) )
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Movimentando Regua de Processamento                          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( lGpem110ExecInRemote )
				IncProc()
			EndIF	
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Se Contabilizar Por Funcionarios                             ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( lExistMatSRZ )
				#IFNDEF TOP
					If  ( cEncInss == "S" ) 
						If ( lContabCC .and.  !( SRZ->RZ_MAT $ cZzsInRZMat ) ) .or.  ;  	//-- Despreza os RZ_MAT com 'zz..' quando cEncInss == "S"
						   (!lContabCC .and.  ( SRZ->RZ_MAT $ cZzsInRZMat ) ) 			//-- Despreza os RZ_MAT sem 'zz..' quando cEncInss == "S"
							SRZ->( dbskip() ) 
							Loop
      	    			Endif
					Else
                    
						/*
						ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						³ Despreza os RZ_MAT sem 'zz..' quando cEndInss == "N"		   ³
						ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
						IF  !( SRZ->RZ_MAT $ cZzsInRZMat ) 
			    	   		SRZ->( dbskip() )
			      			Loop
				      	EndIF
					Endif
			      	
				#ENDIF
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Posicona SRA na Matricula que esta sendo processada		   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		      	SRA->( MsSeek( SRZ->( RZ_FILIAL + RZ_MAT ) ) )
			EndIF
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Nao Contabiliza para Filial, CC e PD iguais a zz... 		   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			#IFNDEF TOP
				IF SRZ->( ( RZ_FILIAL $ cZzsInRZFil ) .or. ( RZ_CC $ cZzsInRZCc ) .or. ( RZ_PD $ cZzsInRZPd ) )
					SRZ->( dbskip() )
					Loop
				EndIF
			#ENDIF	
		
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Folha, ou Se Solicitou e nao con³
			³segui Gerar a Folha										   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			#IFNDEF TOP
				IF ( !( lFolPgto ) .or. ( ( lFolPgto ) .and. !( lGerouFol ) ) )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Despreza Todos os Lancamentos para o Tipo Folha "FL"          ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					IF ( SRZ->RZ_TIPO == "FL" )
						SRZ->( dbskip() )
						Loop
					EndIF
				EndIF
			#ENDIF		
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se Nao Solicitou a Geracao da Folha de 13o. Salario, ou Se  So³
			³licitou e nao Consegui Gerar Informacoes de 13o. Salario	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			#IFNDEF TOP
				IF ( !( lFol13Sl ) .or. ( ( lFol13Sl ) .and. !( lGerou13s ) ) )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Despreza Todos os Lancamentos para o Tipo 13o. Salario "13"   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					IF ( SRZ->RZ_TIPO == "13" )
						SRZ->( dbskip() )
						Loop                                          
					EndIF
				EndIF
			#ENDIF    
		
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica se Gerou as Provisoes de Ferias               	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			#IFNDEF TOP
				IF !( lProvFer ) 
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Despreza Todos os Lancamentos para Provisao de Ferias "PD"    ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					IF ( SRZ->RZ_TIPO == "PF" )
						SRZ->( dbskip() )
						Loop
					EndIF
				EndIF
			#ENDIF	
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica se Gerou as Provisoes de 13o. Salario         	   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	        #IFNDEF TOP
		        IF !( lProv13o )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³Despreza Todos os Lancamentos para Provisao 13o.Salario "PD"  ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		        	IF ( SRZ->RZ_TIPO == "PD" )
						SRZ->( dbskip() )
						Loop
		        	EndIF
		        EndIF
			#ENDIF
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Carrega o Lancamento Padrao da Verba						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			lCtbOk := ( ( nPosPadrao := SRZ->( aScan( aSrvLanc[ __aSrvReturn_aCols__ ] , bSrvLancAsc ) ) ) > 0.00 )
			
			If lIntePco
				lPCOOk := ( ( nPosPCO := SRZ->( aScan( aSrvPCO[ __aSrvReturn_aCols__ ] , bSrvPcoAsc ) ) ) > 0.00 )
			Else
				lPCOOk := .F.
			EndIf
			
			If !lCTBok .And. !lPCOok 
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Nao Contabiliza Verbas sem Lancamento Padrao				   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				#IFNDEF TOP
					SRZ->( dbskip() )
					Loop
				#ELSE
					IF !( lSelectPds )
						SRZ->( dbskip() )
						Loop
					EndIF
				#ENDIF	
			EndIF
	
			
			IF lCTBok
				cPadrao := aSrvLanc[ __aSrvReturn_aCols__ , nPosPadrao , aSrvLanc[ __aSrvReturn_nPosLctop__ ] ]
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Verifica se deve Posicionar o SRV. O posisionamento eh  desne³
				³ cessario uma vez que ja se obteve o Lancamento Padrao para  a³
				³ verba. Foi mantido apenas para compatibilidade               ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			    IF ( lMvCPosSrv )	
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Posiciona no SRV                    						   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				    SRV->( MsGoto( aSrvLanc[ __aSrvReturn_aRecnos__ , nPosPadrao ] ) )
				EndIF
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Verifica se Deve Posicionar o Lancamento Padrao. O  Posiciona³
				³ mento no Lancamento Padrao eh Garantido pela Funcao DetProva.³
				³ Sendo assim, aqui ele se torna descecessario. Foi mantido ape³
				³ nas para compatibilidade.									   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				IF ( lMvCPosLcp )	
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Posiciona no Lancamento Padrao      						   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					( aSrvLanc[ __aSrvReturn_cAliasLctop__ ] )->( MsGoto( aSrvLanc[ __aSrvReturn_aRecLctop__ , nPosPadrao ] ) )
				EndIF
			EndIf
			
			IF lIntePco .And. lPCOok
				cLancPCO := aSrvPCO[ __aSrvReturn_aCols__ , nPosPCO , aSrvPco[ __aSrvReturn_nPosLctop__ ] ]
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Verifica se deve Posicionar o SRV. O posisionamento eh  desne³
				³ cessario uma vez que ja se obteve o Lancamento Padrao para  a³
				³ verba. Foi mantido apenas para compatibilidade               ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			    IF ( lMvCPosSrv )	
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Posiciona no SRV                    						   ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				    SRV->( MsGoto( aSrvPCO[ __aSrvReturn_aRecnos__ , nPosPCO ] ) )
				EndIF
			EndIf
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Verifica se deve Posicionar o centro de custo (CTT ou SI3).  ³
			³ Se for utilizado o ponto de entrada GP110QRY o posicionamento³
			³ do centro de custo eh desnecessario                          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		    IF ( lMvCPosCtt )	
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Posicona Centro de Custo que esta sendo processado    	   ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		   	  	IF !( ( cAliasCc )->( MsSeek( SRZ->( xFilial( cAliasCc , RZ_FILIAL ) + RZ_CC ) , .F. ) ) )
					cInconsist := STR0029 + SRZ->RZ_CC + STR0030 + cFilAnt + STR0031 + cAliasCc +"."	//" - O Centro de Custo: "###" da Filial: "###" nao foi encontrado no: "
					cInconsist += STR0032																//" - Devera ser Cadastrado para que possa ocorrer a contabiliacao."
					IF ( aScan( aIncLog , { |x| ( x == cInconsist ) } ) == 0.00 )
						aAdd( aIncLog , cInconsist )
					EndIF
					SRZ->( dbskip() )
					Loop
		   	  	EndIF
		   EndIf
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Incrementa o Contador de Lancamentos        				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( ( ++nContador ) == 1 )
				nHdlPrv := HeadProva(cLote,"GPEM110",Substr(cUsuario,7,6),@cArquivo)
			EndIF
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Chamada a DetProva                          				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			dbSelectArea( "SRZ" )
			nTotal += DetProva(nHdlPrv,cPadrao,"GPEM110",cLote,,,,,,@aCT5)
	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Escrituracao dos movimentos - Integracao com modulo PCO     ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF lIntePCO .And. lPCOok .And. !Empty(cLancPCO)
				PcoDetLan(__LCTO_PCO_FOLHA__, cLancPCO, "GPEM110")
            EndIf
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Proximo Lancamento                          				   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			SRZ->( dbskip() )
	
		End While
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Totalizadores                               				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( nTotal > 0 )
	  		IF ( lGpem110ExecInRemote )
	  			MsAguarde( bTotal , STR0038 + cFilAnt ) // "Totalizando a Filial: "
			Else
				Eval( bTotal )
			EndIF
		EndIF
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Restaura o SRZ padrao										   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		#IFDEF TOP
			IF ( Select("SRZ") > 0.00 )
				SRZ->( dbCloseArea() )
				ChkFile( "SRZ" )
				RestArea( aAreaSRZ )
			EndIF
		#ENDIF
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Se o SRZ for compartilhjado, ja processou tudo na 1a. Vez	   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If ( lSrzCompart )		// ja processou tudo na primeira vez
			Exit
		EndIF

		//--GRAVA NO PARAMETRO O MES CONTABILIZADO
		If GetMv("MV_GPMESCT",.T.) .And. cMesCont < Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2) 
			PutMv("MV_GPMESCT", Right(cMesAnoCtab,4)+Left(cMesAnoCtab,2) )
		EndIf
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Se existir integracao com LOGIX							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If SuperGetMv("MV_RHCONEX",,.F.)   
			Gp110IntLogix(cMesAnoCtab)
		EndIf
		
	Next nFilial
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Finaliza Processo Lancamento - Integracao com modulo PCO     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	PcoFinLan(__LCTO_PCO_FOLHA__)


End Sequence
	
ReabSRZ()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura a Filial de Entrada                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFilAnt := cSvFilAnt

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura o Modulo de Entrada                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF !Empty( aModuloReSet )
	ReSetModulo( aModuloReSet )
EndIF	

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Recupera a Integridade dos dados                             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RestArea( aAreaSRA )
RestArea( aAreaSI3 )
RestArea( aAreaSRZ )
RestArea( aAreaSM0 )
RestArea( aArea    )

Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpem110LoteCont³Autor³Marinaldo de Jesus  ³ Data ³29/05/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna o Numero do Lote da Folha no SX5					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Gpem110LoteCont( cChave , cFil )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso 	 	 ³GPEM110     												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpem110LoteCont( cChave , cFil )
DEFAULT cChave	:= "GPE"
DEFAULT cFil	:= cFilAnt
If CtbInUse()
	Return( fDesc( "SX5" , "09"+AllTrim(cChave) , "X5Descri()" , 06 , cFil , 01 ) )
Else
	Return( fDesc( "SX5" , "09"+AllTrim(cChave) , "X5Descri()" , 04 , cFil , 01 ) )
EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpem110SrvLanc ³Autor³Marinaldo de Jesus  ³ Data ³29/05/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Carrega Array das Verbas Vs Lancamentos Padroes			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³<Vide Parametros Formais>              				  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso 	     ³GPEM110     												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpem110SrvLanc( cSrvFilDe , cSrvFilAte , aPadrao , lCtbInUse )

Local aSrvReturn	:= Array( __Elementos_aSrvReturn__ )
Local aSrvCols		:= {}
Local aSrvHeader	:= {}
Local aSrvRecnos	:= {}
Local aSrvFields	:= { "RV_FILIAL", "RV_COD" , "RV_LCTOP" }
Local aSrvQuery 	:= {}
Local aRecLctop		:= {}
Local bSrvWhile		:= { |cKey,cFil| ( ( cFil := RV_FILIAL ) >= cSrvFilDe ) .and. ( cFil <= cSrvFilAte ) }
Local bSrvSkip		:= { || NIL }
Local cAliasLctop	:= IF( lCtbInUse , "CT5" , "SI5" )
Local cCpoLctop		:= IF( ( cAliasLctop == "CT5" ) , "CT5_LANPAD" , "I5_CODIGO" )
Local cFilLctop		:= xFilial( cAliasLctop )
Local cPrefixLctop	:= ( PrefixoCpo( cAliasLctop ) + "_" )
Local lTudoOk		:= .F.
Local nPosFil		:= 0.00
Local nPosPd		:= 0.00
Local nPosLctop		:= 0.00
Local nRecnoLctop	:= 0.00
Local lEqualFil		:= .F.

#IFDEF TOP
	Local cSpcLctop	:= Space( TamSx3("RV_LCTOP")[1] )
#ENDIF

DEFAULT cSrvFilDe	:= cFilAnt
DEFAULT cSrvFilAte	:= cSrvFilDe
DEFAULT aPadrao		:= {}
DEFAULT lCtbInUse	:= CtbInUse()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Redefine as Filiais De/Ate                   				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cSrvFilDe	:= xFilial( "SRV" , cSrvFilDe  )
cSrvFilAte	:= xFilial( "SRV" , cSrvFilAte )
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Anula bSrvWhile caso as Filiais sejam Iguais				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lEqualFil := ( cSrvFilDe == cSrvFilAte ) )
	bSrvWhile := NIL
EndIF

#IFDEF TOP
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Redefine bSrvSkip Quando TOP                 				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvSkip := { |lVerPadrao|	( lVerPadrao := ( MyVerPadrao( RV_LCTOP , @nRecnoLctop , @aPadrao , cAliasLctop , cFilLctop , cCpoLctop , cPrefixLctop ) ) ) ,;
								IF(;
									( lVerPadrao ) ,;
									aAdd( aRecLctop , nRecnoLctop ),;
									 NIL;
								   ),;
								 !( lVerPadrao );
				}
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Condicao da Query Para Carga das Informacoes do SRV		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aSrvQuery	  := Array( 09 )
	aSrvQuery[01] := "("
	aSrvQuery[02] := 	"RV_FILIAL>='"+cSrvFilDe+"'"
	aSrvQuery[03] := 	" AND "
	aSrvQuery[04] := 	"RV_FILIAL<='"+cSrvFilAte+"'"
	aSrvQuery[05] := ")"
	aSrvQuery[06] := " AND "
	aSrvQuery[07] := "RV_LCTOP<>'"+cSpcLctop+"'"
	aSrvQuery[08] := " AND "
	aSrvQuery[09] := "D_E_L_E_T_=' ' "
#ELSE
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Redefine bSrvSkip Quando Nao TOP             				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvSkip := { |lVerPadrao| IF(;
									!Empty( RV_LCTOP ),;
									(;
										( lVerPadrao := ( MyVerPadrao( RV_LCTOP , @nRecnoLctop , @aPadrao , cAliasLctop ) ) ) ,;
										IF(;
											( lVerPadrao ) ,;
												aAdd( aRecLctop , nRecnoLctop ),;
								 				NIL;
						   				   ),;
										!( lVerPadrao );
									),;	
									.T.;
								  );
				  }
#ENDIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega as Informacoes do SRV                				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aSrvCols := GdMontaCols(;
							@aSrvHeader	,;	//01 -> Array com os Campos do Cabecalho da GetDados
							NIL			,;	//02 -> Numero de Campos em Uso
							NIL			,;	//03 -> [@]Array com os Campos Virtuais
							NIL			,;	//04 -> [@]Array com os Campos Visuais
							"SRV"		,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							aSrvFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aSrvRecnos	,;	//07 -> [@]Array unidimensional contendo os Recnos
							"SRV"   	,;	//08 -> Alias do Arquivo Pai
							cSrvFilDe	,;	//09 -> Chave para o Posicionamento no Alias Filho
							bSrvWhile	,;	//10 -> Bloco para condicao de Loop While
							bSrvSkip	,;	//11 -> Bloco para Skip no Loop While
							.F.			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							.F.			,;	//13 -> Se cria variaveis Publicas
							.F.			,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL			,;	//15 -> Lado para o inicializador padrao
							.F.			,;	//16 -> Opcional, Carregar Todos os Campos
							.T. 		,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							aSrvQuery	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							.T.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.			,;	//21 -> Carregar Coluna Fantasma
							.T.			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							.F.			,;	//23 -> Verifica se Deve verificar se o campo eh usado
							.F.			,;	//24 -> Verifica se Deve verificar o nivel do usuario
							.F.	 		 ;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
					)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Se carregou as Verbas Vs Lancamentos                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lTudoOk := ( Len( aSrvRecnos ) > 0.00 ) )
	nPosFil		:= GdFieldPos( "RV_FILIAL"	, aSrvHeader )
	nPosPd		:= GdFieldPos( "RV_COD"		, aSrvHeader )
	nPosLctop	:= GdFieldPos( "RV_LCTOP"	, aSrvHeader )
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Alimenta o aSrvReturn                        				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aSrvReturn[ __aSrvReturn_TudoOK__ 		] := lTudoOk
aSrvReturn[ __aSrvReturn_aCols__		] := aClone( aSrvCols )
aSrvReturn[ __aSrvReturn_aHeader__		] := aClone( aSrvHeader )
aSrvReturn[ __aSrvReturn_aRecnos__		] := aClone( aSrvRecnos )
aSrvReturn[ __aSrvReturn_nPosFil__		] := nPosFil
aSrvReturn[ __aSrvReturn_nPosPD__		] := nPosPd
aSrvReturn[ __aSrvReturn_nPosLctop__	] := nPosLctop
aSrvReturn[ __aSrvReturn_aRecLctop__	] := aClone( aRecLctop )
aSrvReturn[ __aSrvReturn_cAliasLctop__	] := cAliasLctop

Return( aClone( aSrvReturn ) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpem110PCOSrv  ³Autor³Marinaldo de Jesus  ³ Data ³31/10/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Carrega Array das Verbas Vs Lancamentos Padroes do PCO	  ³±±
±±³          ³Baseado na funcao Gpem110SrvLanc() para manter a mesma      ³±±
±±³          ³estrutura do array com relacao a lanctos padroes CTB        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³<Vide Parametros Formais>              				  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso 	     ³GPEM110     												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpem110PCOSrv( cSrvFilDe , cSrvFilAte , aLancPCO )

Local aSrvReturn	:= Array( __Elementos_aSrvReturn__ )
Local aSrvCols		:= {}
Local aSrvHeader	:= {}
Local aSrvRecnos	:= {}
Local aSrvFields	:= { "RV_FILIAL", "RV_COD" , "RV_LANCPCO" }
Local aSrvQuery 	:= {}
Local aRecLcPco		:= {}
Local bSrvWhile		:= { |cKey,cFil| ( ( cFil := RV_FILIAL ) >= cSrvFilDe ) .and. ( cFil <= cSrvFilAte ) }
Local bSrvSkip		:= { || NIL }
Local cAliasLcPco	:= "AKC"
Local cCpoLcPco		:= "AKC_SEQ"
Local cFilLcPco		:= xFilial( cAliasLcPco )
Local cPrefixLcPco	:= ( PrefixoCpo( cAliasLcPco ) + "_" )

Local lTudoOk		:= .F.
Local nPosFil		:= 0.00
Local nPosPd		:= 0.00
Local nPosLcPco		:= 0.00
Local nRecnoLcPco	:= 0.00


Local lEqualFil		:= .F.

#IFDEF TOP
	Local cSpcLcPco	:= Space( TamSx3("RV_LANCPCO")[1] )
#ENDIF

DEFAULT cSrvFilDe	:= cFilAnt
DEFAULT cSrvFilAte	:= cSrvFilDe
DEFAULT aLancPCO	:= {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Redefine as Filiais De/Ate                   				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cSrvFilDe	:= xFilial( "SRV" , cSrvFilDe  )
cSrvFilAte	:= xFilial( "SRV" , cSrvFilAte )
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Anula bSrvWhile caso as Filiais sejam Iguais				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lEqualFil := ( cSrvFilDe == cSrvFilAte ) )
	bSrvWhile := NIL
EndIF

#IFDEF TOP

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Redefine bSrvSkip Quando TOP                 				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvSkip := { |lVerPadrao|	( lVerPadrao := ( MyVerLancPCO( RV_LANCPCO , @nRecnoLcPco , @aLancPCO , cAliasLcPco , cFilLcPco , cCpoLcPco , cPrefixLcPco ) ) ) ,;
								IF(;
									( lVerPadrao ) ,;
									aAdd( aRecLcPco , nRecnoLcPco ),;
									 NIL;
								   ),;
								 !( lVerPadrao );
				}
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Condicao da Query Para Carga das Informacoes do SRV		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aSrvQuery	  := Array( 09 )
	aSrvQuery[01] := "("
	aSrvQuery[02] := 	"RV_FILIAL>='"+cSrvFilDe+"'"
	aSrvQuery[03] := 	" AND "
	aSrvQuery[04] := 	"RV_FILIAL<='"+cSrvFilAte+"'"
	aSrvQuery[05] := ")"
	aSrvQuery[06] := " AND "
	aSrvQuery[07] := "RV_LANCPCO<>'"+cSpcLcPco+"'"
	aSrvQuery[08] := " AND "
	aSrvQuery[09] := "D_E_L_E_T_=' ' "
#ELSE
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Redefine bSrvSkip Quando Nao TOP             				   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	bSrvSkip := { |lVerPadrao| IF(;
									!Empty( RV_LANCPCO ),;
									(;
										( lVerPadrao := ( MyVerLancPCO( RV_LANCPCO , @nRecnoLcPco , @aLancPCO , cAliasLcPco ) ) ) ,;
										IF(;
											( lVerPadrao ) ,;
												aAdd( aRecLcPco , nRecnoLcPco ),;
								 				NIL;
						   				   ),;
										!( lVerPadrao );
									),;	
									.T.;
								  );
				  }
#ENDIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega as Informacoes do SRV                				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aSrvCols := GdMontaCols(;
							@aSrvHeader	,;	//01 -> Array com os Campos do Cabecalho da GetDados
							NIL			,;	//02 -> Numero de Campos em Uso
							NIL			,;	//03 -> [@]Array com os Campos Virtuais
							NIL			,;	//04 -> [@]Array com os Campos Visuais
							"SRV"		,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
							aSrvFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
							@aSrvRecnos	,;	//07 -> [@]Array unidimensional contendo os Recnos
							"SRV"   	,;	//08 -> Alias do Arquivo Pai
							cSrvFilDe	,;	//09 -> Chave para o Posicionamento no Alias Filho
							bSrvWhile	,;	//10 -> Bloco para condicao de Loop While
							bSrvSkip	,;	//11 -> Bloco para Skip no Loop While
							.F.			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
							.F.			,;	//13 -> Se cria variaveis Publicas
							.F.			,;	//14 -> Se Sera considerado o Inicializador Padrao
							NIL			,;	//15 -> Lado para o inicializador padrao
							.F.			,;	//16 -> Opcional, Carregar Todos os Campos
							.T. 		,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
							aSrvQuery	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
							.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
							.T.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
							.F.			,;	//21 -> Carregar Coluna Fantasma
							.T.			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
							.F.			,;	//23 -> Verifica se Deve verificar se o campo eh usado
							.F.			,;	//24 -> Verifica se Deve verificar o nivel do usuario
							.F.	 		 ;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
					)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Se carregou as Verbas Vs Lancamentos                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lTudoOk := ( Len( aSrvRecnos ) > 0.00 ) )
	nPosFil		:= GdFieldPos( "RV_FILIAL"	, aSrvHeader )
	nPosPd		:= GdFieldPos( "RV_COD"		, aSrvHeader )
	nPosLcPco	:= GdFieldPos( "RV_LANCPCO"	, aSrvHeader )
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Alimenta o aSrvReturn                        				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aSrvReturn[ __aSrvReturn_TudoOK__ 		] := lTudoOk
aSrvReturn[ __aSrvReturn_aCols__		] := aClone( aSrvCols )
aSrvReturn[ __aSrvReturn_aHeader__		] := aClone( aSrvHeader )
aSrvReturn[ __aSrvReturn_aRecnos__		] := aClone( aSrvRecnos )
aSrvReturn[ __aSrvReturn_nPosFil__		] := nPosFil
aSrvReturn[ __aSrvReturn_nPosPD__		] := nPosPd
aSrvReturn[ __aSrvReturn_nPosLctop__	] := nPosLcPco
aSrvReturn[ __aSrvReturn_aRecLctop__	] := aClone( aRecLcPco )
aSrvReturn[ __aSrvReturn_cAliasLctop__	] := cAliasLcPco

Return( aClone( aSrvReturn ) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gpem110FilCar  ³Autor³Marinaldo de Jesus  ³ Data ³29/05/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Carrega Array com as Filiais que serao contabilizadas		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³<Vide Parametros Formais>              				  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso 	     ³GPEM110     												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gpem110FilCar( cEmp , cFilDe , cFilAte  )
Return( GetFilRecnos( cEmp , cFilDe , cFilAte ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fGeraFolSR³ Autor ³ Emerson Rosa de Souza	³ Data ³ 18/08/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera o arquivo SRZ com os lancamentos da folha			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fGeraFolSRZ(nFol13S,aTpContr,cMesAnoRef)				      |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nFol13S    - Tipo de lancamento FOLHA ou 13 SALARIO		  |±±
±±³          ³ aTpContr   - Tipo de Contrato DET/INDET ou AMBOS			  |±±
±±³          ³ cMesAnoRef - Mes e Ano de geracao dos lancamentos 	      |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGeraFolSRZ(nFol13S,aTpContr,cMesAnoRef, nParcela13)
Local aArea		:= GetArea()
Local lRet 		  := .T.
Local cMesArqRef  := If(nFol13S == 2,"13"+Right(cMesAnoRef,4),cMesAnoRef)
Local cTipoLanc,cDescLanc,cStrGeracao,nNroContr,lLimpaSRZ:=.F.
Local cChaveUnica := "RZ_FILIAL+RZ_CC+RZ_MAT+RZ_PD+RZ_TPC+RZ_TIPO"
Local nTamCC	  := TamSx3("RZ_CC")[1]
Local lExecProc   := .F.

Private aInfo := {}
Private lItemClVl := SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "13"
Private lSPGP110  := SuperGetMv("MV_SPGP110",,.F.)

DEFAULT nParcela13 := 2
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas na funcao GR040Imp() para gerar o SRZ   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSit	  := " ADFT"
cCat      := "ACDEGHIJMPST"
Semana    := If(cPaisLoc <> "DOM","99",cSemana)
If lItemClVl
	cChaveUnica := "RZ_FILIAL+RZ_CC+RZ_ITEM+RZ_CLVL+RZ_MAT+RZ_PD+RZ_TPC+RZ_TIPO"
Endif
If nFol13S == 1  // 1-Folha  4-Decimo Terceiro 2a Parcela
	nRelat    := 2
Else 
	nRelat    := If( nParcela13 == 1, 3, 4)
EndIf	
lImpNiv   := lImpTot := .F.
nOrdem    := If(lItemClVl,7,4)                      // 4 - Ordem de Centro de Custo do Movimento / 7 - c.custo/item/classe
cFilDe    := Space(FWGETTAMFILIAL)
cFilAte   := Replicate("z", FWGETTAMFILIAL)
cCcDe     := Space(9)
cCcAte    := Replicate("z",09)
cMatDe    := Space(6)
cMatAte   := Replicate("z",06)
cNomDe    := Space(30)
cNomAte   := Replicate("z",30)

cTipoLanc := If(nFol13S == 1, "FL", "13")
cDescLanc := Substr(cTipoLanc + Space(1) + cMesAnoRef + space(nTamCC),1,nTamCC)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Alterar parametros da geracao de lancamentos para o SRZ		 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("GP110APL")
   EXECBLOCK("GP110APL",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa ajuste na chave unica da tabela SRZ                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SX2" )
If dbSeek( "SRZ" ) .And. RTrim( X2_UNICO ) # cChaveUnica
	RecLock( "SX2",.F.)
	X2_UNICO := cChaveUnica
	MsUnLock()
EndIf

For nNroContr := 1 To Len(aTpContr)

	cTpc  := Str(aTpContr[nNroContr],1)
	cTpc1 := If(cTpc == "1", " *1",cTpc)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o texto para a geracao dos lancamentos                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nFol13S == 1 .and. cTpc == "1"
		cStrGeracao := STR0019  // "Geracao de Lancamentos - Folha/Indeterminado"
	ElseIf nFol13S == 1 .and. cTpc == "2"
		cStrGeracao := STR0020  // "Geracao de Lancamentos - Folha/Determinado"
	ElseIf nFol13S == 2 .and. cTpc == "1"
		cStrGeracao := STR0021  // "Geracao de Lancamentos - 13o Sal/Indeterminado"
	ElseIf nFol13S == 2 .and. cTpc == "2"
		cStrGeracao := STR0022  // "Geracao de Lancamentos - 13o Sal/Determinado"
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gerar SRZ caso nao tenha sido gerado ou a folha recalculada  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SRZ")
	dbSetOrder(1)
	If (cPaisLoc $ "DOM/URU") .Or. lSPGP110 .Or. ( !lSPGP110 .And. !SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + cTpc ) ) )
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Devera limpar tambem, se a geracao dos lancamentos for no mes³
		//³ seguinte ao mes encontrado no arquivo.						 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lLimpaSRZ := .F.
		If SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cTipoLanc , .F. ) ) .and.; 
		   Right(cMesAnoRef,4)+Left(cMesAnoRef,2) > Substr(SRZ->RZ_CC,6,4)+Substr(SRZ->RZ_CC,4,2)
		   lLimpaSRZ := .T.
		EndIf
        
	    /*
	    ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Deleta os Registros no SRZ para nova contabilizacao de Folha  |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		DeleteSrzTop(	cTpc		,;	//Conteudo para comparar com RZ_TPC
						cTipoLanc	,;	//Tipo de Lancamento a comparar com RZ_TIPO
						lLimpaSRZ	,;	//Se Limpa Todos os Tipos do SRZ
						.F.			,;	//Se se refere a Provisoes
						.F.			 ;	//Se mostra Dialogo dom barra de evolucao
					 )
	    
	    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gera lancamentos no SRZ a partir da rotina de imp. da folha  |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Processa({|| lRet := GR040Imp(,,"SRZ",cMesAnoRef,aTpContr[nNroContr],.T.,@lExecProc)},cStrGeracao,,.F.)
		dbSelectArea("SRZ")
		If lItemClVl
			dbSetOrder(3)
		Else
			dbSetOrder(1)
		Endif
		If !lRet
			Exit
		ElseIf !lExecProc
		    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gera um registro indicando mes e tipo de lancamento gerado.  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock("SRZ",.T.)
			SRZ->RZ_FILIAL := Replicate("z", FWGETTAMFILIAL)
			SRZ->RZ_CC     := cDescLanc
			SRZ->RZ_PD     := Space(3)
			SRZ->RZ_TPC    := cTpc
			SRZ->RZ_TIPO   := cTipoLanc
			If cPaisLoc == "DOM"
				SRZ->RZ_SEMANA := cSemana
			Endif	
		   SRZ->( MsUnlock() )
		EndIf
	EndIf
Next nNroContr
RestArea( aArea )
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fGeraProvS³ Autor ³ Emerson Rosa de Souza	³ Data ³ 18/08/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Gera o arquivo SRZ com os lancamentos da provisao		  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fGeraProvSRZ( cMesAnoRef , lExecInRemote )				  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGeraProvSRZ( cMesAnoRef , lExecInRemote )

Local aArea		 := GetArea()
Local nTamCC	 := TamSx3("RZ_CC")[1]
Local cTipoLanc  := If(lProvFer,"PF",If(lProv13o,"PD","PR"))
Local cDescLanc  := Substr(cTipoLanc + Space(1) + cMesAnoRef + space(nTamCC),1,nTamCC)
Local cTiposDel	 := ""

Private dDataRef := CTOD("")
Private cEncInss := GetMv("MV_ENCINSS",,"N")

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se o Processo de Contabilizacao esta sendo Executado³
³ via Remote												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Gpem110InRemote( @lExecInRemote )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gerar SRZ caso nao tenha sido gerado ou provisao recalculada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SRZ")
If lItemClVl
	dbSetOrder(3)
Else
	dbSetOrder(1)
Endif
If !SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + "1" ) )

    /*
    ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Deleta os Registros no SRZ para nova contabilizacao de Provisoes|
	³ de Ferias e de 13o Salario								     |
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	#IFNDEF TOP
		cTiposDel := If(lProvFer,"PF","")+ If(lProv13o,"*PD","")
	#ELSE
		cTiposDel := If(lProvFer,"'PF'","") + If(lProv13o,",'PD'","")
	   	If !lProvFer .AND. lProv13o
			cTiposDel := "'PD'"
		Endif 
	#ENDIF	
	DeleteSrzTop(	NIL			,;	//Conteudo para comparar com RZ_TPC
					cTiposDel	,; 	//Tipo de Lancamento a comparar com RZ_TIPO
					.F.			,;	//Se Limpa Todos os Tipos do SRZ
					.T.			,;	//Se se refere a Provisoes
					.F.			 ;	//Se mostra Dialogo com barra de evolucao
				 )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a data de referencia para geracao dos lancamentos    |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nUltDia  := f_UltDia(CTOD("01/" + Left(cMesAnoCtab,2)+"/"+Right(cMesAnoCtab,4),"DDMMYY"))
	dDataRef := CTOD(StrZero(nUltDia,2) + "/" + Left(cMesAnoCtab,2)+"/"+Right(cMesAnoCtab,4),"DDMMYY")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama a rotina que gera lancamentos contabeis da provisao  |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	fGeraCtab( dDataRef , lGpem110ExecInRemote )

	dbSelectArea("SRZ")
	If lItemClVl
		dbSetOrder(3)
	Else
		dbSetOrder(1)
	Endif

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gera um registro indicando mes e tipo de lancamento gerado.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RecLock("SRZ",.T.)
	SRZ->RZ_FILIAL := Replicate("z", FWGETTAMFILIAL)
	SRZ->RZ_CC     := cDescLanc
	SRZ->RZ_PD     := Space(3)
	SRZ->RZ_TPC    := "1"
	SRZ->RZ_TIPO   := cTipoLanc
	If cPaisLoc == "DOM"
		SRZ->RZ_SEMANA := cSemana
	Endif	
  	SRZ->( MsUnlock() )
                       
	If lProvFer .and. lProv13o
		RecLock("SRZ",.T.)
		SRZ->RZ_FILIAL := Replicate("z", FWGETTAMFILIAL)
		SRZ->RZ_CC     := If(cTipoLanc=="PD","PF",If(cTipoLanc=="PF","PD",cTipoLanc))+Substr(cDescLanc,3)
		SRZ->RZ_PD     := Space(3)
		SRZ->RZ_TPC    := "1"
		SRZ->RZ_TIPO   := If(cTipoLanc=="PD","PF",If(cTipoLanc=="PF","PD",cTipoLanc))
		If cPaisLoc == "DOM"
			SRZ->RZ_SEMANA := cSemana
		Endif	
		SRZ->( MsUnlock() )
	EndIf	
EndIf
RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³fGeraCtab ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 16.07.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Gera os lancamentos contabeis de provisao    			  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³fGeraCtab( dDataRef , lExecInRemote )						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso	 	 ³Generico 												  	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGeraCtab( dDataRef , lExecInRemote )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Programa)							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cTPRDbf,cTPRNtx,nCnt1,nCnt2,nx
Local cAlias := ALIAS()
Local aFilMat := {}
Local aRateio := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Programa)					  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aFerVenc     := Array(_Linhas,_Colunas)
Private aFerProp     := Array(_Linhas,_Colunas)
Private a13Salar     := Array(_Linhas,_Colunas)
Private a14Salar     := Array(_Linhas,_Colunas)
Private aTotCcFer    := Array(_Linhas,_Colunas)
Private aTotFunFer   := Array(_Linhas,_Colunas)
Private aTotCc13s    := Array(_Linhas,_Colunas)
Private aTotCc14s    := Array(_Linhas,_Colunas)
Private aTotAux  	 := {}
Private aVerba  	 := {}
Private aVerAux	 	 := {}
Private aTransf      := {}
Private aCodFol  	 := {}
Private lSalInc    	 := .F.
Private lTrataTrf    := .F.
Private lAbortPrint  := .F.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se o Processo de Contabilizacao esta sendo Executado³
³ via Remote												   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Gpem110InRemote( @lExecInRemote )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para montagem do arquivo temporario.    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem		:= If(lItemClVl,6,2)						    // Centro de Custo
cFilDe	   	:= Space(FWGETTAMFILIAL)				//	Filial De
cFilAte    	:= Replicate("z", FWGETTAMFILIAL)       //	Filial Ate
cCcDe	   	:= Space(9)							    //	Centro de Custo De
cCcAte	   	:= Replicate("z",09)  					//	Centro de Custo Ate
cMatDe	   	:= Space(6)							    //	Matricula De
cMatAte     := Replicate("z",06)     				//	Matricula Ate
cNomeDe     := Space(30)							//	Nome De
cNomeAte    := Replicate("z",30)					//	Nome Ate
cCateg		:= "ACDEGHIJMPST"						//  Categorias

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Alterar parametros da geracao de lancamentos para o SRZ		 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("GP110APP")
   EXECBLOCK("GP110APP",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o arquivo temporario "TPR" a partir do SRA e SRE     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
fMonta_TPR(@cTPRDbf,@cTPRNtx,nOrdem,dDataRef,@lSalInc,@lTrataTrf,@aTransf)

dbSelectArea( "SRA" )
dbSetOrder(1)

dbSelectArea( "TPR" )
dbGoTop()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega regua de processamento							   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF ( lGpem110ExecInRemote )
	ProcRegua( TPR->( RecCount() ) )
EndIF	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Limpa o array com o conteudo especificado no 2§ parametro    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
fLimpaArray( @aTotCcFer, 0 )
fLimpaArray( @aTotFunFer, 0 )
fLimpaArray( @aTotCc13s, 0 )
fLimpaArray( @aTotCc14s, 0 )

cFilialAnt := "__cFilialAnt__"
cCcAnt	   := TPR->PR_CC
If lItemClVl
	cItAnt	   := TPR->PR_ITEM
	cClAnt	   := TPR->PR_CLVL
Else
	cItAnt	   := ""
	cClAnt	   := ""
EndIf
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Percorre Todo o Arquivo Temporario						   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
While TPR->( !Eof() )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se cancalou o processamento     							   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    IF ( lAbortPrint )
       Exit
    EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Garante o Posicionamento do Funcionario no SRA			   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	SRA->( dbSeek( TPR->PR_FILIAL + TPR->PR_MAT ) )

    /*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Quebra de Filial											   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF ( TPR->PR_FILIAL # cFilialAnt )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Atualiza a Vilial                       				       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cFilialAnt := TPR->PR_FILIAL

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Encerra caso nao Tenha conseguido carregar as Verbas	       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF !( Fp_CodFol( @aCodFol , cFilialAnt ) )
			Exit
		EndIF
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Carrega os identificadores da Provisoes					   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		fIdentProv(@aVerba,aCodFol,.T.,.T.)
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Atualiza Mensagem na Regua de Processamento				   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( lGpem110ExecInRemote )
			IncProc( ( OemToAnsi( STR0034 ) + cFilialAnt )  )	//"Gerando os Lan‡amentos Cont beis para a Filial: "
		EndIF	
    
	Else
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Movimenta Regua de Processamento							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    	IF ( lGpem110ExecInRemote )
    		IncProc()
    	EndIF	

    EndIF	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se nao houver informacoes do cabecalho despreza funcionario  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If fBusCabSRT(dDataRef)

		If lGeraPMes .And. aScan( aFilMat, { |aFilMat| aFilMat[1] == TPR->PR_FILIAL + TPR->PR_MAT } ) == 0
			aAdd( aFilMat, { TPR->PR_FILIAL + TPR->PR_MAT } )
			fGrvRateio(lProvFer,lProv13o)
		Else
			#IFDEF TOP
				fQryDetSRT(aVerba,aTransf,dDataRef,lTrataTrf,.T.,lProvFer,lProv13o)
			#ELSE
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca os lancamentos de ferias vencidas e proporcionais      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				fBusDetSRT(@aFerVenc,,aVerba,aTransf,dDataRef,_FerVenc,lTrataTrf,.T.)
				fBusDetSRT(@aFerProp,,aVerba,aTransf,dDataRef,_FerProp,lTrataTrf,.T.)
		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca os lancamentos de 13§ e 14§ Salario				     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				fBusDetSRT(@a13Salar,,aVerba,aTransf,dDataRef,_13Salar,lTrataTrf,.T.)
				fBusDetSRT(@a14Salar,,aVerba,aTransf,dDataRef,_14Salar,lTrataTrf,.T.)
			#ENDIF
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Totalizador -> Vencidas/Proporcionais/13§/14§				 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			For nCnt1 := 1 To _Linhas
				For nCnt2 := 1 To _Colunas
					aTotCcFer[nCnt1,nCnt2]  += aFerVenc[nCnt1,nCnt2]+aFerProp[nCnt1,nCnt2]
					aTotFunFer[nCnt1,nCnt2] += aFerVenc[nCnt1,nCnt2]+aFerProp[nCnt1,nCnt2]
					aTotCc13s[nCnt1,nCnt2] += a13Salar[nCnt1,nCnt2]
					aTotCc14s[nCnt1,nCnt2] += a14Salar[nCnt1,nCnt2]
				Next nCnt2
			Next nCnt1
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava no SRZ valores dos Funcionarios - Contabilizacao Analitica ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cEncInss == "S"
				If lCtabBxaRes
					fChkVBxa(@aTotFunFer,_FerVenc,_BxRes)
				EndIf	
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//| Ponto de Entrada para devolver o conteudo dos arrays aTotFunFer|
				//| a13Salar e a14Sala rateado. O retorno deve ser um array        |
				//| tridimensional onde o 1o nivel representa a quantidade de      |
				//| rateios efetuada e os dois ultimos niveis tem a mesma estrutura|
				//| o array aTotFunFer com duas colunas a mais trazendo os codigos |
				//| de Item e Classe.                                              |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("GP110SRZ")
					aRateio		:= EXECBLOCK("GP110SRZ",.F.,.F.,{"FER",dDataRef})
				Endif
				If Len(aRateio) > 0 .And. ( Len(aRateio[1,1]) == _Colunas + 2 )
					For nx := 1 to Len(aRateio)
						fGravaSrz(aClone(aRateio[nx]),_FerVenc,cFilialAnt,cCcAnt,SRA->RA_MAT)
					Next nx
					aRateio		:= {}
				Else			
					fGravaSrz(aTotFunFer, _FerVenc, cFilialAnt, cCcAnt, SRA->RA_MAT, cItAnt, cClAnt)
				EndIf
				fLimpaArray( @aTotFunFer, 0 )
				If lCtabBxaRes
					fChkVBxa(@a13Salar,_13Salar,_BxRes)
					fChkVBxa(@a14Salar,_14Salar,_BxRes)
				EndIf
		
				//-- Repeticao do ponto de entrada para tratamento do 13o salario
				If ExistBlock("GP110SRZ")
					aRateio		:= EXECBLOCK("GP110SRZ",.F.,.F.,{"13",dDataRef})
				EndIf
				If Len(aRateio) > 0 .And. ( Len(aRateio[1,1]) == _Colunas + 2 )
					For nx := 1 to Len(aRateio)
						fGravaSrz(aRateio[nx],_13Salar,cFilialAnt,cCcAnt,SRA->RA_MAT)
					Next nx
					aRateio		:= {}
				Else
					fGravaSrz(a13Salar, _13Salar, cFilialAnt, cCcAnt, SRA->RA_MAT, cItAnt, cClAnt)
				Endif
	
				//-- Repeticao do ponto de entrada para tratamento do 14o salario
				If ExistBlock("GP110SRZ")
					aRateio		:= EXECBLOCK("GP110SRZ",.F.,.F.,{"14",dDataRef})
				EndIf
				If Len(aRateio) > 0 .And. ( Len(aRateio[1,1]) == _Colunas + 2 )
					For nx := 1 to Len(aRateio)
						fGravaSrz(aRateio[nx],_14Salar,cFilialAnt,cCcAnt,SRA->RA_MAT)
					Next nx
					aRateio		:= {}
				Else
					fGravaSrz(a14Salar, _14Salar, cFilialAnt, cCcAnt, SRA->RA_MAT, cItAnt, cClAnt)
				Endif
			Endif		    
		EndIf	    
	Endif
	
	dbSelectArea( "TPR" )
	dbSkip()

	If !lGeraPMes .And. ;
		( ( !lItemClVl .And. TPR->PR_FILIAL + TPR->PR_CC # cFilialAnt + cCcAnt ) .Or. ; 
		  ( lItemClVl .And. TPR->PR_FILIAL + TPR->PR_CC + TPR->PR_ITEM + TPR->PR_CLVL # cFilialAnt + cCcAnt + cItAnt + cClAnt ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existem as verbas de rescisao, se nao existirem  ³
		//³ subtrair os valores destas verbas dos lancamentos do mes.    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCtabBxaRes
			fChkVBxa(@aTotCcFer,_FerVenc,_BxRes)
		EndIf	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava o array aTotCCFer no arquivo SRZ.                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		fGravaSrz(aTotCcFer, _FerVenc, cFilialAnt, cCcAnt, "zzzzzz", cItAnt, cClAnt)
		fLimpaArray( @aTotCcFer, 0 )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existem as verbas de rescisao, se nao existirem  ³
		//³ subtrair os valores destas verbas dos lancamentos do mes.    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCtabBxaRes
			fChkVBxa(@aTotCc13s,_13Salar,_BxRes)
			fChkVBxa(@aTotCc14s,_14Salar,_BxRes)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava o array aTotCCFer no arquivo SRZ.                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		fGravaSrz(aTotCc13s, _13Salar, cFilialAnt, cCcAnt, "zzzzzz", cItAnt, cClAnt)
		fGravaSrz(aTotCc14s, _14Salar, cFilialAnt, cCcAnt, "zzzzzz", cItAnt, cClAnt)
		fLimpaArray( @aTotCc13s, 0 )
		fLimpaArray( @aTotCc14s, 0 )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o centro de custo para uma nova totalizacao		 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCcAnt := TPR->PR_CC
		If lItemClVl
			cItAnt := TPR->PR_ITEM
			cClAnt := TPR->PR_CLVL
		EndIf

	EndIf
Enddo

TPR->(dbCloseArea())
fErase(cTPRNtx + OrdBagExt())
fErase(cTPRDbf + ".DBF")

//**********************************//
// Descarrega no SRZ os totais por  //
// Centro de Custo obtidos do SRT   //
//**********************************//
If Len(aTotAux) > 0
	SRZTotAux(aTotAux)
Endif

dbSelectArea(cAlias)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³fGravaSRZ ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 25.08.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Grava os totais de centro de custo no SRZ			 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fGravaSRZ(aProvisao,nTipoProv,cFilialAnt,cCCProv)		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGravaSRZ(aProvisao,nTipoProv,cFilProv,cCCProv,cMat,cItProv,cClProv)
Local cAlias  := Alias()
Local lEstrut		:= ( len(aProvisao[1])==_Colunas+2 )
Local lTemItem		:= SRZ->( FieldPos( "RZ_ITEM" ) # 0 )
Local lTemClVl		:= SRZ->( FieldPos( "RZ_CLVL" ) # 0 )
Local nTipo2,nTipo3
Local nPosVerba,cCodVerba
Local nPosAux
Local nRat

dbSelectArea( "SRZ" )
For nTipo2 := 1 To _Linhas
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Linhas que nao deverao ser gravadas no arquivo				 |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nTipo2 == _Anter .or. nTipo2 == _Atual .or. nTipo2 == _BxTot
		Loop
	EndIf

    If (nTipo2 == _BxFer .and. !lCtabBxaFer) .or. (nTipo2 == _BxRes .and. !lCtabBxaRes )
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava as colunas de valores no arquivo detalhe               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nTipo3 := 1 To _Colunas
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Busca a verba e grava os lancamentos no SRZ				     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nPosLinha := If(nTipo2 == _NoMes,_Atual,nTipo2)
        nPosVerba := aScan(aVerba, { |X| X[1] == nTipoProv .and. X[2] == nPosLinha .and. X[3] == nTipo3 })
        cCodVerba := If( nPosVerba > 0, aVerba[nPosVerba,4], Space(3) )
        If !Empty(cCodVerba).and. aProvisao[nTipo2,nTipo3] # 0
        	
        	RecLock( "SRZ", .T. )
			SRZ->RZ_FILIAL  := cFilProv
			SRZ->RZ_CC      := cCCProv
			SRZ->RZ_PD      := cCodVerba
			SRZ->RZ_HRS     := 0
			SRZ->RZ_VAL     := aProvisao[nTipo2,nTipo3]
			SRZ->RZ_OCORREN := 0
			SRZ->RZ_TIPO    := If(nTipoProv == _FerVenc, "PF", "PD")
			SRZ->RZ_TPC     := "1"           
			if Type("SRZ->RZ_MAT") # "U"
				SRZ->RZ_MAT     := cMat
			Endif	
			
			//Tratamento de Integração Logix x Rh Protheus 
			IF SRZ->( FieldPos('RZ_ARELIN') ) > 0
				fGravArelin( cMat )
			ENDIF
							
			If cPaisLoc == "DOM"
				SRZ->RZ_SEMANA := cSemana
			Endif	
			If lTemItem
				If lEstrut
					SRZ->RZ_ITEM	:= aProvisao[nTipo2,_Colunas+1]
				Else
					SRZ->RZ_ITEM	:= cItProv
				EndIf
			Endif
			If lTemClVl
				If lEstrut
					SRZ->RZ_CLVL	:= aProvisao[nTipo2,_Colunas+2]
				Else
					SRZ->RZ_CLVL	:= cClProv
				EndIf
			Endif
			SRZ->( MsUnlock() )
		EndIf
	Next nTipo3
Next nTipo2
dbSelectArea( cAlias )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³fGrvRateio³ Autor ³ Kelly Soares          ³ Data ³ 20.06.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Grava verbas rateadas no SRZ.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fGrvRateio()                            					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fGrvRateio(lProvFer,lProv13o)

Local aArea	:= GetArea()
Local cChave	:= TPR->PR_FILIAL+TPR->PR_MAT+DTOS(dDataRef)
Local nInd		:= If(lItemClVl,4,5)
Local cInd		:= "" 
Local nTipoPr	:= ""

Default lProvFer:= .T.
Default lProv13o:= .T.    

DbSelectArea("RHT")
DbSetOrder(RetOrdem("RHT","RHT_FILIAL+RHT_MAT+DTOS(RHT_DTCALC)+RHT_TPPROV"))

If RHT->(DbSeek(cChave))
	While RHT->(!Eof()) .and. RHT->(RHT_FILIAL+RHT_MAT+DTOS(RHT_DTCALC)) == cChave

		If (!lProvFer .And. RHT->RHT_TPPROV $ "1-2") .Or. (!lProv13o .And. !(RHT->RHT_TPPROV $ "1-2"))
			RHT->(dbSkip())
			Loop
		Endif
		
		If lItemClVl
		  	cInd := RHT->RHT_FILIAL+RHT->RHT_CC+RHT->RHT_ITEM+RHT->RHT_CLVL+RHT->RHT_MAT+RHT->RHT_VERBA+"1"+If(RHT->RHT_TPPROV $ "1-2", "PF", "PD")
		Else
			cInd := RHT->RHT_FILIAL+RHT->RHT_CC+RHT->RHT_MAT+RHT->RHT_VERBA+"1"+If(RHT->RHT_TPPROV $ "1-2", "PF", "PD")
		Endif		                                                                                                                               
		
		If !RHT->RHT_VALOR == 0.00
	        SRZ->(dbSetOrder(nInd)) 
			If SRZ->(DbSeek(cInd))
				RecLock( "SRZ", .F. )
		       	SRZ->RZ_VAL += RHT->RHT_VALOR
		       	If SRZ->RZ_VAL == 0.00
		       		dbDelete() //O registro é excluído pois não pode haver contabilização de valor zerado.
		       	Endif
	    	   	SRZ->( MsUnlock() )
			Else
		        RecLock( "SRZ", .T. )
				SRZ->RZ_FILIAL  := RHT->RHT_FILIAL
				SRZ->RZ_CC      := RHT->RHT_CC
				SRZ->RZ_PD      := RHT->RHT_VERBA
				SRZ->RZ_HRS     := 0
				SRZ->RZ_VAL     := RHT->RHT_VALOR
				SRZ->RZ_OCORREN := 0
				SRZ->RZ_TIPO    := If(RHT->RHT_TPPROV $ "1-2", "PF", "PD")
				SRZ->RZ_TPC     := "1"           
				If Type("SRZ->RZ_MAT") # "U"
					SRZ->RZ_MAT  := RHT->RHT_MAT
				Endif	
				If lItemClVl
					SRZ->RZ_ITEM := RHT->RHT_ITEM
					SRZ->RZ_CLVL := RHT->RHT_CLVL
				Endif			
				SRZ->( MsUnlock() )
			Endif
				
			//Acumula valor no array de totais
			nTipoPr := If(RHT->RHT_TPPROV $ "1-2", "PF", "PD")
			If lItemClVl
				If (nPos := aScan(aTotAux, { |X| X[1] == RHT->RHT_FILIAL .and. X[2] == RHT->RHT_CC .and. X[3] == RHT->RHT_VERBA .and. ;
				                                                              X[6] == RHT->RHT_ITEM .and. X[7] == RHT->RHT_CLVL })) > 0
					aTotAux[nPos,4] += RHT->RHT_VALOR
				Else
					aAdd(aTotAux,{RHT->RHT_FILIAL,RHT->RHT_CC,RHT->RHT_VERBA,RHT->RHT_VALOR,nTipoPr,RHT->RHT_ITEM,RHT->RHT_CLVL})
	     		Endif
			Else
				If (nPos := aScan(aTotAux, { |X| X[1] == RHT->RHT_FILIAL .and. X[2] == RHT->RHT_CC .and. X[3] == RHT->RHT_VERBA })) > 0
					aTotAux[nPos,4] += RHT->RHT_VALOR
				Else
					aAdd(aTotAux,{RHT->RHT_FILIAL,RHT->RHT_CC,RHT->RHT_VERBA,RHT->RHT_VALOR,nTipoPr})
				Endif  
			Endif
		Endif
		RHT->(DbSkip())
	Enddo
Endif

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³fDelRegSRZ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 28.08.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Delete registro indicando que devera ser gerado um novo SRZ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fDelRegSRZ(nFol13s,cTpCFol,dDtRefBus)   					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fDelRegSRZ(nFl13Pr,cTpCFol,dDtRefBus)
Local cAlias := Alias(),cDescLanc
Local nTamCC	  := TamSx3("RZ_CC")[1]

dbSelectArea("SRZ")
dbSetOrder(1)


cTpCFol   := If(Empty(cTpCFol), "1", cTpCFol)
dDtRefBus := If(dDtRefBus == Nil, dDataBase, dDtRefBus)
cDescLanc := Substr(If(nFl13Pr == 1, "FL", If(nFl13Pr == 2,"13", "PR")) + Space(1) +;
				 StrZero(Month(dDtRefBus),2)+StrZero(Year(dDtRefBus),4) + Space(nTamCC),1,nTamCC)

If nFl13Pr == 3                                        
	cDescLanc := Substr("PR" + Space(1) + StrZero(Month(dDtRefBus),2)+StrZero(Year(dDtRefBus),4) + Space(nTamCC),1,nTamCC)
	If SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + cTpCFol ) )
		RecLock("SRZ",.F.)
		SRZ->( dbDelete() )
	 	SRZ->( MsUnlock() )
	EndIf

	cDescLanc := Substr("PD" + Space(1) + StrZero(Month(dDtRefBus),2)+StrZero(Year(dDtRefBus),4) + Space(nTamCC),1,nTamCC)
	If SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + cTpCFol ) )
		RecLock("SRZ",.F.)
		SRZ->( dbDelete() )
	 	SRZ->( MsUnlock() )
	EndIf
	
	cDescLanc := Substr("PF" + Space(1) + StrZero(Month(dDtRefBus),2)+StrZero(Year(dDtRefBus),4) + Space(nTamCC),1,nTamCC)
	If SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + cTpCFol ) )
		RecLock("SRZ",.F.)
		SRZ->( dbDelete() )
	 	SRZ->( MsUnlock() )
	EndIf
Else
	If SRZ->( dbSeek( Replicate("z", FWGETTAMFILIAL) + cDescLanc + Space(3) + cTpCFol ) )
		RecLock("SRZ",.F.)
		SRZ->( dbDelete() )
	   	SRZ->( MsUnlock() )
	EndIf
Endif
dbSelectArea(cAlias)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³ fChkVBxa ³ Autor ³ Emerson Rosa de Souza ³ Data ³ 12.02.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Verifica se existe codigo de verba para baixa de rescisao  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fChkVBxa(aProvisao,nTipProv,nTipBxa)						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fChkVBxa(aProvisao,nTipProv,nTipBxa)
Local nPosVBxa,cCodVBxa,nCntBx

nTipProv := If(nTipProv == _FerProp,_FerVenc,nTipProv)
nPosVBxa := aScan(aVerba, { |X| X[1] == nTipProv .and. X[2] == nTipBxa .and. X[3] == _Prov })
cCodVBxa := If( nPosVBxa > 0, aVerba[nPosVBxa,4], Space(3) )
If Empty(cCodVBxa)
	For nCntBx := 1 To _Colunas
		aProvisao[_NoMes,nCntBx]  -= aProvisao[nTipBxa,nCntBx]
		aProvisao[nTipBxa,nCntBx] := 0
	Next nCntBx
EndIf

Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³DeleteSrzXbs ³ Autor ³Marinaldo de Jesus   ³ Data ³14/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Deletar Registros no SRZ para ambientes em TOP				 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                   	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM110  													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function DeleteSrzXbs(	cTpc			,;	//Conteudo para comparar com RZ_TPC
								cTipoLanc		,;	//Tipo de Lancamento a comparar com RZ_TIPO
								lLimpaSRZ		,;	//Se Limpa Todos os Tipos do SRZ
								lProvisoes		,;	//Se se refere a Provisoes
								lShowDele		 ;	//Se mostra Dialogo dom barra de evolucao
							 )
                
Local cTiposVld			:= __TiposValidos__
Local cSpaceRZTPC   	:= Space( TamSx3("RZ_TPC")[1] )
Local lDeleteSRZ		:= .F.
Local nProcRegua		:= 0.00

DEFAULT cTpc			:= cSpaceRZTPC
DEFAULT cTipoLanc		:= ""
DEFAULT lLimpaSRZ		:= .F.
DEFAULT lProvisoes		:= .F.
DEFAULT lShowDele		:= .F.

IF ( !( lShowDele ) .and. ( lGpem110ExecInRemote ) )
	////"Preparando SRZ para a Contabiliza‡„o"
	Processa( { || DeleteSrzXbs(	cTpc			,;	//Conteudo para comparar com RZ_TPC
									cTipoLanc		,;	//Tipo de Lancamento a comparar com RZ_TIPO
									lLimpaSRZ		,;	//Se Limpa Todos os Tipos do SRZ
									lProvisoes		,;	//Se se refere a Provisoes
									.T.				 ;	//Se mostra Dialogo dom barra de evolucao
							 );
               },;
               OemToAnsi( STR0037 );
             )
	Return( NIL )
EndIF

IF ( lGpem110ExecInRemote )
	ProcRegua( ( nProcRegua := SRZ->( RecCount() ) ) )
EndIF	

SRZ->( dbGoTop() )
While SRZ->( !Eof() )
	IF ( lGpem110ExecInRemote )
		IncProc()
	EndIF	
	IF !( lProvisoes )
		lDeleteSRZ := SRZ->(;
								(;
									( RZ_TPC == cTpc ) .or. ;
									( RZ_TPC == cSpaceRZTPC ) ;
								) .and. ;
								(;
									( lLimpaSRZ )			 .or. ;
									( RZ_TIPO == cTipoLanc ) .or. ;
									!( RZ_TIPO $ cTiposVld )      ; 
								);
	   					    )
	Else
		lDeleteSRZ := ( SRZ->RZ_TIPO $ cTipoLanc )
	EndIF
	IF ( lDeleteSRZ )
		SRZ->( RecLock( "SRZ" , .F. , .T. ) )
		SRZ->( dbdelete() )
		SRZ->( MsUnlock() )
	EndIF
	SRZ->( dbSkip() )
End While

Return( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³DeleteSrzTop ³ Autor ³Marinaldo de Jesus   ³ Data ³14/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Deletar Registros no SRZ para ambientes em TOP				 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                   	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM110  													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function DeleteSrzTop(	cTpc			,;	//Conteudo para comparar com RZ_TPC
								cTipoLanc		,;	//Tipo de Lancamento a comparar com RZ_TIPO
								lLimpaSRZ		,;	//Se Limpa Todos os Tipos do SRZ
								lProvisoes		,;	//Se se refere a Provisoes
								lShowDele		 ;	//Se mostra Dialogo dom barra de evolucao
							 )
#IFDEF TOP

	Local cTiposVld  		:= __TiposValidos__
	Local cRetSqlName		:= InitSqlName( "SRZ" )
	Local cQueryExec		:= ""
	Local cQueryCond		:= ""
	Local cQueryRecno		:= ""
	Local cQueryDelet		:= "DELETE"
	Local cSpaceRZTPC   	:= Space( TamSx3("RZ_TPC")[1] )
	Local cCpoRecno			:= ""
	Local cCpoDeleted		:= ""
	Local lAs400			:= ( TcSrvType() == "AS/400" )
	Local nMinRec			:= 0.00
	Local nMaxRec			:= 0.00
	Local nProcRegua		:= 0.00

	DEFAULT cTpc			:= cSpaceRZTPC
	DEFAULT cTipoLanc		:= ""
	DEFAULT lLimpaSRZ		:= .F.
	DEFAULT lProvisoes		:= .F.
	DEFAULT lShowDele		:= .F.

	IF ( !( lShowDele ) .and. ( lGpem110ExecInRemote ) )
		//"Preparando SRZ para a Contabiliza‡„o"
		Processa( { || DeleteSrzTop(	cTpc			,;	//Conteudo para comparar com RZ_TPC
										cTipoLanc		,;	//Tipo de Lancamento a comparar com RZ_TIPO
										lLimpaSRZ		,;	//Se Limpa Todos os Tipos do SRZ
										lProvisoes		,;	//Se se refere a Provisoes
										.T.				 ;	//Se mostra Dialogo dom barra de evolucao
							 		);
				  },;
			   	  OemToAnsi( STR0037 );
			    )
		Return( NIL )
	EndIF

	cCpoRecno 	:= IF( lAs400 , "RRN("+cRetSqlName+")", "R_E_C_N_O_" )
	cCpoDeleted	:= IF( lAs400 , "@DELETED@" , "D_E_L_E_T_" )

	MsMinMaxRec( "SRZ" , @nMinRec , @nMaxRec )

	IF ( lGpem110ExecInRemote )
		nProcRegua := ( ( nMaxRec - nMinRec ) + 1 )
		IF ( nProcRegua >= 1024 )
			nProcRegua := Round( ( nProcRegua / 1024 ) , 0 )
			nProcRegua := Int( nProcRegua )
		EndIF
		ProcRegua( nProcRegua )
	EndIF	

	IF ( nMaxRec > 0.00 )
	
		cQueryCond	:= " FROM "
		cQueryCond	+= cRetSqlName
		cQueryCond	+= " WHERE "
		cQueryCond	+= "("
		cQueryCond	+=	"("
		IF !( lProvisoes )
			cQueryCond	+=		"("
			cQueryCond	+=			"RZ_TPC='" + cTpc + "'"
			cQueryCond	+=			" OR "
			cQueryCond	+=			"RZ_TPC='" + cSpaceRZTPC + "'"
			cQueryCond	+=		 ")"
			IF !( lLimpaSRZ )
				cQueryCond	+= " AND "
				cQueryCond	+= "("
				cQueryCond	+=		"RZ_TIPO IN('" + cTipoLanc + "')"
				cQueryCond	+=		" OR "
				cQueryCond	+=		"RZ_TIPO NOT IN(" + cTiposVld + ")"
				cQueryCond	+= ")"
			EndIF
		Else
			cQueryCond	+= "RZ_TIPO IN(" + cTipoLanc + ")"
		EndIF	
		cQueryCond	+=	")"
		cQueryCond	+= 	" OR "
		cQueryCond	+= 	cCpoDeleted + "='*'"
		cQueryCond	+= ")"
		
		cQueryDelet += cQueryCond
		cQueryDelet += " AND "
		
		While ( nMinRec <= nMaxRec )
			IF ( lGpem110ExecInRemote )
				IncProc()
			EndIF	
			cQueryRecno := "("
			cQueryRecno +=		cCpoRecno + " >= " + Str( nMinRec , 10 , 0 )
			cQueryRecno += 		" AND "
			cQueryRecno += 		cCpoRecno + " <= " + Str( ( nMinRec += 1024 ) , 10 , 0 )
			cQueryRecno += ")"
			cQueryExec	:= ( cQueryDelet + cQueryRecno )
			TcSqlExec( cQueryExec )
			SRZ->( dbGotop() )
		End While

	EndIF
		
#ELSE

	DeleteSrzXbs(	cTpc			,;	//Conteudo para comparar com RZ_TPC
					cTipoLanc		,;	//Tipo de Lancamento a comparar com RZ_TIPO
					lLimpaSRZ		,;	//Se Limpa Todos os Tipos do SRZ
					lProvisoes		,;	//Se se refere a Provisoes
					lShowDele		 ;	//Se mostra Dialogo dom barra de evolucao
				 )

#ENDIF
	
Return( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³MyVerPadrao  ³ Autor ³Marinaldo de Jesus   ³ Data ³05/06/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Verificar a Existencia do Lancamento Padrao   				 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³lLctopOk                                               	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM110  													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function MyVerPadrao(	cPadrao 		,;	//Codigo do Lancamento Padrao
								nRecnoLctop		,;	//Recno do Lancamento Padrao ( Por Referencia)
								aPadrao 		,;	//Array com os Codigos vs Recnos ( Por Referencia )
								cAliasLctop		,;	//Alias do arquivo para pesquisa 
								cFilLctop		,;	//Filial do arquivo para pesquisa ( Apenas Quando TOP )
								cCpoLctop		,;	//Campo do arquivo para pesquisa ( Apenas Quando TOP )
								cPrefixLctop	 ;	//Prefixo do campo do arquivo para pesquisa ( Apenas Quando TOP )
							)

Local cSvAlias		:= Alias()
Local cQuery		:= ""
Local lLctopOk		:= .T.
Local nPosLctop		:= 0.00

nRecnoLctop := 0.00

IF !( lLctopOk := !( ( nPosLctop := aScan( aPadrao , { |x| x[1] == cPadrao } ) ) == 0.00 ) )
	#IFDEF TOP
		cQuery	:= "SELECT "
		cQuery	+= ( cPrefixLctop+"FILIAL" )
		cQuery	+= ", "
		cQuery	+= cCpoLctop
		cQuery	+= ", "
		cQuery	+= "R_E_C_N_O_ RECNO"
		cQuery	+= " FROM "
		cQuery	+= InitSqlName( cAliasLctop )
		cQuery	+= " "
		cQuery	+= cAliasLctop
		cQuery	+= " WHERE "
		cQuery	+= ( ( cAliasLctop+"."+cPrefixLctop+"FILIAL='" ) + cFilLctop + "'" )
		cQuery	+= " AND "
		cQuery	+= ( ( cAliasLctop+"."+cCpoLctop+"='" ) + cPadrao + "'" )
		cQuery	+= " AND "
		cQuery	+= ( cAliasLctop + ".D_E_L_E_T_=' ' " )
		cQuery	+= " ORDER BY " + SqlOrder( (cAliasLctop)->( IndexKey() ) )
		cQuery  := ChangeQuery( cQuery )
		IF MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQuery),"MyVerLctop",.T.,.T.)
			IF ( lLctopOk := ( MyVerLctop->( !Eof() ) ) )
				nRecnoLctop := MyVerLctop->RECNO
			EndIF
			MyVerLctop->( dbCloseArea() )
		EndIF
	#ELSE
		IF ( lLctopOk := ( VerPadrao( cPadrao ) ) )
			nRecnoLctop := ( cAliasLctop )->( Recno() )
		EndIF
	#ENDIF
	aAdd( aPadrao , { cPadrao , nRecnoLctop } )
Else
	nRecnoLctop := aPadrao[ nPosLctop , 02 ]
EndIF

dbSelectArea( cSvAlias )

lLctopOk := ( nRecnoLctop > 0.00 )

Return( lLctopOk  )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³MyVerLancPCO ³ Autor ³Marinaldo de Jesus   ³ Data ³31/10/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Verificar a Existencia do Lancamento Padrao do PCO   		 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³lLcPcoOk                                               	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM110  													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function MyVerLancPCO(	cSeqLancPCO		,;	//SequenciaLancamento Padrao PCO
								nRecnoLcPCO		,;	//Recno da Seq Lancamento Padrao PCO( Por Referencia)
								aLancPCO 		,;	//Array com os Codigos vs Recnos ( Por Referencia )
								cAliasLcPCO		,;	//Alias do arquivo para pesquisa 
								cFilLcPCO		,;	//Filial do arquivo para pesquisa ( Apenas Quando TOP )
								cCpoLcPCO		,;	//Campo do arquivo para pesquisa ( Apenas Quando TOP )
								cPrefixLcPCO	 ;	//Prefixo do campo do arquivo para pesquisa ( Apenas Quando TOP )
							)

Local cSvAlias		:= Alias()
Local cQuery		:= ""
Local lLcPCOOk		:= .T.
Local nPosLcPCO		:= 0.00

nRecnoLcPCO := 0.00

IF !( lLcPCOOk := !( ( nPosLcPCO := aScan( aLancPCO , { |x| x[1] == cSeqLancPCO } ) ) == 0.00 ) )
	#IFDEF TOP
		cQuery	:= "SELECT "
		cQuery	+= ( cPrefixLcPCO+"FILIAL" )
		cQuery	+= ", "
		cQuery	+= cCpoLcPCO
		cQuery	+= ", "
		cQuery	+= "R_E_C_N_O_ RECNO"
		cQuery	+= " FROM "
		cQuery	+= InitSqlName( cAliasLcPCO )
		cQuery	+= " "
		cQuery	+= cAliasLcPCO
		cQuery	+= " WHERE "
		cQuery	+= ( ( cAliasLcPCO+"."+cPrefixLcPCO+"FILIAL='" ) + cFilLcPCO + "'" )
		cQuery	+= " AND "
		cQuery	+= ( cAliasLcPCO+".AKC_PROCES='"+__LCTO_PCO_FOLHA__+"'" )
		cQuery	+= " AND "
		cQuery	+= ( ( cAliasLcPCO+"."+cCpoLcPCO+"='" ) + cSeqLancPCO + "'" )
		cQuery	+= " AND "
		cQuery	+= ( cAliasLcPCO + ".D_E_L_E_T_=' ' " )
		cQuery	+= " ORDER BY " + SqlOrder( (cAliasLcPCO)->( IndexKey() ) )
		cQuery  := ChangeQuery( cQuery )
		IF MsOpenDbf(.T.,"TOPCONN",TcGenQry(NIL,NIL,cQuery),"MyVerLcPCO",.T.,.T.)
			IF ( lLcPCOOk := ( MyVerLcPCO->( !Eof() ) ) )
				nRecnoLcPCO := MyVerLcPCO->RECNO
			EndIF
			MyVerLcPCO->( dbCloseArea() )
		EndIF
	#ELSE
		IF ( lLcPCOOk := ( ( cAliasLcPCO )->(dbSeek(xFilial()+__LCTO_PCO_FOLHA__+cSeqLancPco)) ) )
			nRecnoLcPCO := ( cAliasLcPCO )->( Recno() )
		EndIF
	#ENDIF
	aAdd( aLancPCO , { cSeqLancPCO , nRecnoLcPCO } )
Else
	nRecnoLcPCO := aLancPCO[ nPosLcPCO , 02 ]
EndIF

dbSelectArea( cSvAlias )

lLcPCOOk := ( nRecnoLcPCO > 0.00 )

Return( lLcPCOOk  )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³SRZFilReplace³ Autor ³Marinaldo de Jesus   ³ Data ³05/06/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Executar Replace no Campo RZ_FILIAL           				 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                    	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Ponto de Entrada GP110ACT									 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function SRZFilReplace( cFilReplace , bCondReplace )
         
Local aArea				:= GetArea()
Local aAreaSRZ			:= SRZ->( GetArea() )
Local aSrzRecnos		:= {}
Local aSrzFields		:= { "RZ_FILIAL" }
Local aSrzQuery			:= {}
Local bSrzWhile			:= { || NIL }
Local bSrzSkip			:= { || NIL }
Local cZzsInRZFil		:= ( SRZzs()[__aSzs_Pos_Fil__] )
Local nMinRec	 		:= 0.00
Local nMaxRec			:= 0.00

DEFAULT bCondReplace	:= { || .T. }

#IFDEF TOP

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Condicao da Query Para Carga das Informacoes do SRZ		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aSrzQuery	  := Array( 05 )
	aSrzQuery[01] := "("
	aSrzQuery[02] :=	"RZ_FILIAL NOT IN("+ cZzsInRZFil + ")"
	aSrzQuery[03] := ")"
	aSrzQuery[04] := " AND "
	aSrzQuery[05] := "D_E_L_E_T_=' ' "

#ELSE

	bSrzWhile := { |cKey| .T. }
	bSrzSkip  := { || ( RZ_FILIAL $ cZzsInRZFil ) }	

#ENDIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Posiciona no Inicio do Arquivo                     		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
SRZ->( dbGotop() )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregando os Recnos                               		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
GdMontaCols(;
				NIL			,;	//01 -> Array com os Campos do Cabecalho da GetDados
				NIL			,;	//02 -> Numero de Campos em Uso
				NIL			,;	//03 -> [@]Array com os Campos Virtuais
				NIL			,;	//04 -> [@]Array com os Campos Visuais
				"SRZ"		,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
				aSrzFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
				@aSrzRecnos	,;	//07 -> [@]Array unidimensional contendo os Recnos
				"SRZ"   	,;	//08 -> Alias do Arquivo Pai
				NIL			,;	//09 -> Chave para o Posicionamento no Alias Filho
				bSrzWhile	,;	//10 -> Bloco para condicao de Loop While
				NIL			,;	//11 -> Bloco para Skip no Loop While
				.F.			,;	//12 -> Se Havera o Elemento de Delecao no aCols 
				.F.			,;	//13 -> Se cria variaveis Publicas
				.F.			,;	//14 -> Se Sera considerado o Inicializador Padrao
				NIL			,;	//15 -> Lado para o inicializador padrao
				.F.			,;	//16 -> Opcional, Carregar Todos os Campos
				.T. 		,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
				aSrzQuery	,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
				.F.			,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
				.F.			,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
				.F.			,;	//21 -> Carregar Coluna Fantasma
				.T.			,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
				.F.			,;	//23 -> Verifica se Deve verificar se o campo eh usado
				.F.			,;	//24 -> Verifica se Deve verificar o nivel do usuario
				.F.	 		 ;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
			)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Substituindo o Conteudo do RZ_FILIAL               		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nMaxRec := Len( aSrzRecnos )
For nMinRec := 1 To nMaxRec
	SRZ->( dbGoto( aSrzRecnos[ nMinRec ] ) )
	IF SRZ->( !( RZ_FILIAL $ cZzsInRZFil ) .and. Eval( bCondReplace ) )
		SRZ->( RecLock( "SRZ" , .F. ) )
		SRZ->RZ_FILIAL := cFilReplace
		SRZ->( MsUnLock() )
	EndIF	
Next nMinRec

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaurando os Ponteiros de Entrada                		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
RestArea( aAreaSRZ )
RestArea( aArea )

Return( NIL )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³SRZzs()      ³ Autor ³Marinaldo de Jesus   ³ Data ³05/06/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna Array com os zz..s. possiveis para o SRZ			 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                    	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPEM110                   								     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function SRZzs()

Local aSRZzs		:= Array( __Elementos_aSRZzs__ )
Local cZzsInRZFil	:= ""
Local cZzsInRZCc	:= ""
Local cZzsInRZMat	:= ""
Local cZzsInRZPd	:= "" 
Local lExistMatSRZ	:= ( Type("SRZ->RZ_MAT") == "C" ) 

/* 
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os "zz..." possiveis para o campo RZ_FILIAL		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cZzsInRZFil	:= ( "'" + Replicate( "z" , 2 ) + "'," )
cZzsInRZFil	+= ( "'" + Padr( Replicate( "z" , 2 ) , TamSx3( "RZ_FILIAL")[1] ) + "'," )
cZzsInRZFil	+= ( "'" + Replicate( "z" , TamSx3( "RZ_FILIAL" )[1] ) + "'," )
cZzsInRZFil	+= ( "'" + Replicate( "Z" , 2 ) + "'," )
cZzsInRZFil	+= ( "'" + Padr( Replicate( "Z" , 2 ) , TamSx3( "RZ_FILIAL")[1] ) + "'," )
cZzsInRZFil	+= ( "'" + Replicate( "Z" , TamSx3( "RZ_FILIAL" )[1] ) + "'" ) 

/* 
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os "zz..." possiveis para o campo RZ_CC			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cZzsInRZCc	:= ( "'" + Replicate( "z" , 9 ) + "'," )
cZzsInRZCc	+= ( "'" + Padr( Replicate( "z" , 9 ) , TamSx3( "RZ_CC" )[1] ) + "'," )
cZzsInRZCc	+= ( "'" + Replicate( "z" , TamSx3( "RZ_CC" )[1] ) + "'," )
cZzsInRZCc	+= ( "'" + Replicate( "Z" , 9 ) + "'," )
cZzsInRZCc	+= ( "'" + Padr( Replicate( "Z" , 9 ) , TamSx3( "RZ_CC" )[1] ) + "'," )
cZzsInRZCc	+= ( "'" + Replicate( "Z" , TamSx3( "RZ_CC" )[1] ) + "'" )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os "zz..." possiveis para o campo RZ_PD			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cZzsInRZPd	:= ( "'" + Replicate( "z" , 3 ) + "'," )
cZzsInRZPd	+= ( "'" + Padr( Replicate( "z" , 3 ) , TamSx3( "RZ_PD" )[1] ) + "'," )
cZzsInRZPd	+= ( "'" + Replicate( "z" , TamSx3( "RZ_PD" )[1] ) + "'," )
cZzsInRZPd	+= ( "'" + Replicate( "Z" , 3 ) + "'," )
cZzsInRZPd	+= ( "'" + Padr( Replicate( "Z" , 3 ) , TamSx3( "RZ_PD" )[1] ) + "'," )
cZzsInRZPd	+= ( "'" + Replicate( "Z" , TamSx3( "RZ_PD" )[1] ) + "'" )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define os "zz..." possiveis para o campo RZ_MAT			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF ( lExistMatSRZ )
	cZzsInRZMat	:= ( "'" + Replicate( "z" , 6 ) + "'," )
	cZzsInRZMat	+= ( "'" + Padr( Replicate( "z" , 6 ) , TamSx3( "RZ_MAT" )[1] ) + "'," )
	cZzsInRZMat	+= ( "'" + Replicate( "z" , TamSx3( "RZ_MAT" )[1] ) + "'," )
	cZzsInRZMat	+= ( "'" + Replicate( "Z" , 6 ) + "'," )
	cZzsInRZMat	+= ( "'" + Padr( Replicate( "Z" , 6 ) , TamSx3( "RZ_MAT" )[1] ) + "'," )
	cZzsInRZMat	+= ( "'" + Replicate( "Z" , TamSx3( "RZ_MAT" )[1] ) + "'" )
EndIF 

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega o Array                                 		       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aSRZzs[__aSzs_Pos_Fil__] := cZzsInRZFil
aSRZzs[__aSzs_Pos_Cc__ ] := cZzsInRZCc
aSRZzs[__aSzs_Pos_Mat__] := cZzsInRZMat
aSRZzs[__aSzs_Pos_Pd__ ] := cZzsInRZPd

Return( aClone( aSRZzs ) )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³Gpem110InRemote³Autor³Marinaldo de Jesus   ³ Data ³24/06/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Verifica se o Processo de Contabilizacao esta sendo Executado³
³          ³via Remote													 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                                                    	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPEM110                   								     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function Gpem110InRemote( lExecInRemote )

DEFAULT lExecInRemote := .T.

lGpem110ExecInRemote := lExecInRemote
IF ( ( Type("__cInternet") <> "U" ) .and. ( __cInternet == "AUTOMATICO" ) )
	lGpem110ExecInRemote := .F.
EndIF

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGravArelin  ºAutor  ³Tiago Malta      º Data ³  10/09/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Integração Logix X Rh Protheus.                  			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fGravArelin( cMat )

Local cDepto  := space(10)
Local cArelin := space(10) 

	IF cMat <> nil .AND. !EMPTY(cMat) .AND. SuperGetMv("MV_ERPLOGI",,"2") == '1'
	
		SRA->( dbSetOrder(1) )
		SRA->( dbSeek( xFilial('SRA') + cMat ) )
		cDepto := SRA->RA_DEPTO
		
		SQB->( dbSetOrder(1) )
		SQB->( dbSeek( xFilial('SQB') + cDepto ) )
		cArelin := SQB->QB_ARELIN 
		
		IF !EMPTY(cArelin)
			SRZ->RZ_ARELIN := cArelin
		ENDIF 
		
	ENDIF

Return()
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fValidDataºAutor  ³Luciana Silveira    º Data ³  02/02/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fValidData() 
Local lRet:= .T. 
Private cMesAnoCtab := mv_par01
Private aMesValid:= { "01","02","03","04","05","06","07","08","09","10","11","12" }

	//Verifica se a competencia informada e invalida e exibe uma mensagem de alerta
	If aScan( aMesValid, Subst( cMesAnoCtab, 1 , 2 ) ) == 0 
		lRet := .F.
		Aviso(STR0062 ,STR0063 + cMesAnoCtab + STR0064 + CHR(13)+CHR(10) + STR0065,{"Ok"}) //Formato Invalido  
	Endif 
Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³ SRZTotAux³ Autor ³ Kelly Soares          ³ Data ³ 27.04.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Adiciona totais de provisao no SRZ.         			 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ SRZTotAux(aTot)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	 	 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function SRZTotAux(aTot)

Local nCont		:= 1

aSort(aTot,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3] })

While nCont <= Len(aTot)            
	If aTot[nCont,4] # 0
		RecLock( "SRZ", .T. )
		SRZ->RZ_FILIAL  := aTot[nCont,1]
		SRZ->RZ_CC      := aTot[nCont,2]
		SRZ->RZ_PD      := aTot[nCont,3]
		SRZ->RZ_HRS     := 0
		SRZ->RZ_VAL     := aTot[nCont,4]
		SRZ->RZ_OCORREN := 0
		SRZ->RZ_MAT     := 'zzzzzz'
		SRZ->RZ_TIPO    := aTot[nCont,5]
		SRZ->RZ_TPC     := "1"  
		If lItemClVl
			SRZ->RZ_ITEM	:= aTot[nCont,6]
			SRZ->RZ_CLVL	:= aTot[nCont,7]
		Endif
		SRZ->( MsUnlock() )      
	Endif
	nCont++
Enddo

Return nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ReabSRZ   ºAutor  ³Ricardo Berti       º Data ³  01/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Reabre compart SRZ                                          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReabSRZ()

Local aArea
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Reabre SRZ padrao											   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
#IFDEF TOP
	 aArea		:= GetArea()
	If ( Select("SRZ") > 0.00 )
		SRZ->( dbCloseArea() )
		ChkFile( "SRZ" )
	EndIf
	RestArea( aArea )
#ENDIF
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp110IntLogixºAutor  ³Leandro Drumond  º Data ³  23/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Integra CT2 com LOGIX.                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Gp110IntLogix(cCompetencia)
Local oDlgMain
Local cDirPesq	:= SuperGetMv("MV_PASCON",,"")
Local lRet		:= .T.
Local nOpca		:= 0
Local oDirPesq
Local cHist		:= "000"
Local oHist


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para Dimensionar Tela		                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

Local bSet15	:= {|| lRet := Gp110Monta(cDirPesq,cCompetencia,cHist),If(lRet,oDlgMain:End(),)}
Local bSet24	:= {|| oDlgMain:End()}

Private oLbx
Private aLbx		:= {}
Private aEmpresa	:= {} 
Private aDadosEmp	:= {}
Private aDadosCarg	:= {}
Private aCargosInf	:= {}
Private aFuncao		:= {}
Private cFile		:= ""

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )


If lErpLogix
	DEFINE MSDIALOG oDlgMain TITLE OemToAnsi(STR0066) FROM  0,0 TO aAdvSize[1]+420,aAdvSize[1]+550 OF oMainWnd PIXEL //"Integração Contábil - Protheus GPE X Sistema Externo"

		@ 003, 003 GROUP oGroup1 TO aObjSize[1][1] + 190, aObjSize[1][3]+4 OF oDlgMain COLOR 0, 16777215 PIXEL
	
		@ aObjSize[1][1] + 10 , aObjSize[1][1]+5 SAY OemToAnsi(STR0067)	  	PIXEL //"Serão exportados através de arquivo texto as informações contabilizadas no GPE (Lote 008890)"
		@ aObjSize[1][1] + 20 , aObjSize[1][1]+5 SAY OemToAnsi(STR0068)		PIXEL //"da Tabela CT2 com as seguintes informações separadas por pipe (|)"
		@ aObjSize[1][1] + 30 , aObjSize[1][1]+5 SAY OemToAnsi(STR0069)		PIXEL //" - Empresa Sistema Externo"
		@ aObjSize[1][1] + 40 , aObjSize[1][1]+5 SAY OemToAnsi(STR0070)		PIXEL //" - Número do Lote"
		@ aObjSize[1][1] + 50 , aObjSize[1][1]+5 SAY OemToAnsi(STR0071)		PIXEL //" - Código do sistema (fixo GPE)"
		@ aObjSize[1][1] + 60 , aObjSize[1][1]+5 SAY OemToAnsi(STR0072)		PIXEL //" - Data de referência"
		@ aObjSize[1][1] + 70 , aObjSize[1][1]+5 SAY OemToAnsi(STR0073)		PIXEL //" - Número da Conta"
		@ aObjSize[1][1] + 80 , aObjSize[1][1]+5 SAY OemToAnsi(STR0074)		PIXEL //" - Tipo de Lançamento"
		@ aObjSize[1][1] + 90 , aObjSize[1][1]+5 SAY OemToAnsi(STR0075)		PIXEL //" - Data de Movimento"
		@ aObjSize[1][1] + 100, aObjSize[1][1]+5 SAY OemToAnsi(STR0076)		PIXEL //" - Valor do Lançamento"
		@ aObjSize[1][1] + 110, aObjSize[1][1]+5 SAY OemToAnsi(STR0077)		PIXEL //" - Código do Rateio (fixo 0)"
		@ aObjSize[1][1] + 120, aObjSize[1][1]+5 SAY OemToAnsi(STR0079)		PIXEL //" - Texto Complementar"
		@ aObjSize[1][1] + 130, aObjSize[1][1]+5 SAY OemToAnsi(STR0080)		PIXEL //" - Situação do Lançamento (fixo N)"
		@ aObjSize[1][1] + 140, aObjSize[1][1]+5 SAY OemToAnsi(STR0081)		PIXEL //" - Item Contábil" 
		
		@ aObjSize[1][1] + 160, aObjSize[1][1]+10	SAY OemToAnsi(STR0078)		PIXEL 	//"Código do Histórico Padrão: "
		@ aObjSize[1][1] + 160, aObjSize[1][1]+80	MSGET oHist VAR cHist		PICTURE "999" PIXEL SIZE 18,5
		@ aObjSize[1][1] + 175, aObjSize[1][1]+10	SAY OemToAnsi(STR0082)		PIXEL 	//"Destino: "
		@ aObjSize[1][1] + 175, aObjSize[1][1]+34	MSGET oDirPesq VAR cDirPesq	PICTURE "@!" PIXEL SIZE 100,7
		@ (aObjSize[1][1] + 172)*2, (aObjSize[1][1]+129)*2 	BTNBMP oBtn1 NAME "S4WB058N" SIZE 72,30 ACTION Gp110Dir(@cDirPesq)	 OF oDlgMain PIXEL

Else

	DEFINE MSDIALOG oDlgMain TITLE OemToAnsi(STR0066) FROM  0,0 TO aAdvSize[1]+470,aAdvSize[1]+550 OF oMainWnd PIXEL //"Integração Contábil - Protheus GPE X Sistema Externo"

		@ 003, 003 GROUP oGroup1 TO aObjSize[1][1] + 220, aObjSize[1][3]+4 OF oDlgMain COLOR 0, 16777215 PIXEL
	
		@ aObjSize[1][1] + 10 , aObjSize[1][1]+5 SAY OemToAnsi(STR0067)	  	PIXEL //"Serão exportados através de arquivo texto as informações contabilizadas no GPE (Lote 008890)"
		@ aObjSize[1][1] + 20 , aObjSize[1][1]+5 SAY OemToAnsi(STR0068)		PIXEL //"da Tabela CT2 com as seguintes informações separadas por pipe (|)"
	
		@ aObjSize[1][1] + 30 , aObjSize[1][1]+5 SAY OemToAnsi(STR0089)		PIXEL //" - Empresa Protheus"
		@ aObjSize[1][1] + 40 , aObjSize[1][1]+5 SAY OemToAnsi(STR0090)		PIXEL //" - Filial Protheus"		
		@ aObjSize[1][1] + 50 , aObjSize[1][1]+5 SAY OemToAnsi(STR0070)		PIXEL //" - Número do Lote"
		@ aObjSize[1][1] + 60 , aObjSize[1][1]+5 SAY OemToAnsi(STR0071)		PIXEL //" - Código do sistema (fixo GPE)"
		@ aObjSize[1][1] + 70 , aObjSize[1][1]+5 SAY OemToAnsi(STR0072)		PIXEL //" - Data de referência"
		@ aObjSize[1][1] + 80 , aObjSize[1][1]+5 SAY OemToAnsi(STR0073)+OemToAnsi(STR0091)		PIXEL //" - Número da Conta"#" Debito, caso houver"
		@ aObjSize[1][1] + 90 , aObjSize[1][1]+5 SAY OemToAnsi(STR0073)+OemToAnsi(STR0092)		PIXEL //" - Número da Conta"#" Credito, caso houver"
		@ aObjSize[1][1] + 100, aObjSize[1][1]+5 SAY OemToAnsi(STR0074)		PIXEL //" - Tipo de Lançamento"
		@ aObjSize[1][1] + 110, aObjSize[1][1]+5 SAY OemToAnsi(STR0075)		PIXEL //" - Data de Movimento"
		@ aObjSize[1][1] + 120, aObjSize[1][1]+5 SAY OemToAnsi(STR0076)		PIXEL //" - Valor do Lançamento"
		@ aObjSize[1][1] + 130, aObjSize[1][1]+5 SAY OemToAnsi(STR0077)		PIXEL //" - Código do Rateio (fixo 0)"
		@ aObjSize[1][1] + 140, aObjSize[1][1]+5 SAY OemToAnsi(STR0079)		PIXEL //" - Texto Complementar"
		@ aObjSize[1][1] + 150, aObjSize[1][1]+5 SAY OemToAnsi(STR0080)		PIXEL //" - Situação do Lançamento (fixo N)"
		@ aObjSize[1][1] + 160, aObjSize[1][1]+5 SAY OemToAnsi(STR0081)+OemToAnsi(STR0091)		PIXEL //" - Item Contábil"#" Debito, caso houver"
		@ aObjSize[1][1] + 170, aObjSize[1][1]+5 SAY OemToAnsi(STR0081)+OemToAnsi(STR0092)		PIXEL //" - Item Contábil"#" Credito, caso houver" 
		
		@ aObjSize[1][1] + 190, aObjSize[1][1]+10	SAY OemToAnsi(STR0078)		PIXEL 	//"Código do Histórico Padrão: "
		@ aObjSize[1][1] + 190, aObjSize[1][1]+80	MSGET oHist VAR cHist		PICTURE "999" PIXEL SIZE 18,5
		@ aObjSize[1][1] + 205, aObjSize[1][1]+10	SAY OemToAnsi(STR0082)		PIXEL 	//"Destino: "
		@ aObjSize[1][1] + 205, aObjSize[1][1]+30	MSGET oDirPesq VAR cDirPesq	PICTURE "@!" PIXEL SIZE 100,7
		@ (aObjSize[1][1] + 202)*2, (aObjSize[1][1]+125)*2 	BTNBMP oBtn1 NAME "S4WB058N" SIZE 72,30 ACTION Gp110Dir(@cDirPesq)	 OF oDlgMain PIXEL	
			
EndIf	

ACTIVATE MSDIALOG oDlgMain ON INIT EnchoiceBar( oDlgMain , bSet15 , bSet24 ) CENTERED

PutMv("MV_PASCON",cDirPesq)	

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp110Dir     ºAutor  ³Leandro Drumond  º Data ³  23/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o diretorio selecionado pelo usuario.               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Gp110Dir(cDirPesq)

_oWnd := GetWndDefault()

cDirPesq:=cGetFile(OemToAnsi(STR0083),OemToAnsi(STR0084),0,,.F.,GETF_LOCALHARD + GETF_RETDIRECTORY)

If _oWnd != Nil
	GetdRefresh()
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp110Monta   ºAutor  ³Leandro Drumond  º Data ³  23/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria arquivo para integracao com LOGIX.                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Gp110Monta(cDirPesq,cCompetencia,cHist)
Local aArea 	:= GetArea()
Local aStruCT2 	:= CT2->(dbStruct())
Local aItemAux	:= {}
Local aContaD	:= {}
Local aContaC	:= {}
Local cQuery	:= ""
Local cWhere	:= ""
Local cFilAux	:= ""
Local cFilLog	:= ""
Local cFilLogA	:= ""
Local cItemD	:= ""
Local cItemC	:= ""
Local cItemAux	:= ""
Local cArquivo 	:= ""
Local cMsg		:= ""
Local cContaD	:= ""
Local cContaC	:= ""
Local cAliasTrb := GetNextAlias()
Local dDataIni  := CtoD("01/"+SubStr(cCompetencia,1,2)+"/"+SubStr(cCompetencia,3,4))
Local dDataFim  := CtoD(StrZero(f_UltDia(dDataIni),2)+"/"+SubStr(cCompetencia,1,2)+"/"+SubStr(cCompetencia,3,4))
Local nArq		:= 0
Local nX		:= 0
Local cLoteCT2	:= ""

If Empty(cDirPesq)
	MsgInfo( STR0088, STR0044 ) // Atencao#"Diretório de destino não especificado"
	Return .F.
EndIf

cLoteCT2 := padl(allTrim(cLote), tamsx3("CT2_LOTE")[1], "0")

cWhere := "		CT2_LOTE = '"+cLoteCT2+"' AND "
cWhere += "		CT2_DATA >= '" + DtoS(dDataIni) + "' AND CT2_DATA <= '" + Dtos(dDataFim) + "' AND "
cWhere += "		D_E_L_E_T_=' ' "

cQuery := "SELECT * FROM " + InitSqlName( "CT2" ) + " WHERE "
cQuery += cWhere
cQuery += " AND CT2_SEQUEN = ( SELECT MAX(CT2_SEQUEN) FROM " + InitSqlName("CT2") + " WHERE " + cWhere + " ) "
cQuery += "		ORDER BY CT2_FILIAL, CT2_DATA "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTrb)

For nX := 1 To Len(aStruCT2)
	If ( aStruCT2[nX][2] <> "C" )
		TcSetField(cAliasTrb,aStruCT2[nX][1],aStruCT2[nX][2],aStruCT2[nX][3],aStruCT2[nX][4])
	EndIf
Next nX

dbSelectArea(cAliasTrb)

If (cAliasTrb)->( !Eof() )
	While (cAliasTrb)->( !Eof() )
		If !cFilAux == (cAliasTrb)->CT2_FILIAL
			cFilAux := (cAliasTrb)->CT2_FILIAL
			cFilLogA := Gp110GetFil(cEmpAnt,cFilAux)
			If !(cFilLogA == cFilLog)
				If !Empty(cFilLog)
					FClose(nArq)
				EndIf
				cFilLog := cFilLogA
				cArquivo:= cDirPesq + AllTrim(STRTRAN(cFilLog,"|","")) + "GPE" + AllTrim((cAliasTrb)->CT2_SEQUEN) + ".CON"
				nArq := MSFCREATE(cArquivo, 0)
				If Ferror() # 0 .And. nArq = -1 
					cMsg := OemToAnsi(STR0085) + STR(Ferror(),3) //-- "Erro de Gravacao do Arquivo - Codigo DOS: "
					MsgInfo( cMsg, STR0044 ) // Atencao
					Return(.F.)
				EndIf				
			EndIf
			If Empty(cFilLog)
				MsgInfo(  OemToAnsi(STR0086),STR0042 ) //Aviso#Não há DE/PARA de códigos entre Empresa/Filial Protheus X Empresa Externa (APCFG050) no Configurador.
				Exit
			EndIf
		EndIf
		If lErpLogix
			If (cAliasTrb)->CT2_DC $ "1|3"
				If Empty((cAliasTrb)->CT2_ITEMD)
					cItemD	:= "0|0|0|0
				Else
					cItemAux := If(Len((cAliasTrb)->CT2_ITEMD) < 8,(cAliasTrb)->CT2_ITEMD + Space(8-(Len((cAliasTrb)->CT2_ITEMD))),(cAliasTrb)->CT2_ITEMD)
					aItemAux := {If(Empty(SubStr(cItemAux,1,2)),"0",AllTrim(SubStr(cItemAux,1,2))),If(Empty(SubStr(cItemAux,3,2)),"0",AllTrim(SubStr(cItemAux,3,2))),If(Empty(SubStr(cItemAux,5,2)),"0",AllTrim(SubStr(cItemAux,5,2))),If(Empty(SubStr(cItemAux,7,2)),"0",AllTrim(SubStr(cItemAux,7,2)))}
					cItemD   := aItemAux[1] + "|" + aItemAux[2] + "|" + aItemAux[3] + "|" + aItemAux[4]
				EndIf
				cContaD := CFGA070Ext( "LOGIX", 'CT1', 'CT1_CONTA', cEmpAnt + "|" + xFilial("CT1") + "|" + (cAliasTrb)->CT2_DEBITO)
				If Empty(cContaD)
					cContaD := (cAliasTrb)->CT2_DEBITO
				Else
					aContaD := Separa(cContaD,"|")
					If Len(aContaD) > 1
						cContaD := aContaD[2]
					Else
						cContaD := aContaD[1]
					EndIf
				EndIf
				cLinha := AllTrim(cFilLog) + "|" + AllTrim((cAliasTrb)->CT2_SEQUEN) + "|" + "GPE" + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + AllTrim(cContaD) + "|" + "D" + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + CVALTOCHAR((cAliasTrb)->CT2_VALOR) + "|" + "0" + "|" + cHist + "|" + AllTrim((cAliasTrb)->CT2_HIST) + "|" + "N" + "|" + cItemD + CRLF
				Fwrite( nArq, cLinha )
			EndIf
			If (cAliasTrb)->CT2_DC $ "2|3"
				If Empty((cAliasTrb)->CT2_ITEMC)
					cItemC	:= "0|0|0|0
				Else
					cItemAux := If(Len((cAliasTrb)->CT2_ITEMC) < 8,(cAliasTrb)->CT2_ITEMC + Space(8-(Len((cAliasTrb)->CT2_ITEMC))),(cAliasTrb)->CT2_ITEMC)
					aItemAux := {If(Empty(SubStr(cItemAux,1,2)),"0",AllTrim(SubStr(cItemAux,1,2))),If(Empty(SubStr(cItemAux,3,2)),"0",AllTrim(SubStr(cItemAux,3,2))),If(Empty(SubStr(cItemAux,5,2)),"0",AllTrim(SubStr(cItemAux,5,2))),If(Empty(SubStr(cItemAux,7,2)),"0",AllTrim(SubStr(cItemAux,7,2)))}
					cItemC   := aItemAux[1] + "|" + aItemAux[2] + "|" + aItemAux[3] + "|" + aItemAux[4]
				EndIf
				cContaC := CFGA070Ext( "LOGIX", 'CT1', 'CT1_CONTA', cEmpAnt + "|" + xFilial("CT1") + "|" + (cAliasTrb)->CT2_CREDITO)
				If Empty(cContaC)
					cContaC := (cAliasTrb)->CT2_CREDITO
				Else
					aContaC := Separa(cContaC,"|")
					If Len(aContaC) > 1
						cContaC := aContaC[2]
					Else
						cContaC := aContaC[1]
					EndIf
				EndIf
				cLinha := AllTrim(cFilLog) + "|" + AllTrim((cAliasTrb)->CT2_SEQUEN) + "|" + "GPE" + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + AllTrim(cContaC) + "|" + "C" + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + CVALTOCHAR((cAliasTrb)->CT2_VALOR) + "|" + "0" + "|" + cHist + "|" + AllTrim((cAliasTrb)->CT2_HIST) + "|" + "N" + "|" + cItemC + CRLF
				Fwrite( nArq, cLinha )
			EndIf
		Else
			cLinha := AllTrim(cFilLog) + "|" + AllTrim((cAliasTrb)->CT2_SEQUEN) + "|" + "GPE" + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + AllTrim((cAliasTrb)->CT2_DEBITO) + "|" + AllTrim((cAliasTrb)->CT2_CREDITO) + "|" + AllTrim((cAliasTrb)->CT2_DC) + "|" + DtoC((cAliasTrb)->CT2_DATA) + "|" + CVALTOCHAR((cAliasTrb)->CT2_VALOR) + "|" + "0" + "|" + cHist + "|" + AllTrim((cAliasTrb)->CT2_HIST) + "|" + "N" + "|" + AllTrim((cAliasTrb)->CT2_ITEMD) + "|" + AllTrim((cAliasTrb)->CT2_ITEMC) + CRLF
			Fwrite( nArq, cLinha )
		EndIf

		dbSelectArea(cAliasTrb)
		dbSkip()
	EndDo
	If !(nArq == 0)
		FClose(nArq)
	EndIf
Else
	MsgInfo(  OemToAnsi(STR0087), STR0042 ) //Aviso#Não existem informaç~eos para serem contabilizadas no Sistema Externo.
EndIf

RestArea(aArea)

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp110GetFil  ºAutor  ³Leandro Drumond  º Data ³  23/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carregar informacoes do DE/PARA de empresa/filial LOGIX.    º±±
±±º          ³Frame nao dispoe de nenhuma funcao para tal                 º±±
±±º          ³FWEAIEMPFIL, que nao possui documentacao, eh usada apenas p/º±±
±±º          ³obter a empresa filial protheus.                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Gp110GetFil(cEmpPrt,cFilPrt)
Local aArea 	:= GetArea()
Local cFilRet   := ""

If lErpLogix
	DbSelectArea('XXD')
	DbSetOrder(2)
	
	If DbSeek(RhTamFilial(xFilial('XXD'))+cEmpPrt+RhTamFilial(cFilPrt))
		cFilRet := XXD_COMPA
	ElseIf DbSeek(cEmpPrt+RhTamFilial(cFilPrt))
		cFilRet := XXD_COMPA
	EndIf
Else
	cFilRet := cEmpPrt + "|" + cFilPrt
EndIf

RestArea(aArea)

Return cFilRet

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³ fValSIX  ³ Autor ³ Claudinei Soares   	  ³ Data ³12/07/2013³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Funcao para validar atualizacao de incides p/ desoneracao  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³ fValSIX()													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ GPEXFUN 												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fValSIX()

Local lAchou := .F.               

If SIX->(dbSeek( "SRZ"+"5") )
	lAchou := .T. 
EndIf

Return(lAchou)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AjustaSx6 ºAutor  ³Leandro Drumond     º Data ³  08/11/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria parametor MV_ERPLOGI                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSx6()

Local aArea		:= GetArea()
Local lGrava	:= .F.

dbSelectArea("SX6")
dbSetOrder(1)

If !( dbSeek( Space(FWGETTAMFILIAL) + "MV_ERPLOGI" ) )
	RecLock("SX6",.T.)
	lGrava := .T.
Else
	If X6_TIPO <> "C"
		RecLock("SX6",.F.)
		lGrava := .T.
	EndIf
EndIf

If lGrava
	Replace X6_FIL    	With Space(FWGETTAMFILIAL)
	Replace X6_VAR    	With "MV_ERPLOGI"
	Replace X6_TIPO   	With "C"
	Replace X6_CONTEUD  With "2"
	Replace X6_CONTENG  With "2"
	Replace X6_CONTSPA  With "2"
	Replace X6_DESCRIC  With "Indica se utiliza integracao"
	Replace X6_DESC1    With "com LOGIX."
	
	MsUnlock()
EndIf

RestArea( aArea )

Return( NIL )